//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Loja de Alquimista
//===== Por: ==================================================
//= rAthena Dev Team
//===== Versão Atual: =====================================
//= 1.9
//===== Compatível Com: ===================================
//= Cronus
//===== Descrição: =========================================
//= Comercio de Materiais de alquimista e Manuais.
//===== Comentários Adicionais: =================================
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//= 1.9 Aplicada Tradução (PT-BR). [SkyHD]
//============================================================

alde_alche,24,188,3	script	Comerciante da Guilda	2_M_ALCHE,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, volte novamente -";
		mes "- apos deixar alguns itens no Armazém da Kafra. -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Bem-vindo à";
	mes "União dos Alquimistas.";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Comprar materiais.:Comprar manual de produção.:Cancelar.")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "O que você gostaria de comprar?";
		next;
		if (select("Vasilha de Mistura - 8 Zeny:Cancelar.") == 1) {
			mes "[Gever Al Sharp]";
			mes "Quantas você deseja?";
			mes "Digite '0' se você deseja cancelar.";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "A operação foi cancelada.";
					mes "Volte sempre.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "O número deve";
					mes "ser inferior a 2000.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "Vejo que você não";
				mes "tem a quantidade de zeny";
				mes "suficiente. Verifique";
				mes "quanto você tem de zeny primeiro.";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "Me parece que você não";
				mes "pode carregar tudo.";
				mes "Verifique o espaço";
				mes "em seu iventário.";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "Obrigado.";
			mes "Volte sempre.";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "Pois bem,";
		mes "volte novamente caso";
		mes "necessite de mais";
		mes "materiais relacionados a";
		mes "Alquimia, certo?";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "Do que você precisa?";
		mes "Os manuais geralmente custam 100,000 zeny. Mas há vários manuais especiais que irão custar muito mais.";
		next;
		switch(select("Manual de Criação de Poções:Manual de Criação de Álcool:Manual de Criação de Fogo Grego:Manual de Criação de Pote de Ácido:Manual de Criação de Plantas:Manual de Criação de Esfera Marinha:Manual de Criação de Revestimentos:Manual de Criação de Poções Condensadas:Cancelar")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Pois bem...";
			mes "Volte caso você tenha";
			mes "nescessidade de comprar";
			mes "um manual de produção.";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Certo, então";
		mes "tenha um bom dia.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[Gever Al Sharp]";
		mes "Vejo que você não";
		mes "tem zeny suficiente.";
		mes "Verifique quanto você";
		mes "tem de zeny primeiro.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes ""+getitemname(getarg(0))+"?";
	mes "Isso vai custar "+getarg(1)+" zeny.";
	next;
	if (select("Comprar:Cancelar") == 1) {
		set Zeny, Zeny-getarg(1);
		getitem getarg(0),1;
		mes "[Gever Al Sharp]";
		mes "Obrigado pela";
		mes "sua fidelidade.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Volte novamente";
	mes "outra hora.";
	close;
}
