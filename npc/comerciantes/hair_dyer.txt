//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Tintura de cabelo
//===== Por: ==================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: =====================================
//= 1.5
//===== Compatível Com: ===================================
//= Cronus
//===== Descrição: =========================================
//= Tintura de cabelo padrão
//===== Comentários Adicionais: =================================
//= Fully working
//= 1.1 Bugfix: fixed missing menu label LEnd->M_End [Lupus]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.3 Fixed problem what "freezes" the NPC. (bugreport:509) [Samuray22]
//= 1.4 Fixed getlook checking the wrong color. (bugreport:2392) [L0ne_W0lf]
//= 1.5 Added Lighthalzen Hair Dying NPC. [L0ne_W0lf]
//= 1.6  Aplicada Tradução(PT_BR). [SkyHD]
//============================================================

prt_in,243,168,4	script	Jovovich	4_F_02,{
	mes "[Cabeleireiro Jovovich]";
	mes "Bem Vindo~!";
	mes "Como posso ajudá-lo?";
	next;
	mes "[Cabeleireiro Jovovich]";
	if (Sex) mes "Oh, não! O seu cabelo está danificado. Parece que você vai precisar de um tratamento profissional. Sente-se aqui, por favor.";
	else {
		if (rand(20) > 11) {
			mes "Eh!? Oh meu Deus! Oh, não, não, não, não! Seu cabelo está muuuito danificado! Não é bom se você deixar seu cabelo assim.";
			next;
			mes "[Cabeleireiro Jovovich]";
			mes "Você deixa eu dar uma olhada no seu cabelo, por favor?";
		}
		else mes "Uau! Seu cabelo ficaria perfeito se você tingisse. Que tal tingir o seu cabelo para uma mudança?";
	}
	next;
	while(1) {
		switch(select("Tingir o Cabelo:Dicas e Informações:Cancelar")) {
		case 1:
			mes "[Cabeleireiro Jovovich]";
			mes "Sim, sim, uma boa escolha~";
			mes "Pois bem, essa é a sua chance para um mudança, a sua chance de florescer em beleza! Escolha a cor que você gostaria.";
			next;
			while(1) {
				if (.@choose_success == 1) {
					mes "[Cabeleireiro Jovovich]";
					mes "O que você acha? Você deseja uma cor diferente?";
					next;
					if (select("Sim:Não") == 1) {
						mes "[Cabeleireiro Jovovich]";
						mes "Ok! Escolha a cor que você gostaria.";
						next;
					}
					else  {
						mes "[Cabeleireiro Jovovich]";
						mes "Hmm, estou decepicionado, eu queria ter feito um trabalho melhor. Mas eu prometo que que farei melhor da próxima vez. Volte sempre~";
						close;
					}
				}

				while(1) {
					switch(select("Vermelho, por favor.:Amarelo, por favor.:Roxo, por favor.:Laranja, por favor.:Verde, por favor.:Azul, por favor.:Branco, por favor.:Preto, por favor.:Na verdade, prefiro meu cabelo como ele é.")) {
					case 1: set .@headpalette,8; break;
					case 2: set .@headpalette,1; break;
					case 3: set .@headpalette,2; break;
					case 4: set .@headpalette,3; break;
					case 5: set .@headpalette,4; break;
					case 6: set .@headpalette,5; break;
					case 7: set .@headpalette,6; break;
					case 8: set .@headpalette,7; break;
					case 9:
						if (.@choose_success != 0) {
							mes "[Cabeleireiro Jovovich]";
							mes "Você deve gostar de sua cor de cabelo~";
							close;
						}
						mes "[Cabeleireiro Jovovich]";
						mes "Eehh~? Você não irá tingir o seu cabelo? Estou triste...";
						close;
					}

					if (.@headpalette == getlook(VAR_HEADPALETTE)) {
						mes "[Cabeleireiro Jovovich]";
						mes "Eh? Mas essa é a cor do cabelo que você já tem. Por favor, escolha uma cor diferente.";
						next;
					}
					else {
						switch(.@headpalette) {
						case 1: callsub S_NoDye,976,"amarelo"; break;
						case 2: callsub S_NoDye,978,"roxo"; break;
						case 3: callsub S_NoDye,980,"laraja"; break;
						case 4: callsub S_NoDye,979,"verde"; break;
						case 5: callsub S_NoDye,981,"azul"; break;
						case 6: callsub S_NoDye,982,"branco"; break;
						case 7: callsub S_NoDye,983,"preto"; break;
						case 8: callsub S_NoDye,975,"vermelho"; break;
						}
						if (Zeny < 1000) {
							mes "[Cabeleireiro Jovovich]";
							mes "A taxa é de 1000 zeny. Você não tem o suficiente...?";
							close;
						}

						switch(.@headpalette) {
						case 1: delitem 976,1; break; //corante_limão
						case 2: delitem 978,1; break; //corante_roxo
						case 3: delitem 979,1; break; //corante_laranja
						case 4: delitem 980,1; break; //corante_verdeescuro
						case 5: delitem 981,1; break; //corante_azul
						case 6: delitem 982,1; break; //corante_branco
						case 7: delitem 983,1; break; //corante_preto
						case 8: delitem 975,1; break; //corante_escarlatina
						}
						set Zeny, Zeny-1000;
						setlook VAR_HEADPALETTE,.@headpalette;
						set .@choose_success,1;
						break;
					}

				}

			}

		case 2:
			mes "[Cabeleireiro Jovovich]";
			mes "Quando você está se sentindo para baixo, quando você levar um fora, quando você quer impressionar alguém, ou mesmo quando você simplesmente quer se destacar...";
			next;
			mes "[Cabeleireiro Jovovich]";
			mes "Para aquele lugar especial e no tempo, que você quer ter um penteado próprio? Contanto que você tenha os corantes apropriados, vou fazer o seu cabelo ficar maravilhoso.";
			next;
			mes "[Cabeleireiro Jovovich]";
			mes "E tente não se preocupar muito com a taxa. Aquisição de beleza é o mesmo que adquirir tudo. Hehe~";
			next;
			mes "[Cabeleireiro Jovovich]";
			mes "* Taxas e Informações *";
			mes "- 1 Tintura da cor do cabelo que você quer.";
			mes "- 1000 taxa de zeny.";
			break;

		case 3:
			mes "[Cabeleireiro Jovovich]";
			mes "Homens ou Mulheres...";
			mes "Toda pessoa tem o direito e a obrigação de ser bonito.";
			close;
		}

	}

S_NoDye:
	if (countitem(getarg(0)) == 0) {
		mes "[Cabeleireiro Jovovich]";
		mes "Eh! Mas você precisa de uma '"+getitemname(getarg(0))+"' para Tingir o cabelo "+getarg(1)+"...";
		close;
	}
	return;
}

// Lighthalzen
lhz_in02,100,134,3	script	Tintureira#lich	4_F_EINWOMAN,{
	mes "[Rossa]";
	if (Sex == 1) {
		mes "Bem-vindo, venha!"; 
		mes "Oh, Eu vejo que você deve tomar"; 
		mes "muito cuidado com seu"; 
		mes "cabelo do que os outros meninos."; 
		mes "Agora você gostaria de tingir"; 
		mes "o cabelo de outra cor?";

	}
	else {
		mes "Oh, Wow"; 
		mes "Onde Você conseguiu este estilo de cabelo?"; 
		mes "Eu adoro isso, mas ... Ficaria"; 
		mes "ainda mais bonito se"; 
		mes "você tingisse o seu cabelo."; 
		mes "O que você acha disso?";
	}
	while (.@choose_success != 2) {
		next;
		switch(select("Tingir Cabelo:Dicas e Informações:Cancelar")) {
		case 1:
			mes "[Rossa]";
			mes "Ho ho ho ho!"; 
			mes "Então, qual cor você"; 
			mes "gostaria de tingir?"; 
			mes "Algo Vivo ou Escuro? Sexy ou Bonito?";
			next;
			while (.@choose_success != 2) {
				if (.@choose_success == 1) {
					mes "[Rossa]";
					mes "Ooh, eu gosto dessa cor!"; 
					mes "Mas você gostaria de"; 
					mes "tentar uma diferente?";
					next;
					switch(select("Sim:Não")) {
					case 1:
						mes "[Rossa]";
						mes "Por favor, selecione"; 
						mes "outra cor.";
						next;
						break;
					case 2:
						mes "[Rossa]";
						mes "Uma excelente escolha!"; 
						mes "Tudo bem então, obrigado"; 
						mes "por usar o meu serviço e"; 
						mes "Espero que você passe por aqui de novo!";
						close2;
						set .@choose_success,2;
						break;
					}
				}
				while (.@choose_success != 2) {
					switch(select("Vermelho, por favor.:Amarelo, por favor.:Roxo, por favor.:Laranja, por favor.:verde, por favor.:Azul, por favor.:Branco, por favor.:Preto, por favor.: Na verdade, eu gosto do meu cabelo como ele é.")) {
					case 1:
						set .@headpalette,8;
						break;
					case 2:
						set .@headpalette,1;
						break;
					case 3:
						set .@headpalette,2;
						break;
					case 4:
						set .@headpalette,3;
						break;
					case 5:
						set .@headpalette,4;
						break;
					case 6:
						set .@headpalette,5;
						break;
					case 7:
						set .@headpalette,6;
						break;
					case 8:
						set .@headpalette,7;
						break;
					case 9:
						if (.@choose_success != 0) {
							mes "[Rossa]";
							mes "Tem certeza?"; 
							mes "Tudo bem então, você"; 
							mes "sabe o que é melhor para"; 
							mes "a sua beleza e vou lhe dizer"; 
							mes "a verdade, eu concordo com você";
							close2;
							set .@choose_success,2;
						}
						else {
							mes "[Rossa]";
							mes "Oh, eu vejo. Ainda assim, eu não posso"; 
							mes "ajudar, me sinto tão desapontado."; 
							mes "Você ficaria tão bonito se você tingisse"; 
							mes "o seu cabelo de uma cor diferente";
							close2;
							set .@choose_success,2;
						}
						break;
					}
					if (getlook(VAR_HEADPALETTE) == .@headpalette) {
						mes "[Rossa]";
						mes "Hmm, sua cor de cabelo"; 
						mes "está boa, então não há"; 
						mes "necessidade de tingi-la da mesma"; 
						mes "cor de novo, se é isso com o que"; 
						mes "você está preocupado.";

						next;
					}
					else {
						if ((.@headpalette == 8) && (countitem(975) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Vermelha com você ...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 1) && (countitem(976) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Amarela com você ...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 2) && (countitem(981) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Violeta com você...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 3) && (countitem(980) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Laranja com você...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 4) && (countitem(979) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Verde."; 
							mes "Por que você não"; 
							mes "volta depois que conseguir um pouco?";

							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 5) && (countitem(978) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Azul com você...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 6) && (countitem(982) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Branca com você...";
							close2;
							set .@choose_success,2;
							break;
						}
						else if ((.@headpalette == 7) && (countitem(983) == 0)) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,"; 
							mes "mas eu não posso Tingir o"; 
							mes "seu cabelo, se você não me trazer"; 
							mes "Tintura Preta com você...";
							close2;
							set .@choose_success,2;
							break;
						}
						if (Zeny < 1000) {
							mes "[Rossa]";
							mes "Oh, Sinto muito querida,";
							mes "mas a minha taxa de serviço é";
							mes "1.000 zeny. Você se esqueceu";
							mes "de trazer o seu dinheiro com você?";
							close2;
							set .@choose_success,2;
							break;
						}
						if (.@headpalette == 8) delitem 975,1; //Scarlet_Dyestuffs
						else if (.@headpalette == 1) delitem 976,1; //Lemon_Dyestuffs
						else if (.@headpalette == 2) delitem 981,1; //Violet_Dyestuffs
						else if (.@headpalette == 3) delitem 980,1; //Orange_Dyestuffs
						else if (.@headpalette == 4) delitem 979,1; //Darkgreen_Dyestuffs
						else if (.@headpalette == 5) delitem 978,1; //Cobaltblue_Dyestuffs
						else if (.@headpalette == 6) delitem 982,1; //White_Dyestuffs
						else if (.@headpalette == 7) delitem 983,1; //Black_Dyestuffs
						set Zeny, Zeny-1000;
						setlook VAR_HEADPALETTE,.@headpalette;
						set .@choose_success,1;
						break;
					}
				}
			}
			break;
		case 2:
			mes "[Rossa]";
			mes "Quando você está se sentindo"; 
			mes "para baixo, quando você quer apenas"; 
			mes "uma boa aparência"; 
			mes "ou quando você quer apenas"; 
			mes "um olhar diferente."; 
			mes "Você quer tingir seu cabelo?";
			next;
			mes "[Rossa]";
			mes "Tudo que você precisa é de"; 
			mes "uma Tintura da"; 
			mes "cor que você deseja"; 
			mes "tingir seu cabelo, assim como"; 
			mes "uma taxa de serviço 1.000 zeny."; 
			mes "Estou aqui para as suas necessidades de beleza.";
			break;
		case 3:
			mes "[Rossa]";
			mes "Você sabe, quando você"; 
			mes "fizer alguma mudança em"; 
			mes "sua aparência, você vai"; 
			mes "não só vai parecer melhor, mas"; 
			mes "você vai se sentir melhor para"; 
			mes "sí mesmo. Faz sentido, certo?";
			close;
		}
	}
	close;
}
