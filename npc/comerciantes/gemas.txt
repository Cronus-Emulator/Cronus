//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Comercio de pedras preciosas
//===== Por: ==================================================
//= L0ne_W0lf
//===== Versão Atual: =====================================
//= 1.4
//===== Compatível com: =====================================
//= Cronus
//===== Descrição: =========================================
//= [Official Conversion]
//= Comércio de várias cores de pedras preciosas.
//===== Comentários Adicionais: =================================
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Any notes pertaining to the prior trader may be found
//=     in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=     Added a checkweight. 
//= 1.3 Fixed checks. [Euphy]
//= 1.4 Aplicado Tradução(PT_BR). [SkyHD]
//============================================================

payon,173,238,5	script	Jade#pay	4_M_SAGE_A,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FFEspere um momento! Creio que você esteja carregando muitos itens com você. Por favor, volte quando você tiver deixado alguns itens em seu Armazém.^000000";
		close;
	}
	mes "[Jade]";
	mes "Traga-me duas";
	mes "Gemas de mesma";
	mes "cor e eu vou";
	mes "mudá-las para gemas";
	mes "de cores diferentes.";
	next;
	switch(select("Gemas Azuis para Vermelhas!:Gemas Vermelhas para Amarelas!:Gemas Amarelas para Azuis!")) {
	case 1: callsub S_TradeGems,717,716,"Gemas Azuis","Gemas Vermelhas";
	case 2: callsub S_TradeGems,716,715,"Gemas Vermelhas","Gemas Amarelas";
	case 3: callsub S_TradeGems,715,717,"Gemas Amarelas","Gemas Azuis";
	}

S_TradeGems:
	if (countitem(getarg(0)) < 2) {
		mes "[Jade]";
		mes "Hah...!";
		mes "Você está brincando comigo, não é?";
		mes "Eu não posso prestar";
		mes "esse serviço a você se você não";
		mes "me der pelo menos";
		mes "2 " + getarg(2) + "!";
		close;
	}
	else {
		set .@gems,countitem(getarg(0))/2;
		mes "[Jade]";
		mes "Acho que posso criar";
		mes "um total de " + .@gems + " " + getarg(3);
		mes "usando as " + getarg(2);
		mes "que você tem.";
		mes "O que você quer fazer?";
		next;
		switch(select("Me dê todas que você puder:Eu quero escolher a quantidade:Cancelar")) {
		case 1:
			delitem getarg(0),.@gems * 2;
			getitem getarg(1),.@gems;
			mes "[Jade]";
			mes "Aqui está.";
			mes "Sinta-se à vontade para voltar";
			mes "a qualquer hora.";
			mes "Hum, o que está olhando?";
			mes "Há algo em meu rosto?";
			close;
		case 2:
			mes "[Jade]";
			mes "Então, quantas";
			mes "você quer?";
			mes "O máximo que";
			mes "você pode escolher é 100.";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[Jade]";
					mes "Nada?";
					mes "Acho que você";
					mes "mudou de idéia...";
					close;
				}
				else if (.@input > 100) {
					mes "[Jade]";
					mes "Errm...";
					mes "Falei para colocar";
					mes "um número menor";
					mes "que 100, lembra...?";
					next;
				}
				else {
					break;
				}
			}
			if (.@gems > .@input) {
				delitem getarg(0),.@input * 2;
				getitem getarg(1),.@input;
				mes "[Jade]";
				mes "Aqui está.";
				mes "Sinta-se à vontade para voltar";
				mes "a qualquer hora.";
				mes "Hum, o que está olhando?";
				mes "Há algo em meu rosto?";
			}
			close;
		case 3:
			mes "[Jade]";
			mes "Tudo bem, sem problemas.";
			mes "Volte sempre.";
			close;
		}
	}
}
