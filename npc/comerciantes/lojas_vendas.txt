//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Lojas de Vendas
//===== Por: ==================================================
//= Kisuka
//===== Versão Atual: =====================================
//= 1.2a
//===== Compatível com: =========================================
//= Cronus
//===== Descrição: =========================================
//= Compra de Licenças de lojas e Catálogos.
//===== Comentários Adicionais: =================================
//= 1.0 First version. [Kisuka]
//= 1.1 Added Catalog Magician. [Euphy]
//= 1.1a Moved Catalog Magician to Renewal path. [Euphy]
//= 1.2a Aplicada Tradução(PT_BR). [SkyHD]
//============================================================

// Black Marketeer (Buy Licenses - Non-Merchant Classes)
//============================================================
que_job01,68,84,1	script	Black Marketeer#Buying	4_M_SITDOWN,{
	if (getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[Mr. Jass]";
		mes "Ei, você já fez um contrato com Hugh.";
		mes "Eu não tenho nenhum negócio com você.";
		close;
	}
	mes "[Mr. Jass]";
	mes "Você deve precisar muito de alguma coisa para vir me procurar.";
	mes "O que você precisa?";
	next;
	switch(select("Lincença de Grande Comprador de Lojas:Quem é você?:Nada, nada!")) {
	case 1:
		mes "[Mr. Jass]";
		mes "Eu sabia!";
		mes "Claro, eu posso fazer isso por você.";
		mes "Mine apenas se parece com os autênticos comerciantes que começa a partir desse canalha Sr. Hugh!";
		next;
		mes "[Sr. Jass]";
		mes "E a minha licença é melhor 'porque você não precisa' aprender todas as habilidades.";
		mes "Quantas você quer?";
		next;
		mes "[Sr. Jass]";
		mes "Só para você saber, eu só posso fazê-las em pequenas quantidades, até 10 de uma vez.";
		mes "Vai custar 500 zeny para cada um.";
		next;
		while(1) {
			input .@input;
			mes "[Mr. Jass]";
			if (.@input == 0) {
				mes "Você não precisa dessas licenças?";
				close;
			} else if (.@input > 10) {
				mes "Eu só posso fazer até 10 de cada vez, você sabe.";
				next;
			} else {
				mes "It'll cost "+(.@input*500)+" zeny.";
				if (Zeny < (.@input*500)) {
					mes "mas você não tem dinheiro suficiente.";
					next;
					mes "[Sr. Jass]";
					mes "Você não sabe o básico de negócios? Tudo tem um preço.";
					mes "Se você quer algo, você tem que pagar por isso.";
				} else {
					mes "Ha... Ha ha ha!";
					mes "Sr. Hugh, Eu vou assumir a sua licença comercial. Você vai ver!";
					mes "*Risos Risos*";
					getitem 12548,.@input; //Shabby_Purchase_Street
					set Zeny,Zeny-(.@input*500);
				}
				close;
			}
		}
	case 2:
		mes "[Sr. Jass]";
		mes "Deixei minha cidade natal há muito tempo.";
		mes "Faz sentido perguntar quem eu sou, porque tudo o que eu tive deixado para trás até agora foi meu ódio";
		next;
		mes "[Sr. Jass]";
		mes "...";
		mes "Hugh é um comerciante corrupto sem nenhum senso de ética nos negócios.";
		mes "Meu único objetivo na vida é destruir Hugh.";
		next;
		mes "[Sr. Jass]";
		mes "Ah, eu bebi demais... (*Soluço*)";
		mes "Isso é só o álcool a falar.";
		mes "Por favor, esqueça tudo o que eu disse.";
		close;
		case 3:
		mes "[Sr. Jass]";
		mes "Certo, certo! Você não precisa gritar.";
		mes "Me deixe em paz se você não tem negócio comigo.";
		close;
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================
alberta_in,58,52,4	script	Purchasing Team#Buying	1_M_ORIENT01,{
	mes "[Mr. Hugh]";
	if (BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if (getskilllv("ALL_BUYING_STORE") == 1) {
			mes "Eu sou Hugh da Equipe de Compras.";
			mes "Como posso ajudá-lo hoje?";
			next;
			if (select("Comprar uma Licença:Sair") == 2) {
				mes "[Mr. Hugh]";
				mes "Por favor, não hesite em perguntar-me, se você precisar de alguma Licença para Loja de Compras em Massa fique a vontade para falar.";

				close;
			}
			mes "[Mr. Hugh]";
			mes "São 200 zeny para cada Licença para Loja de Compras em Massa, e você pode comprar até 50 por vez.";
			mes "Quantas licenças você precisa?";
			next;
			while(1) {
				input .@input;
				mes "[Mr. Hugh]";
				if (.@input == 0) {
					mes "Você cancelou o negócio.";
					mes "Tenha um bom dia.";
					close;
				} else if (.@input > 50) {
					mes "Por favor insira um valor menor que 50";
					next;
				} else {
					mes "It'll cost "+(.@input*200)+" zeny for "+.@input+" licenses.";
					if (Zeny < (.@input*200)) {
						mes "mas você parece não ter dinheiro suficiente.";
					} else {
						mes "Obrigado pelo seu patrocínio.";
						getitem 6377,.@input; //Buy_Stall_Permit
						set Zeny,Zeny-(.@input*200);
					}
					close;
				}
			}
		} else {
mes "Eu sou Hugh da Equipe de Compras do Clã dos Mercadores de Alberta.";
			mes "Você está...";
			next;
			mes "[Mr. Hugh]";
			mes "Você sabia? Nosso Clã tem emitido uma licença que permiti que os indivíduos possa comprar mercadorias de outros.";
			next;
			select("Eu nunca tive problemas comprando itens...");
			mes "[Mr. Hugh]";
			mes "Você está certo, mas pense comigo:";
			mes "Você não teve dificuldade de comprar em massa?";
			mes "Você tem que encontrar e conversar com todo mundo que tem um item que você deseja.";
			mes "Consideravelmente inconveniente, não é?";
			next;
			mes "[Mr. Hugh]";
			mes "Uma vez que a compra em massa é uma questão importante para nós, comerciantes,";
			mes "Eu tenho proposto um plano inovador para o nosso Clã, baseado em meus 10 anos de experiência em fazer compras.";
			next;
			mes "[Mr. Hugh]";
			mes "'Vamos Abrir Individualmente";
			mes "Uma Loja de Muitos Compradores!'";
			mes "Esse é o título da minha proposta.";
			mes "Você vai ver, quando você ler isso...";
			next;
			mes "[Mr. Hugh]";
			mes "...";
			mes "...(Sr. Hugh falou-me muito, nos mínimos detalhes.)";
			next;
			select("Certo, onde quer chegar?");
			mes "[Mr. Hugh]";
			mes "Ah, sim. Em resumo,";
			mes "você pode comprar alguns itens em massa através de Lojas.";
			next;
			mes "[Mr. Hugh]";
			mes "Para abrir uma Loja de Compra em Massa, você precisa de uma licença emitida pelo Clã Mercador.";
			next;
			mes "[Mr. Hugh]";
			mes "Você precisa dela, cada vez que for abrir uma loja. Nós estamos esperando um aumento significativo nos lucros através deste novo tipo de licenciamento.";
			next;
			mes "[Mr. Hugh]";
			mes "Esse é o ponto da minha proposta!";
			mes "Nosso presidente ficou muito feliz em saber que nós vamos fazer muito dinheiro!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Esse é o ponto da minha proposta!";
			next;
			mes "[Mr. Hugh]";
			mes "Não seja tão impaciente, certo?";
			mes "Concluíndo, podemos deixá-lo abrir uma Loja de Compra em Massa, isso se você já aprendeu a vender.";
			next;
			mes "[Mr. Hugh]";
			mes "Claro, nós cobramos 10,000 zeny uma única vez por uma taxa de inscrição.";
			mes "Você vai precisar da ^4A4AFFLicença para Loja de Compras em Massa^000000 cada vez que você abrir uma loja.";
			next;
			mes "[Mr. Hugh]";
			mes "Diga, gostaria de registrar agora?";
			mes "Se o fizer, eu vou te ensinar como abrir uma Loja de Compras em Massa";
			next;
			if (select("Quero abrir uma loja!:Cancelar") == 2) {
				mes "[Mr. Hugh]";
				mes "Cara, isso é decepcionante!";
				mes "Usando esta opção de compra em massa você pode beneficiar o seu negócio em muitas maneiras, mas a decisão é sua.";
				mes "Estou sempre aberto para consulta!";
				close;
			}
			mes "[Mr. Hugh]";
			if (Zeny < 10000) {
				mes "A taxa de inscrição é de um valor único de 10,000 zeny.";
				mes "Por favor, tenha o dinheiro em mãos.";
				close;
			} else {
				mes "Você fez uma boa decisão.";
				mes "Por favor me dê a taxa de inscrição e assine aqui mesmo....";
				next;
				input .@name$;
				mes "[Mr. Hugh]";
				mes .@name$+"....";
				mes "Eu gosto da sua escrita.";
				mes "Ok, você agora está aprovado de abrir uma Loja de Compras em Massa.";
				set Zeny,Zeny-10000;
				getitem 6377,5; //Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,3;
				next;
				mes "[Mr. Hugh]";
				mes "Atualmente, apenas itens normais ^8C2121EXCETO^000000 equipamentos, algumas poções e itens artesanais podem ser adquiridos em massa, mas esta ainda pode ser de muito benéfico para você, dependendo de como usa-la.";
				next;
				mes "[Mr. Hugh]";
				mes "Ah, e você precisa de pelo menos um dos itens que você deseja comprar em seu inventário porque você tem que mostrá-la a outros através de sua loja.";
				next;
				mes "[Mr. Hugh]";
				mes "Sua habilidade agora deve estar registrada em sua janela de habilidades. Se você não pode vê-la, provavelmente deverá minimizar a sua Lista de Habilidades e checar na Tabela das 3rd Classes";				next;
				mes "[Sr. Hugh]";
				mes "Eu te dei 5 Licença para Loja de Compras em Massa para o seu experimento.";
				mes "Boas compras!";
				close;
			}
		}
	}
	mes "Eu sou Hugh da Equipe de Compras do Clã dos Mercadores de Alberta.";
	mes "Eu adoraria conversar, mas eu estou muito ocupado no momento.";
	next;
	mes "[Sr. Hugh]";
	mes "Meu tempo é dedicado exclusivamente aos nossos clientes na indústria do comercio.";
	close;
}
