//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//	    Cronus Script
//------------------------------------------------------------
//= Npcs de refino
//===== Por: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Versão Atual: =====================================
//= 3.4
//===== Descrição: =========================================
//= Refinamentos e vendas de mental.
//===== Comentários Adicionais: =================================
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in morroc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed repairman prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the repairman function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morroc repairman to Morroc Ruins. [L0ne_W0lf]
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//=	3.4 Aplicada Tradução(PT_BR). [SkyHD]
//============================================================

// Christopher: Geffen Blacksmith
//============================================================
geffen_in,110,172,0	script	Christopher#1	1_M_SMITH,{
	mes "[Christopher Guillenrow]";
	mes "Seja bem-vindo à loja do Christopher. Você pode achar tudo relacionado a forja aqui. O que traz";
	mes "você até mim?";
	next;
	switch(select("Comprar uma bigorna:Comprar itens de forja:Comprar metais:Purificar minérios:Cancelar")) {
	case 1:
		mes "[Christopher Guillenrow]";
		mes "Uma boa bigorna aumenta muito as suas chances de se fazer uma boa arma, você sabe né? Mas irá custar mais zeny. Escolha a que melhor se encaixa em suas finalidade.";
		next;
		switch(select("Bigorna - 30,000 zeny:Bigorna de Oridecon - 120,000 zeny:Bigorna de Ouro - 300,000 zeny:Uma bigorna melhor que as outras.:Cancelar.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";
				close;
			}
			getitem 986,1; // Anvil
			set Zeny, Zeny-30000;
			mes "[Christopher Guillenrow]";
			mes "Este é um dos mais baratos, mas suficiente para forjar vários itens. Obrigado por comprar na minha oficina. Sinta-se livre para voltar a qualquer hora, sempre que precisar.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			set Zeny, Zeny-120000;
			mes "[Christopher Guillenrow]";
			mes "Humm, muitos compram essa bigorna. Essa é uma bigorna própria para forjadores, não é? Obrigado por comprar na minha oficina. Sinta-se livre para voltar a qualquer hora, sempre que precisar.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";				close;
			}
			getitem 988,1; // Golden_Anvil
			set Zeny, Zeny-300000;
			mes "[Christopher Guillenrow]";
			mes "Essa é a melhor bigorna que eu vendo na minha oficina! Com ela, você irá dominar o mundo da forja! Obrigado por comprar na minha oficina. Sinta-se livre para voltar a qualquer hora, sempre que precisar.";			close;
		case 4:
			mes "[Christopher Guillenrow]";
			mes "Bem, sinto muito. Mas eu não tenha nada melhor que a Bigorna de Ouro.";
			next;
			mes "[Christopher Guillenrow]";
			mes "Bem, sinto muito. Mas eu não tenha nada melhor que a Bigorna de Ouro.";
			next;
			mes "[Christopher Guillenrow]";
			mes "Eu acho, que o lendário criador de bigornas tem uma. Mas, eu não acho que você possa achá-lo, embora ele esteja em algum lugar do mundo.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "OK, sinta-se a vontade para voltar quando quiser, quando precisar. Até mais.";
			close;
		}
	case 2:
		mes "[Christopher Guillenrow]";
		mes "Um forjador respeitável deve usar ótimas ferramentas. Você pode comprá-las aqui. Escolha qual quer.";
		next;
		switch(select("Mini-Fornalha - 150 zeny:Martelo de Ferro - 1000 zeny:Martelo de Ouro - 3000 zeny:Martelo de Oridecon - 5000 zeny:Cancelar.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "É uma ferramenta muito útil para se refinar metais! Mas, quantas você irá comprar? Se você quiser desistir, basta colocar o número '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Bem, a negociação foi cancelada. Até mais.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Você pode comprar 500, ou menos.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 150;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Me parece que você não tem espaço suficiente no seu inventário. Porque você não coloca seus itens no armazém da Kafra?";
				close;
			}
			getitem 612,.@input; // Portable_Furnace
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se livre para vir aqui, sempre que precisar.";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";				close;
			}
			getitem 613,1; // Iron_Hammer
			set Zeny, Zeny-1000;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se livre para vir aqui, sempre que precisar.";			close;
		case 3:
			if (Zeny < 3000) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";				close;
			}
			getitem 614,1; // Golden_Hammer
			set Zeny, Zeny-3000;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se livre para vir aqui, sempre que precisar.";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";				close;
			}
			getitem 615,1; // Oridecon_Hammer
			set Zeny, Zeny-5000;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se livre para vir aqui, sempre que precisar.";			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Volte aqui quando quiser, sempre que precisar. Até mais.";
			close;
		}
	case 3:
		mes "[Christopher Guillenrow]";
		mes "Eu preparo muitos metais, e todos com grande qualidade. Mas então, qual deles você precisa?";
		next;
		switch(select("Fracon - 200z.:Emveretarcon - 1000z.:Cancelar.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "Bem, quantos você deseja comprar? Se você quiser cancelar a negociação, basta colocar o número '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "A negociação";
					mes "foi cancelada.";
					mes "Até mais.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Você só pode comprar 500, ou menos.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 200;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Você não tem o dinheiro. Você sabe que eu não posso vender por um baixo preço... Minha esposa reclama muito sobre a falta de Zeny.";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Me parece que você não tem espaço suficiente no seu inventário. Porque você não coloca seus itens no armazém da Kafra?";
				close;
			}
			getitem 1010,.@input; // Phracon
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se livre para vir aqui, sempre que precisar.";			close;
		case 2:
			mes "[Christopher Guillenrow]";
			mes "Bem, quantos você deseja comprar? Se você quiser cancelar a negociação, basta colocar o número '0.'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "A negociação";
					mes "foi cancelada.";
					mes "Até mais.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Você só pode comprar 500, ou menos.";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 1000;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Eu não posso vende-la pra você se você não tem o dinheiro. Eu não posso perder dinheiro por sua causa.";				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Meu amigo... Você não tem espaço suficiente no inventário. Porque não guarda algumas coisas no armazém da Kafra primeiro?";
				close;
			}
			getitem 1011,.@input; // Emveretarcon
			set Zeny, Zeny-.@sell;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se livre para vir aqui, sempre que precisar.";			close;
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Sinta-se livre para voltar, sempre que precisar. Até mais.";
			close;
		}
	case 4:
		mes "[Christopher Guillenrow]";
		mes "Eu posso purificar Oridecons e Eluniuns. Eu crio um metal refinado a partir de 5 metais brutos, seus minérios. Bem... Qual deles você quer fazer?";
		next;
		switch(select("Fazer um Oridecon:Fazer um Elunium:Cancelar.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Christopher Guillenrow]";
				mes "Eu lhe disse, eu preciso de 5 Minérios de Oridecon para fazer um Oridecon.";
				close;
			}
			else {
				delitem 756,5;  //Oridecon_Stone
				getitem 984,1; // Oridecon
				mes "[Christopher Guillenrow]";
				mes "Ótimo, aqui está o seu Oridecon. Volte sempre que precisar, espero sua visita.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Christopher Guillenrow]";
				mes "Eu lhe disse, eu preciso de 5 Minérios de Eluniuns para fazer um Elunium.";
				close;
			}
			else {
				delitem 757,5;  //Elunium_Stone
				getitem 985,1; // Elunium
				mes "[Christopher Guillenrow]";
				mes "Ótimo, aqui está o seu Elunium. Volte sempre que precisar, espero sua visita.";
				close;
			}
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Sinta-se livre para vir aqui, sempre que precisar. Até mais.";
			close;
		}
	case 5:
		mes "[Christopher Guillenrow]";
		mes "Volte sempre, sempre que precisar ou que quiser. Até mais.";
		close;
	}
}

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script	Paul Spanner	1_M_SMITH,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte -";
		mes "- quando você estiver mais leve. -";
		close;
	}
	mes "[Paul Spanner]";
	mes "Bem vindo, meu amigo.";
	mes "Na minha oficina, você podera encontrar tudo que necessita para forjar.";
	mes "Me diga o que você precisa.";
	next;
	switch(select("Comprar uma bigorna.:Comprar itens de forja.:Comprar metais.:Purificar minérios.:Sair.")) {
	case 1:
		mes "[Paul Spanner]";
		mes "A bigorna é o item mais importante para um forjador.";
		mes "Já que você irá usar várias vezes, é melhor você compra uma boa.";
		next;
		switch(select("Bigorna - 30,000z.:Bigorna de Oridecon - 120,000z.:Bigorna de Ouro - 300,000z.:Eu quero a melhor bigorna.:Cancelar.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "Com esse pouquinho de grana, você não pode comprar nem uma bigorna de brinquedo!";
				close;
			}
			getitem 986,1; //Anvil
			set Zeny, Zeny-30000;
			mes "[Paul Spanner]";
			mes "É a bigorna mais barata e mais simples que eu tenho aqui.";
			mes "Obrigado por comprar na minha loja. Se você precisar de qualquer coisa, não se acanhe a me pedir.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "Com esse pouquinho de grana, você não pode comprar nem uma bigorna de brinquedo!";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			set Zeny, Zeny-120000;
			mes "[Paul Spanner]";
			mes "Você tem um olho para forja. Um ferreiro precisa de uma boa bigorna pelo menos.";
			mes "Obrigado por comprar na minha loja. Se você precisar de qualquer coisa, não se acanhe a me pedir.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "Com esse pouquinho de grana, você não pode comprar nem uma bigorna de brinquedo!";
				close;
			}
			getitem 988,1; //Golden_Anvil
			set Zeny, Zeny-300000;
			mes "[Paul Spanner]";
			mes "Eu posso dizer que você tem ambição de se tornar um grande forjador só por ter comprado essa Bigorna de Ouro!";
			mes "Essa bigorna irá ajudá-lo a criar as melhores armas.";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "Me desculpe, mas eu não tenho bigornas melhores que a Bigorna de Ouro.";
			mes "A menos que você consiga encontrar a lendária bigorna de 'Linggell', eu não acho que você poderá encontrar nada melhor que a Bigorna de Ouro.";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes " Se você precisar de algo, basta me dizer.";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "Você precisa de vários materiais para forjar itens e purificar metais.";
		mes "Eu tenho tudo que você precisa. Dê uma olhada.";
		next;
		switch(select("Mini Fornalha - 150z.:Martelo de Ferro - 1,000z.:Martelo de Ouro - 3,000z.:Martelo de Oridecon - 5,000z.:Cancelar.")) {
		case 1:
			set .@item,612;
			set .@item_cost,150;
			set .@item_weight,200;
			mes "[Paul Spanner]";
			mes "Você definitivamente precisará dessa fornalha para purificar os minérios!";
			next;
			break;
		case 2:
			set .@item,613;
			set .@item_cost,1000;
			set .@item_weight,200;
			break;
		case 3:
			set .@item,614;
			set .@item_cost,3000;
			set .@item_weight,300;
			break;
		case 4:
			set .@item,615;
			set .@item_cost,5000;
			set .@item_weight,400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "Se você precisar de algo, basta me dizer.";
			close;
		}
		mes "[Paul Spanner]";
		mes "Então, de quantas você precisa? Se você quiser cancelar a negociação, basta colocar '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "Você cancelou a negociação. Se você precisar de algo, basta me procurar.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "Você só pode comprar 500 ou menos por vez.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "Você não possui dinheiro suficiente. Desculpe, eu não posso vender por menos.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Ei, você parece pálido. Alivie seu peso primeiro.";
			close;
		}
		set Zeny, Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "Obrigado por comprar aqui. Se você precisar de algo, basta me dizer.";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "Eu tenho metais de alta qualidade.";
		mes "Qual tipo de metal você quer comprar?";
		next;
		switch(select("Fracon - 200z.:Emveretarcon - 1,000z.:Sair.")) {
		case 1:
			set .@item,1010;
			set .@item_price,200;
			break;
		case 2:
			set .@item,1011;
			set .@item_price,1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "Se você precisar de algo, basta me dizer.";
			close;
		}
		mes "[Paul Spanner]";
		mes "De quantos você precisa? Se você quiser cancelar a compra, coloque '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "Você cancelou a negociação. Se você precisar de algo, basta me procurar.";				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "Você só pode comprar 500 ou menos por vez.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "Você não possui dinheiro suficiente. Desculpe, eu não posso vender por menos.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Ei, você parece pálido. Por que não alivia seu peso primeiro?";
			close;
		}
		getitem .@item,.@input;
		set Zeny, Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "Obrigado por comprar na minha oficina. Se você precisar de algo, pode me chamar.";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "Eu posso purificar Oridecons e Eluniuns para você.";
		mes "Você precisa de 5 minérios para purificá-los para Oridecons ou Eluniuns.";
		mes "O que você irá processar?";
		switch(select("Oridecon:Elunium:Sair.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "Você precisa de 5 Minérios de Oridecon para processar por um Oridecon.";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "Aqui está. Obrigado por usar os meus serviços.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "Você precisa de 5 Minérios de Elunium para processar por um Elunium.";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "Aqui está. Obrigado por usar os meus serviços.";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "Se você precisar de algo, pode me chamar.";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "Se você precisar de algo, pode me chamar.";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	Hollgrehenn	4_M_03,{
	callfunc "refinemain","Hollgrehenn",0;
	end;
}
morocc_in,73,38,6	script	Aragham	4W_M_03,{
	callfunc "refinemain","Aragham",0;
	end;
}
payon,144,173,5	script	Antonio	4_M_ORIENT01,{
	callfunc "refinemain","Antonio",0;
	end;
}
alberta_in,28,58,0	script	Fredrik	4_M_03,{
	callfunc "refinemain","Fredrik",0;
	end;
}
yuno_in01,171,21,4	script	Lambert	4_M_ORIENT01,{
	callfunc "refinemain","Lambert",0;
	end;
}
ein_in01,24,87,5	script	Manthasman	4_M_DWARF,{
	callfunc "refinemain","Manthasman Pruhag",0;
	end;
}
lhz_in02,282,20,7	script	Fulerr	4_M_LGTMAN,{
	callfunc "refinemain","Fulerr",0;
	end;
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine_db.txt as well.
//============================================================
function	script	refinemain	{
	disable_items;
	set .@features,getarg(1);
	mes "[" + getarg(0) + "]";
	mes "Eu sou um refinador.";
	mes "Eu posso refinar todos os tipos de armas e armadura, só preciso que você me";
	mes "diga o que quer refinar.";
	next;

	setarray .@position$[1], "Head","Body","Left hand","Right hand","Robe","Shoes","Accessory 1","Accessory 2","Head 2","Head 3";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if(getequipisequiped(.@i)) {
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[" + getarg(0) + "]";
		mes "I don't think I can refine any items you have...";
		close;
	}
	set .@part, select(.@menu$);

	if(!getequipisequiped(.@part)) { //custom check
		mes "[" + getarg(0) + "]";
		mes "Você não está usando";
		mes "nada que eu";
		mes "possa refinar.";
		emotion e_an;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "Eu acho que não posso";
		mes "refinar este item...";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "[" + getarg(0) + "]";
		mes "Eu não posso refinar esse item";
		mes "mais. Ele já foi refinado";
		mes "ao máximo permitido!";
		close;
	}
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	switch(getequipweaponlv(.@part)){
	case 0: 	//Refine Armor
		set .@price,2000;
		set .@material,985;
		set .@safe,4;
		break;
	case 1: 	//Refine Level 1 Weapon
		set .@price,50;
		set .@material,1010;
		set .@safe,7; 
		break;
	case 2: 	//Refine Level 2 Weapon
		set .@price,200;
		set .@material,1011;
		set .@safe,6; 
		break;
	case 3: 	//Refine Level 3 Weapon
		set .@price,5000;
		set .@material,984;
		set .@safe,5; 
		break;
	case 4: 	//Refine Level 4 Weapon
		set .@price,20000;
		set .@material,984;
		set .@safe,4; 
		break;
	case 5: 	//Refine other stuff?
		set .@price,2000;
		set .@material,985;
		set .@safe,4; 
		break;
	}
	if(.@features != 1) {
		mes "[" + getarg(0) + "]";
		mes "Para refinar isto eu precisa de";
		mes "um ^003366"+getitemname(.@material)+"^000000 e";
		mes "uma taxa de " + .@price + " Zeny.";
		mes "Você realmente deseja continuar?";
		next;
		if(select("Sim:Não") == 2){
			mes "[" + getarg(0) + "]";
			mes "OK...";
			mes "Não há necessidade de se ter";
			mes "pressa. Nós temos tempo.";
			close;
		}
		if(getequippercentrefinery(.@part) < 100) {
			mes "[" + getarg(0) + "]";
			mes "Oh não! Se você continuar a";
			mes "refinar isso, há um risco de";
			switch(.@material) {
			case 985:
				mes "ser destruido! Isso quer dizer que ^FF0000esse equipamento^000000, assim como ^FF0000qualquer cartas^000000 ou propriedades especiais adicionados nessa armadura, ^FF0000irão sumir^000000.";
				break;
			default:
				mes "ser destruido, e você iria ^FF0000perder essa arma^000000, e qualquer ^FF0000cartas nesta arma^000000,";
				mes "ou propriedades especiais dela.";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "Eu serei claro com você.";
			mes "Se a arma for destruida,";	
			mes "você não a terá de volta.";
			mes "Você realmente tem uma chance de";
			mes "^FF0000perder a sua arma^000000 para sempre.";
			mes "Você ainda quer refinar?";
			next;
			if(select("Sim:Não") == 2){
				mes "[" + getarg(0) + "]";
				mes "Eu entendo completamente...";
				mes "Eu posso ser um grande refinador, mas também posso cometer erros.";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "[" + getarg(0) + "]";
			mes "Você não tem";
			mes "Zeny suficiente ou não tem "+getitemname(.@material)+"...";
			mes "Vá pegar o que eu preciso. Irei";
			mes "estar aqui todos os dias que você precisar.";
			close;
		}
		set Zeny, Zeny-.@price;
		delitem .@material,1;

		//custom checks
		if(getequipisequiped(.@part) == 0) { // hacker has removed the item (not changed, why?)
			mes "[" + getarg(0) + "]";
			mes "Olhe... você não está com o item equipado...";
			close;
		}
		if(getequiprefinerycnt(.@part) != .@refinerycnt || getequipid(.@part) != .@refineitemid) { // hacker has changed the item
			mes "[" + getarg(0) + "]";
			emotion e_an;
			mes "Espere um segundo...";
			mes "Você está pensando que eu sou idiota?!";
			mes "Você trocou de item enquanto eu não estava olhando! Saia daqui agora!";
			close;
		}

		if(getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "[" + getarg(0) + "]";
			emotion (!rand(5))?e_cash:e_omg;
			set .@lose,rand(1,3);
			if (.@lose == 1) {
				mes "OH! MEU DEUS!";
				mes "Que porcaria! De novo não!";
				mes "Eu realmente sinto muito, mas você sabe que eu preciso praticar para melhorar.";
				mes "Um, certo? Heh heh...";
			} else if(.@lose == 2) {
				mes "Nãããoo!";
				mes "Quebrou!";
				mes "M-Me desculpe!";
			} else {
				mes "Droga!";
				mes "Eu poderia ter";
				mes "tido mais cautela!";
				mes "Eu sinto muito...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion e_heh;
		set .@win,rand(1,3);
		if (.@win == 1) {
			mes "Perfeito!";
			mes "Heh heh!";
			mes "Mais uma vez,";
			mes "um trabalho";
			mes "de um mestre~";
		} else if(.@win == 2) {
			mes "Successo...!";
			mes "Mais uma vez, meu grande";
			mes "talento deslumbrante";
			mes "funcionou hoje.";
		} else {
			mes "Heh heh!";
			mes "Está pronto.";
			mes "Sem dúvida, meu trabalho é para";
			mes "a sua satisfação.";
			mes "Bom, ótimo trabalho!~";
		}
		close;
	}

// New Refining Functions ========================
	if(getequiprefinerycnt(.@part) < .@safe) {
		mes "[" + getarg(0) + "]";
		mes "Eu posso refinar até um limite seguro ou uma quantidade de vezes desejada.... É sua escolha...";
		next;
		set .@menu2,select("Para um limite seguro por favor.","Eu irei decidir quantas vezes.","Eu mudei de idéia...");
	} else
		set .@menu2,2;
	switch(.@menu2){
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		next;
		mes "[" + getarg(0) + "]";
		mes "Quantas vezes você quer que eu refine o seu item?";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "[" + getarg(0) + "]";
			mes "Eu não posso refinar este item tantas vezes.";
			close;
		}
		if(.@refinecheck > .@safe) {
			set .@refinecheck,.@refinecheck - .@safe;
			mes "[" + getarg(0) + "]";
			mes "Eu irei tentar refinar o equipamento " + .@refinecheck + " além do limite seguro. Seu equipamento pode ser destruido... Está bem?";
			next;
			if(select("Sim...","Não...") == 2){
				mes "[" + getarg(0) + "]";
				mes "Bem..Que assim seja...";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "[" + getarg(0) + "]";
		mes "Bem..Que assim seja...";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes "[" + getarg(0) + "]";
	mes "Irá custar para refinar " + .@refinecnt + " " + getitemname(.@material) + " e " + .@fullprice + " Zeny. Está certo?";
	next;
	if(select("Sim...","Não...") == 2){
		mes "[" + getarg(0) + "]";
		mes "Bem..Que assim seja...";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[" + getarg(0) + "]";
		mes "É tudo que você tem? Infelizmente, não posso trabalhar para você a um preço mais baixo ou sem os itens necessários.";
		close;
	}
	set Zeny, Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[" + getarg(0) + "]";
			mes "Veja... Você não está com nenhum item equipado...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "[" + getarg(0) + "]";
			mes "Mas você imagina que eu sou um cara estupido!?!";
			mes "Você trocou de item...";
			mes "Saia daqui antes que eu te estoure com o meu martelo!!!";
			close;
		} 
		mes "Clang, clang!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion e_omg;
			mes "[" + getarg(0) + "]";
			mes "WAHHHH!!! Me desculpe... Eu avisei que isso podia acontecer...";
			set .@refinecnt,.@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "Aqui está o material e o zeny não usados...";
			getitem .@material,.@refinecnt;
			set .@fullprice,.@refinecnt * .@price;
			set Zeny, Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion e_no1;
		set .@refinecnt,.@refinecnt - 1;
		next;
	}
	mes "[" + getarg(0) + "]";
	mes "Tudo pronto...Volte aqui novamente.";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	Vurewell	4_M_04,{
	callfunc "phramain","Vurewell";
	end;
}
payon,145,178,3	script	Begnahd	4_M_ORIENT01,{
	callfunc "phramain","Begnahd";
	end;
}
morocc_in,63,32,6	script	Sade	4W_M_03,{
	callfunc "phramain","Sade";
	end;
}
alberta_in,13,71,3	script	Kahlamanlith	4_M_04,{
	callfunc "phramain","Kahlamanlith";
	end;
}
yuno_in01,171,27,4	script	Dilemma	4_M_ORIENT01,{
	callfunc "phramain","Dilemma";
	end;
}
ein_in01,15,87,3	script	Tirehaus	4_M_04,{
	callfunc "phramain","Tirehaus";
	end;
}
lhz_in02,278,24,3	script	Krugg	4_M_04,{
	callfunc "phramain","Krugg";
	end;
}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte -";
		mes "- quando você estiver mais leve. -";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "Eu vendo 2 tipos de metais";
	mes "para temperar armas.";
	mes "Eu tenho ^007777Fracon^000000 para armas de";
	mes "nivel 1, e ^007777Emveretarcon^000000";
	mes "para armas de nivel 2.";
	next;
	switch(select("Fracon - 200 Zeny:Emveretarcon - 1000 Zeny:Perguntar sobre outros metais")) {
	case 1:
		set .@material,1010;
		set .@price,200;
		break;
	case 2:
		set .@material,1011;
		set .@price,1000;
		break;
	case 3:
		mes "[" + getarg(0) + "]";
		mes "Outros metais?";
		mes "Bem, você precisa de metais especiais para refinar armas de alto nível, ou qualquer tipo de armaduras. Mas como você sabe, Oridecon e Elunium são realmente";
		mes "dificeis de encontrar...";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "Quantos você quer comprar?";
	mes "Se você não quiser nenhum, basta colocar o numero, '0.'";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "[" + getarg(0) + "]";
		mes "A negociação foi";
			mes "cancelada.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "[" + getarg(0) + "]";
			mes "Bem, você só pode";
			mes "comprar 500 por vez.";
			mes "Não mais do que isso,";
			mes "entendeu? Ótimo.";
			next;
		}
		else {
			break;
		}
	}
	set .@sell,.@input * .@price;
	if (Zeny < .@sell) {
		mes "[" + getarg(0) + "]";
		mes "Humm...";
		mes "Você não tem";
		mes "Zeny suficiente para comprar";
		mes ""+ .@input +" destes.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "[" + getarg(0) + "]";
		mes "Hmm...";
		mes "Eu não posso te vender nada se você não tiver espaço no inventário. Porque você não guarda algumas coisas no armazém da Kafra e volta aqui?";
		close;
	}
	getitem .@material,.@input;
	set Zeny, Zeny-.@sell;
	mes "[" + getarg(0) + "]";
	mes "Aqui está!";
	mes "Muito obrigado pela";
	mes "sua compra, volte sempre.";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	Dietrich	4_M_02,{
	callfunc "orimain","Dietrich";
	end;
}
payon,137,178,5	script	Hakhim	4_M_ORIENT01,{
	callfunc "orimain","Hakhim";
	end;
}
morocc_in,72,32,6	script	Abdula	4W_M_03,{
	callfunc "orimain","Abdula";
	end;
}
alberta_in,21,63,5	script	Xenophon	4_M_02,{
	callfunc "orimain","Xenophon Zolotas";
	end;
}
yuno_in01,164,27,4	script	Delight	4_M_ORIENT01,{
	callfunc "orimain","Delight";
	end;
}
ein_in01,18,82,6	script	Matestein	4_M_02,{
	callfunc "orimain","Matestein";
	end;
}
lhz_in02,281,24,5	script	Fruel	4_M_02,{
	callfunc "orimain","Fruel";
	end;
}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte -";
		mes "- quando você estiver mais leve. -";
		close;
	}
	mes "[" + getarg(0) + "]";
mes "Eu posso purificar os seus";
	mes "Minérios de Oridecon ou";
	mes "Minérios de Elunium. Eu precisarei de";
	mes "5 minérios para fazer";
	mes "1 pura para você.";
	next;
	switch(select("Fazer Oridecon:Fazer Elunium:Perguntar sobre as pedras encantadas")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "[" + getarg(0) + "]";
		mes "Aqui está o seu Oridecon.";
			mes "Você será bem vindo quando";
			mes "voltar aqui novamente.";
			close;
		}
		else {
			mes "[" + getarg(0) + "]";
			mes "Você está brincando comigo, não é?";
			mes "Eu disse que preciso de 5 Minérios de Oridecon para fazer um Oridecon puro.";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "[" + getarg(0) + "]";
			mes "Aqui está o seu Elunium.";
			mes "Você será bem vindo quando";
			mes "voltar aqui novamente.";
			close;
		}
		else {
			mes "[" + getarg(0) + "]";
			mes "Você está brincando comigo, não é?";
			mes "Eu disse que preciso de 5 Minérios de Elunium para fazer um Elunium puro.";
			close;
		}
	case 3:
		mes "[" + getarg(0) + "]";
		mes "Pedras encantadas...?";
		mes "Eu fui um forjador de pedras por 20 anos, por isso ouvi muito sobre elas. Supostamente, existem";
		mes "quatro tipos diferentes.";
		next;
		mes "[" + getarg(0) + "]";
		mes "Cada pedra possui uma das seguintes propriedades elementais: Terra, Vento, Água e Fogo.";
		next;
		mes "[" + getarg(0) + "]";
		mes "Se você combinar uma pedra encantada com uma arma enquanto estiver forjando, essa arma irá possuir a mesma propriedade elemental da pedra que você usou.";
		next;
		mes "[" + getarg(0) + "]";
		mes "Devo dizer também, você precisará de uma altíssima habilidade de forja para fazer uma arma elemental.";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	Repairman#alb	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

moc_ruins,107,94,4	script	Repairman#moc	4W_M_03,{
	callfunc "repairmain","Repairman";
	end;
}

payon,143,165,4	script	Repairman#pay	4_M_ORIENT01,{
	callfunc "repairmain","Repairman";
	end;
}

prt_in,63,54,2	script	Repairman#prt	4_M_04,{
	callfunc "repairmain","Grendal";
	end;
}

yuno_in01,175,28,3	script	Repairman#juno	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

geffen_in,34,166,3	script	Repairman#gef	4W_M_03,{
	callfunc "repairmain","Repairman";
	end;
}

aldeba_in,38,60,3	script	Repairman#alde	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

lhz_in02,284,14,3	script	Repairman#lhz	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

prt_gld,139,117,4	script	Repairman#prt_gld	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

gef_fild13,263,117,4	script	Repairman#gef_fild	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

pay_gld,295,183,4	script	Repairman#pay_gld	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

alde_gld,220,152,4	script	Repairman#alde_gld	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

aru_gld,189,336,4	script	Repairman#aru_gld	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

sch_gld,340,80,7	script	Repairman#sch_gld	4_M_04,{
	callfunc "repairmain","Repairman";
	end;
}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
	set .@repairprice,5000;
	mes "["+getarg(0)+"]";
	mes "Ei você!";
	mes "Você precisa que";
	mes "eu repare algum item?";
	mes "Você pode contar comigo";
	mes "para reparar itens!";
	next;
	switch(select("Atualmente, ...:Nenhum no momento.")) {
	case 1:
		set .@checkitem,1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			set .@checkitem,.@checkitem+1;
		}
		set .@checkitem,.@checkitem-1;
		if (!.@checkitem) {
			mes "["+getarg(0)+"]";
			mes "Oh, isso é incrivel!";
			mes "Parece que você toma muito cuidado com as suas coisas. Nenhum dos seus itens foi danificado!";
			next;
			mes "["+getarg(0)+"]";
			mes "Se todos fossem como você, eu estaria desempregado!! Haha~!";
			close;
		}
		mes "["+getarg(0)+"]";
		mes "Hmm...";
		mes "Deixe-me ver...";
		mes "De todos os seus itens,";
		mes "" + .@checkitem + " estão danificados.";
		mes "Gostaria de repará-los?";
		next;
		set .@totalcost,.@repairprice*.@checkitem;
		mes "["+getarg(0)+"]";
		mes "Cada reparo custa " + .@repairprice + ". Então para reparar todos os seus itens você irá pagar " + .@totalcost + " Zeny! Você quer mesmo reparar os seus itens?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+getarg(0)+"]";
				mes "Ei ei...";
				mes "Olhe a sua carteira antes de pedir para repararem um item, cara! Eu não posso reparar nada porque você não possui dinheiro suficiente.";
				close;
			}
			set .@checkitem2,1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				set .@checkitem2,.@checkitem2+1;
			}
			set .@checkitem2,.@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				set Zeny, Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					set .@checkitem,.@checkitem-1;
				}
				mes "["+getarg(0)+"]";
				mes "Okay! Tudo pronto. Agora, tente ter um pouco mais de cuidado. Você sabe, itens tem vida também.";
				close;
			}
			else {
				mes "["+getarg(0)+"]";
				mes "Mmm? Tem algo errado. Espere... Pegue os itens que você quer reparar e volte aqui.";
				close;
			}
		case 2:
			mes "["+getarg(0)+"]";
			mes "Bem, não é da minha conta, mas não é bom levar itens quebrados embora. Você deve repará-los o mais rápido possivel!";
			close;
		}
	case 2:
		mes "["+getarg(0)+"]";
		mes "Hohoho...";
		mes "Você não tem";
		mes "nada para fazer aqui";
		mes "se você não tiver";
		mes "itens para reparar.";
		close;
	}
}
