//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Inn Npcs
//===== Por: ==================================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Versão Atual: =====================================
//= 3.0
//===== Compatível Com: ===================================
//= Cronus
//===== Descrição: =========================================
//=Npcs, Salvar e curar
//============================================================
//= variables:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Comentários Adicionais: =================================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Recepcionista. Official warp
//=     and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Recepcionista. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Funcionária#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.8 Removed Moscovia Inn; Its in quests/quest_moscovia.txt [Kisuka]
//= 2.9 Added switch menus, updated to match AEGIS scripts. [Kisuka]
//= 2.9a Added 'npcskill' command. [Euphy]
//= 3.0 Aplicada Tradução(PT_BR). [SkyHD]
//============================================================


//========================Prontera ===================================
// West Side Inn -------------------
prt_in,244,135,2	script	Funcionária#Sammy	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// East Side Inn ------------------
prt_in,61,141,2	script	Funcionária#Ahlma	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//========================Alberta ====================================
alberta_in,32,142,3	script	Funcionária#Jennie	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Jennie]","'Hotel de Pescadores'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//======================Geffen ======================================
geffen_in,70,64,5	script	Funcionária#Cena	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Cena]","'Ifrit,' o único Hotel na cidade de Geffen","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//=======================Payon ======================================
payon_in01,132,62,5	script	Funcionária#Ahee	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Ahee]","Hotel de Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//========================Morocc ====================================
// North East --------------------------
/*
morocc_in,147,141,3	script	Funcionária#Hasna	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Hasna]","Morroc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// South --------------------------------
morocc_in,80,100,5	script	Funcionária#Manar	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Funcionária Manar]","Interior de Morroc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//======================Aldebaran ===================================
aldeba_in,92,58,5	script	Recepcionista#Rilim	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Rilim]","Interior de Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//========================Rachel ====================================
ra_in01,376,69,4	script	Funcionária#Annie	4_M_RACHMAN1,{

	callfunc "F_InnMaid","[Annie]","Interior de Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//======================Lighthalzen =================================
lhz_in02,230,284,4	script	Funcionário do Hotel#01	4_M_04,{
	mes "[Funcionário do Hotel]";
	mes "Dragão Real,";
	mes "onde você encontra as";
	mes "melhores acomodações";
	mes "e o melhor serviço.";
	next;
	switch(select("Salvar:Descansar - 5.000 zenys:Cancelar")) {
	case 1:
		savepoint "lhz_in02",209,275;
		mes "[Funcionário do Hotel]";
		mes "Obrigado, seu";
		mes "Ponto de Retorno foi";
		mes "salvo aqui no";
		mes "Dragão Real.";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[Funcionário do Hotel]";
			mes "Desculpe, mas";
			mes "você precisa de 5.000 zenys";
			mes "para se hospedar aqui.";
			close;
		}
		set Zeny,Zeny - 5000;
		mes "[Funcionário do Hotel]";
		mes "Obrigado!";
		mes "Espero que goste";
		mes "de sua estadia no";
		mes "Dragão Real.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[Funcionário do Hotel]";
		mes "Obrigado e";
		mes "tenha um bom dia.";
		close;
	}
}

//========================Hugel ====================================
hu_in01,246,107,3	script	Recepcionista#Recep	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Receptionist]","Interior de Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//========================Veins ====================================
ve_in,157,219,5	script	Recepcionista#Rece	4_M_SEAMAN,{
	mes "[Recepcionista]";
	mes "Bom dia!";
	mes "Bem-vindo ao";
	mes "mais confortável";
	mes "Hotel de Veins!";
	next;
	switch(select("Ponto de Retorno:Descansar – 5.000z:Sair")) {
	case 1:
		mes "[Recepcionista]";
		mes "Seu Ponto de Retorno";
		mes "foi salvo em Veins.";
		mes "Aproveite sua estadia na cidade.";
		savepoint "ve_in",157,209;
		close;
	case 2:
		mes "[Recepcionista]";
		if(Zeny < 5000){
			mes "Desculpe, mas não acho";
			mes "que você tem dinheiro";
			mes "suficiente para se hospedar. A";
			mes "taxa de serviço é de 5.000 zenys.";
			close;
		}
		mes "Aproveite a sua estada!";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[Recepcionista]";
		mes "Volte sempre.";
		close;
	}
}

//=======================Inn Function ==============================
function	script	F_InnMaid	{
	mes getarg(0);
	mes "Olá, você chegou à";
	mes getarg(1) + ".";
	mes "Em que posso ajudar?";
	next;
	switch(select("Salvar:Descansar - 5.000 zenys:Cancelar")) {
	case 1:
		mes getarg(0);
		mes "O seu Ponto de Retorno foi salvo.";
		mes "Obrigada, volte sempre.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		mes getarg(0);
		if(Zeny < 5000){
			mes "Desculpe, a taxa de serviço é de 5.000 zenys.";
			mes "Por favor, tenha certeza de que tem dinheiro suficiente para esse serviço.";
			close;
		}
		mes "Obrigada.";
		mes "Espero que você tenha um bom descanso.";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
