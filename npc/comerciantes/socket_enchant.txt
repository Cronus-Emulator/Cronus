//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                   Cronus Script
//------------------------------------------------------------
//= Episode 11.1 Socket Enchant NPC
//===== Por: ==================================================
//= rAthena Dev Team
//===== Versão Atual: =====================================
//= 2.1
//===== Compatível Com: ===================================
//= Cronus
//===== Descrição: =========================================
//= [Official Conversion]
//= Adiciona slot para armas e armaduras selecionados.
//===== Comentários Adicionais: =================================
//= 0.1a added missing L_No: to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//= 2.1  Aplicada Tradução(PT_BR). [SkyHD]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Espere um minuto !! -";
		mes "- Você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor volte após -";
		mes "- perder algum peso. -";
		close;
	}
	mes "[Seiyablem]";
	mes "Sou um engenheiro especializado em abrir slots em armas e armaduras normais.";
	mes "O preço, os materiais necessários e a chance de sucesso dependem do material que irei trabalhar.";
	next;
	mes "[Seiyablem]";
	mes "Adicionar slots pode ser simples, mas é mais complicado do que parece.";
	mes "Se estiver interessado em meu serviço, diga-me.";
	next;
	switch(select("Adicionar slot à Arma:Adicionar slot à Armadura:Informações sobre o Encantamento de Slots:Cancelar"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "Você quer adicionar slot em uma arma?";
			mes "Eu categorizo armas pela dificuldade em adicionar slots, Classe C como a mais fácil e Classe S como a mais difícil.";
			mes "Qual classe você quer tentar?";
			next;
			switch(select("Classe C:Classe B:Classe A:Classe S"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Classe C, não é?";
					mes "Agora, eu só posso slotar certos tipos de equipamentos dessa classe, então escolha um dos seguintes itens, então poderemos tentar adicionar um slot nele.";
					next;
					switch(select("Tridente:Cordel:Violino"))
					{
						case 1: callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2: callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3: callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
				mes "Classe B?";
					mes "Tudo bem, está é a classe média de equipamentos.";
					mes "Escolha a arma em que você quer que eu tente adicionar um slot.";
					next;
					switch(select("Mangual Pesado:Gladius:Gakkung:Pique:Haedonggum:Alaúde:Açoite:Waghnakh:Arbaleste"))
					{
						case 1: callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2: callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3: callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4: callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5: callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6: callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7: callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8: callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9: callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, Classe A.";
					mes "É uma tentativa de alto risco!";
					mes "Tudo bem, qual a arma em que você quer que eu tente adicionar um slot?";
					next;
					switch(select("Arco de Caça:Bastão do Sobrevivente(INT):Zweihander:Flamberge:Infiltrador:Balista:Atordoador:Fúria Insana:Claymore"))
					{
						case 1: callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2: callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3: callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4: callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5: callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6: callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7: callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8: callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9: callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					mes "Quê.... mas, Classe S? *Treme**";
					mes "Então...para qual arma de Classe S";
					mes "você quer um slot?";
					next;
					switch(select("Gungnir:Adaga Venenosa:Rondel:Sucsamad:Ginnungagap:Cutelo:Foice:Bastão do Sobrevivente(DEX)"))
					{
						case 1: callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4: callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8: callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
		mes "Armadura, você disse?";
			mes "Ok, sem problema.";
			mes "Armaduras são divididas em 4 diferentes classes, dependendo do nível de dificuldade.";
			mes "Classe C é a mais fácil e Classe S é a mais difícil.";
			mes "Qual classe você quer tentar?";
			switch(select("Classe C:Classe B:Classe A:Classe S"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "Classe C de armaduras, não é?";
					mes "Soa razoável.";
					mes "Em qual armadura você quer tentar adicionar um slot?";
					next;
					switch(select("Capote:Casaco:Tiara:Bireta"))
					{
						case 1: callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2: callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3: callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4: callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "Você escolheu a intermediária Classe B.";
					mes "Então, qual tipo de armadura você tem?";
					next;
					switch(select("Escudo Espelhado:Cota de Malha:Manto Sagrado:Manto de Seda:Botas:Sapatos:Sobrepeliz:Vembrassa:Broquel:Escudo:Chapéu de Bongun"))
					{
						case 1:  callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2:  callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3:  callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4:  callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5:  callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6:  callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7:  callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8:  callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9:  callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10: callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11: callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "Ooh, Classe A.";
					mes "É um tanto arriscado!";
					mes "Tudo bem, em qual equipamento você quer que eu tente adicionar um slot?";
					next;
					switch(select("Capacete de Schallern:Chapéu de Balde:Grimório:Malha:Armadura Legionária:Armadura Metálica:Traje de Gatuno:Grevas:Chapéu de Freira:Manteau:Elmo:Indumentária Ninja:Capacete de Orc:Capa do Marquês:Chapéu de Monge:Peça Dourada:Broche:Chapéu de Munak"))
					{
						case 1:  callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2:  callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3:  callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4:  callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5:  callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6:  callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7:  callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8:  callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9:  callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10: callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11: callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12: callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13: callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14: callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15: callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16: callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17: callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18: callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "Hm... Classe S?";
					mes "Está certo disso?";
					mes "Tudo bem... Deixe-me ver a armadura a qual você quer adicionar um slot e eu tentarei fazê-lo.";
					switch(select("Chifres Majestosos:Chifes Pontudos:Elmo de Osso:Corsário:Coroa:Diadema:Coroa do Faraó:Manto da Conjuração:Brinco:Anel:Luva de Puxada"))
					{
						case 1:  callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2:  callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3:  callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4:  callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5:  callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6:  callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7:  callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8:  callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9:  callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10: callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11: callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "Bem, eu ainda não refinei a arte de adicionar slots totalmente.";
			mes "É muito complicado e eu estaria mentindo se eu falasse que sei todos os fatores que afetam no processo.";
			mes "Ainda, vou contar algumas tendências......";
			next;
			mes "[Seiyablem]";
			mes "Em algumas cidades, adicionar slots é mais fácil com equipamentos com poucos upgrades.";
			mes "Em outras cidades, acontece o contrário.";
			mes "Não é estranho?";
			next;
			mes "[Seiyablem]";
			mes "Oh, tem também algo importante de que você precisa saber.";
			mes "Se você me pedir para adicionar slots em alguma coisa, tenha certeza de que você não tem mais de um no inventário.";
			next;
			mes "[Seiyablem]";
			mes "Aqui vai um exemplo: se você tem um Manteau +7 e um manteau normal no seu inventório, eu vou escolher um dos dois aleatoriamente quando for tentar adicionar um slot.";
			mes "Lembre-se de ser cuidadoso.";
			next;
			mes "[Seiyablem]";
			mes "Denovo, ^FF0000Carregue apenas uma unidade do equipamento o qual você quer que eu adicione um slot^000000.";
			mes "Todos os outros equipamentos com o mesmo nome, devem ser colocados na Storage da Kafra, certo?";
			close;
			
		case 4:
			mes "[Seiyablem]";
			mes "Sem problemas, aventureiro.";
			mes "Se você quizer tentar adicionar um slot em algum equipamento, volte e me fale.";
			mes "Tchau.";
			close;
	}
}

function	script	Func_Socket	{

	disable_items;
	mes "[Seiyablem]";
	mes "Você quer adicionar um Slot para um " + getitemname(getarg(0)) + "?";
	mes "Tudo bem, por favor me traga ^FF0000" + getarg(6) + " " + getitemname(getarg(5)) + (getarg(6) > 1 && getarg(5) != 999 ? "s":"") + "^000000, " + (getarg(7,0) != 0 && getarg(8,0) != 0 ? ("^FF0000" + getarg(8,0) + " " + getitemname(getarg(7,0)) + (getarg(8,0) > 1 && getarg(7,0) != 999 ? "s":"") + "^000000 ") : "") + "and my ^FF0000" + (getarg(4) >= 1000 ? (getarg(4)/1000 + ",000") : getarg(4)) + ",000 zeny^000000 para a taxa de serviço.";
	mes "Ah, e não se esqueça de levar isso " + getitemname(getarg(0)) + "!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(getarg(0),5)&2) // EQP_HAND_R = 2, it's a weapon
	{
		mes "Posso tentar adicionar um slot se você tiver os itens e o zeny necessários.";
		mes "Também, Você deve saber que há a chance de eu falhar.";
		mes "Então, preciso dar a você um pequeno aviso...";
		next;
		mes "[Seiyablem]";
		mes "Se essa tentativa de adicionar um slot na arma falhar, a ^FF0000Arma^000000 e ^FF0000qualquer carta colocada nela serão destruídos^000000.";
	}
	else // armor
	{
	mes "Se você tiver todos os itens requeridos, o zeny para cobrir o preço pelo meu serviço e a armadura, então poderemos continuar com a tentativa de adicionar um slot.";
		mes "Mas antes, preciso alertá-lo dos riscos.";
		next;
		mes "[Seiyablem]";
		mes "Se essa tentativa de adicionar um slot na armadura falhar, a ^FF0000Armadura^000000, os ^FF0000Upgrades^000000 e ^FF0000 qualquer carta colocada nela serão destruídos^000000.";
	}
	mes "Você ainda quer tentar adicionar o Slot?";
	next;
	switch(select("Tentar adicionar Slot:Cancelar"))
	{
		case 1:
			if ((Zeny >= getarg(4)*1000) && (countitem(getarg(5)) >= getarg(6)) && (countitem(getarg(7,512)) >= getarg(8,0)) && (countitem(getarg(0)) > 0)) // if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "Tudo certo então, que o trabalho começe!";
				mes "É melhor você rezar para que dê certo.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD : EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "Ótimo, parece que deu certo.";
					mes "Parece muito bem feito. Parabéns!";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "Até mais, amigo!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "Wah! ...Me desculpe, falhou.";
					mes "De qualquer jeito, eu sou completamente inocente.";
					mes "É a sua sorte, é destinada por Deus, ok?";
					mes "Não fique desapontado, e tente novamente.";
					delitem getarg(0),1;
					delitem getarg(5),getarg(6);
					if (getarg(7,0) != 0 && getarg(8,0) != 0)
						delitem getarg(7),getarg(8);
					set Zeny, Zeny - getarg(4)*1000;
					next;
					mes "[Seiyablem]";
					mes "Eu lhe deseja boa sorte da próxima vez!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "Eu queria continuar, mas você está esquecendo de alguma coisa.";
				mes "Tem certeza de que você está com o equipamento, com o zeny e com os itens?";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "Precisa de um tempo para pensar, não é?";
			mes "Tudo bem, eu entendo.";
			mes "Apenas lembre-se que não há graça em uma vida que em que só se aposta com segurança~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	4_M_02
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	4_M_02
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	4_M_02
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	4_M_02

-	script	Jovem#dummy::SocketMan	-1,{
	mes "[Jovem]";
	mes "Estou pensando em ser um Engenheiro de Slot.";
	mes "É uma nova área onde você pode adicionar slots a equipamentos, isso pode ser legal se você puder trabalhar em uma carreira lucrativa.";
	next;
	mes "[Jovem]";
	mes "Pelo que eu entendi, há muita sorte envolvida na chance de se adicionar slots.";
	mes "Você falha as vezes, mas se tiver muita sorte, pode adicionar até dois slots.";
	mes "Louco, não é?";
	next;
	mes "[Jovem]";
	mes "Mas não fique tão feliz.";
	mes "Apenas alguns equipamentos podem receber dois slots extras.";
	mes "Há muito mais para aprender antes de eu me tornar um aprendiz...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Jovem#prt	4W_M_01
payon,143,143,7	duplicate(SocketMan)	Jovem#pay	4W_M_01
morocc,60,42,3	duplicate(SocketMan)	Jovem#moc	4W_M_01
lhz_in02,269,33,5	duplicate(SocketMan)	Jovem#lhz	4W_M_01
