//------------------------------------------------------------
// _________                                    
// \_   ___ \_______  ____   ____  __ __  ______
// /    \  \/\_  __ \/    \ /    \|  |  \/  ___/
// \     \____|  | \(  ( ) )   |  \  |  /\___ \ 
//  \______  /|__|   \____/|___|  /____//____  >
//         \/                   \/           \/  
//------------------------------------------------------------
//                    Cronus Script
//------------------------------------------------------------
//= Negociador de Aljaves de Felchas
//===== Por: =================================================
//= Muad_Dib; L0ne_W0lf;
//===== Versão Atual: ========================================
//= 1.2c
//===== Compatível Com: ======================================
//= Cronus
//===== Descrição: ===========================================
//= Vende/Troca Aljaves e Flechas
//= Arg (0): Tipo de flecha para ser embalado (item ID)
//= Arg (1): Como muitos de cada 'getarg (0)' seta por quiver
//= Arg (2): O custo de fazer um "getarg (0) 'quiver.
//= Arg (3): A quiver dada pelo NPC. (Item ID)
//===== Comentários Adicionais: =================================
//= 07/06/05 : Added 1st Version. [Muad_Dib]
//= Converted to rAthena format by Dr.Evil
//= added prize to quest - 500 Zeny
//= Fixed a few spelling errors. [Nexon]
//= 1.1 Added Holy Arrow Quivers [Playtester]
//= 1.2 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     Removed "Holy Arrows" from the list.
//= 1.2a Traduzido. [SoulBlaker]
//= 1.2b Revisado. [RoM]
//= 1.2c Removido show [Yum]
//============================================================

payon_in01,5,134,5	script	Inventor Jaax	4_M_ORIENT02,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "[Inventor Jaax]";
		mes "Ei, você está você está carregando";
		mes "muito peso. Por que você não usa o Armazém da Kafra? Nós podemos nos falar depois que você fazer isto, certo?";
		close;
	}
	mes "[Inventor Jaax]";
	mes "Meu nome é Jaxx.";
	mes "Sem ego, posso";
	mes "dizer que sou o";
	mes "^663300maior inventor de todos os tempos^000000.";
	next;
	mes "[Inventor Jaxx]";
	mes "Agora eu";
	mes "criei algo";
	mes "verdadeiramente extraordinário. Eu os chamo de... ^663300Aljaves Mágicos^000000!! Isso é um fato histórico como";
	mes "uma revolução das flechas!";
	emotion e_no1;
	next;
	mes "[Inventor Jaax]";
	mes "Eu estudei magia e aljaves durante anos, trabalhando noite e dia até que eu finalmente descobri como condensar flechas com magia! Você estará transportando mais flechas, mas com menos peso!";
	next;
	mes "[Inventor Jaax]";
	mes "Deseja tentar usar um dos meus aljaves mágicos? Não tenho dúvidas de que alguém como você pode apreciar o meu gênio!";
	next;
	switch(select("Aljave de Flechas","Aljave de Flechas de Ferro","Aljave de Flechas de Aço","Aljave de Flechas de Oridecon","Aljave de Flechas de Fogo","Aljave de Flechas de Prata","Aljave de Flechas de Vento","Aljave de Flechas de Pedra","Aljave de Flechas de Cristal","Aljave de Flechas Sombria","Aljave de Flechas Incorpórea","Aljave de Flechas Enferrujada")){
	case 1: callsub S_BuyQuiver,1750,500,500,12004;
	case 2: callsub S_BuyQuiver,1770,500,500,12005;
	case 3: callsub S_BuyQuiver,1753,500,500,12006;
	case 4: callsub S_BuyQuiver,1765,500,500,12007;
	case 5: callsub S_BuyQuiver,1752,500,500,12008;
	case 6: callsub S_BuyQuiver,1751,500,500,12009;
	case 7: callsub S_BuyQuiver,1755,500,500,12010;
	case 8: callsub S_BuyQuiver,1756,500,500,12011;
	case 9: callsub S_BuyQuiver,1754,500,500,12012;
	case 10: callsub S_BuyQuiver,1767,500,500,12013;
	case 11: callsub S_BuyQuiver,1757,500,500,12014;
	case 12: callsub S_BuyQuiver,1762,500,500,12015;
	}
	//This doesn't seem to be accessable...
	//mes "[Inventor Jaax]";
	//mes "Está Aqui";
	//mes "Você precisa de algo?";
	//close;

S_BuyQuiver:
	if (countitem(getarg(0)) >= getarg(1)) {
		mes "[Inventor Jaax]";
		mes "Excelente!";
		mes "Você não está carregando nenhum Aljave com você? Eu Posso lhe oferecer um Aljave que pode carregar "+getarg(1)+" de sua  "+getitemname(getarg(0))+"s por apenas ^FF3131"+getarg(2)+" Zeny^000000.";
		next;
		switch(select("Guardar suas Flechas em Aljaves:Comprar 1 Aljave:Cancelar")) {
		case 1:
			set .@arrows,countitem(getarg(0));
			set .@quiver,.@arrows / getarg(1);
			set .@arrows_used,.@quiver * getarg(1);
			set .@arrow_zeny01,.@quiver * getarg(2);
			mes "Número de";
			mes "Flechas: ^3131FF"+.@arrows+" ^000000";
			mes "Numero Máximo";
			mes "de Aljaves";
			mes "para Compra: ^3131FF"+.@quiver+" ^000000";
			mes "Zeny Requerido: ^3131FF"+.@arrow_zeny01+" Zeny^000000";
			next;
			mes "[Inventor Jaax]";
			mes "Você gostaria de";
			mes "comprar a máxima";
			mes "quantidade de Aljaves";
			mes "para as flechas que você está carregando?";
			next;
			if (select("Sim:Cancelar") == 1) {
				if (.@arrow_zeny01 < Zeny) {
					mes "[Inventor Jaax]";
					mes "Aqui estão elas!";
					mes "Mas lembre, ^FF0000você não será capaz de usar o Aljave quando seu peso estiver acima de 90% do seu peso máximo^000000.";
					set Zeny, Zeny-.@arrow_zeny01;
					delitem getarg(0),.@arrows_used; //Arrow
					getitem getarg(3),.@quiver; //Quiver
					next;
					mes "[Inventor Jaax]";
					mes "Então...";
					mes "Somente tenha controle";
					mes "da quantidade que você";
					mes "carrega por vez e tudo";
					mes "ficará bem";
					close;
				}
				else {
					mes "[Inventor Jaax]";
					mes "Me desculpe... Mas você não tem Zeny o suficiente. Eu Não trabalhei anos";
					mes "para criar essa tecnologia extraordinaria por nada!";
					close;
				}
			}
			mes "[Inventor Jaax]";
			mes "Você mudou de idéia?";
			mes "Quando a glória de possuir";
			mes "um Aljave estava tão perto?";
			close;
		case 2:
			if (Zeny > getarg(2)) {
				mes "[Inventor Jaax]";
				mes "Aqui estão elas!";
				mes "Mas lembre, ^FF0000você não será capaz de usar o Aljave quando seu peso estiver acima de 90% do seu peso máximo^000000.";
				set Zeny, Zeny-getarg(2);
				delitem getarg(0),getarg(1); //Arrow
				getitem getarg(3),1; //Quiver
				next;
				mes "[Inventor Jaax]";
				mes "Então...";
				mes "Somente tenha controle";
				mes "da quantidade que você";
				mes "carrega por vez e tudo";
				mes "ficará bem";
				close;
			}
			else {
				mes "[Inventor Jaax]";
				mes "Você não tem sequer";
				mes getarg(2)+" Zeny?";
				mes "Me desculpe. Eu não";
				mes "tinha idéia que você era";
				mes "^333333desprovido^000000.";
				close;
			}
		case 3:
			mes "[Inventor Jaax]";
			mes "O que...?";
			mes "Você não vê que esta invenção pode mudar para sempre a maneira como são usadas as Flechas?! O futuro é agora!";
			close;
		}
	}
	else {
		mes "[Inventor Jaax]";
		mes "Você pode transportar um número máximo de 500 Flechas dentro deste aljave. Foi feita usando o meu método secreto,";
		mes "por isso o peso total da Flecha e do Alhave é inferior do que carregar a Flecha sozinha.";
		next;
		mes "[Inventor Jaax]";
		mes "É um milagre da ciência! Um que você pode experimentar por si mesmo se você me trouxer ao menos 500 Flechas e 500 Zeny para cada Aljave.";
		close;
	}
}
