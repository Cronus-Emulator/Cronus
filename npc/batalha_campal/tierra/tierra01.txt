//===== Hercules Script ======================================
// BattleGround System - Tierra Gorge
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Description: =========================================
//= [Official Conversion]
//= Tierra Gorge Battleground.
//= - Winning Team: 3 badges
//= - Losing Team: 1 badge
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Corrected setwalls for barricades. [L0ne_W0lf]
//= 1.2 Updated 'waitingroom' to support required zeny/lvl. [Kisuka]
//= 1.3 Removed MaxLvl check in waitingrooms.
//= 1.4 Label standardization. [Euphy]
//= 1.5 Added GM management function. [Euphy]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,57,227,5	script	Tenente Kalos	4_M_KY_KNT,{
	end;

OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_a01::OnReadyCheck",1,0,80;
	end;

OnEnterBG:
	set $@TierraBG1_id1, waitingroom2bg("bat_a01",50,374,"start#bat_a01::OnGuillaumeQuit","");
	end;
}

bat_room,58,204,1	script	Tenente Eyor	4_M_CRU_KNT,{
	end;

OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_a01::OnReadyCheck",1,0,80;
	end;

OnEnterBG:
	set $@TierraBG1_id2, waitingroom2bg("bat_a01",42,16,"start#bat_a01::OnCroixQuit","");
	end;
}

bat_room,1,151,3	script	#bat_a01_timer	CLEAR_NPC,{
	end;

OnInit:
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	initnpctimer;
	set .@chk_bat_a01,getmapusers("bat_a01");
	if (.@chk_bat_a01 < 1) {
		set $@TierraBG1,0; set $@TierraBG1_Victory, 0;
		if( $@TierraBG1_id1 ) { bg_destroy $@TierraBG1_id1; set $@TierraBG1_id1, 0; }
		if( $@TierraBG1_id2 ) { bg_destroy $@TierraBG1_id2; set $@TierraBG1_id2, 0; }
		donpcevent "start#bat_a01::OnReadyCheck";
	}
	end;
}

bat_a01,352,342,0	script	#bat_a01_quest_a	-1,5,5,{
	end;

OnTouch:
	if (checkquest(2069) < 0)
		setquest 2069;
	end;
}

bat_a01,353,52,0	script	#bat_a01_quest_b	-1,5,5,{
	end;

OnTouch:
	if (checkquest(2069) < 0)
		setquest 2069;
	end;
}

bat_room,57,220,0	warp	back_bgrooma01a	1,1,bat_room,154,150
bat_room,57,211,0	warp	back_bgrooma01b	1,1,bat_room,154,150

// Tierra Gorge Battleground Engine
//============================================================
bat_a01,15,15,3	script	start#bat_a01	CLEAR_NPC,{
OnInit:
	mapwarp "bat_a01","bat_room",154,150;
	end;

OnEnable:
	donpcevent "OBJ#bat_a01_a::OnKill";
	donpcevent "OBJ#bat_a01_a::OnEnable";
	donpcevent "OBJ#bat_a01_b::OnKill";
	donpcevent "OBJ#bat_a01_b::OnEnable";
	donpcevent "barricada#bat_a01_a::OnKill";
	donpcevent "barricada#bat_a01_a::OnEnable";
	donpcevent "barricada#bat_a01_b::OnKill";
	donpcevent "barricada#bat_a01_b::OnEnable";
	donpcevent "OBJ#bat_a01_n::OnKill";
	donpcevent "OBJ#bat_a01_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a01_a::OnKill";
	donpcevent "NOBJ_mob#bat_a01_b::OnKill";
	donpcevent "Curandeira#a01_a::OnEnable";
	donpcevent "Curandeira#a01_b::OnEnable";
	donpcevent "countdown#bat_a01::OnEnable";
	disablenpc "Ferreiro de Guillaume#fg";
	disablenpc "Ferreiro de Croix#fc_01a";
	disablenpc "Armada de Guillaume#ag01";
	disablenpc "Armada de Croix#ac_01b";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	bg_leave;
	end;

OnReadyCheck:
	if( $@TierraBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Lieutenant Kalos");
	set .@Croix, getwaitingroomstate(0,"Lieutenant Eyor");
	if( !.@Guillaume && !.@Croix ) {
		donpcevent "#bat_a01_timer::OnStop";
		end;
	}
	else if( .@Guillaume < 10 || .@Croix < 10 )
		end;
	set $@TierraBG1,1;
	donpcevent "Tenente Kalos::OnEnterBG";
	donpcevent "Tenente Eyor::OnEnterBG";
	donpcevent "start#bat_a01::OnEnable";
	bg_warp $@TierraBG1_id1,"bat_a01",352,342;
	bg_warp $@TierraBG1_id2,"bat_a01",353,52;
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_a01_timer::OnEnable";
	end;
}

bat_a01,15,16,3	script	OBJ#bat_a01_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG1_id1,"bat_a01",177,345,"Suprimentos",1909,"OBJ#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_a::OnMyMobDead") < 1) {
		donpcevent "Curandeira#a01_a::OnStop";
		donpcevent "Curandeira#a01_b::OnStop";
		set $@TierraBG1_Victory, 2;
		enablenpc "Armada de Guillaume#ag01";
		enablenpc "Armada de Croix#ac_01b";
		mapannounce "bat_a01", "Armada de Croix Swandery: Destruímos o armazém de comida de Guillaume. Vencemos desta vez! Uau!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG1_id1,"bat_a01",50,374;
		bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	}
	end;
}

bat_a01,15,17,3	script	OBJ#bat_a01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG1_id2,"bat_a01",167,50,"Suprimentos",1910,"OBJ#bat_a01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_b::OnMyMobDead") < 1) {
		donpcevent "Curandeira#a01_a::OnStop";
		donpcevent "Curandeira#a01_b::OnStop";
		set $@TierraBG1_Victory, 1;
		enablenpc "Armada de Guillaume#ag01";
		enablenpc "Armada de Croix#ac_01b";
		mapannounce "bat_a01", "Armada de Guillaume: Destruímos o armazém de comida de Croix. Vencemos desta vez! Uau!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG1_id1,"bat_a01",50,374;
		bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	}
	end;
}

bat_a01,15,18,3	script	barricada#bat_a01_a	CLEAR_NPC,{
OnEnable:
	for( set .@i,185; .@i < 202; set .@i,.@i+1 )
		bg_monster $@TierraBG1_id1,"bat_a01",.@i,266,"Barricada",1906,"barricada#bat_a01_a::OnMyMobDead";
	setwall "bat_a01",186,266,16,6,1,"bat_a01_c1";
	end;

OnKill:
	killmonster "bat_a01","barricada#bat_a01_a::OnMyMobDead";
	delwall "bat_a01_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricada#bat_a01_a::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricada#bat_a01_a::OnMyMobDead";
		delwall "bat_a01_c1";
		enablenpc "Ferreiro de Guillaume#fg";
		mapannounce "bat_a01", "Armada de Guillaume: A Barricada no vale foi destruída pelo bárbaro do Croix! Precisamos consertá-la. Ajude-nos! Ajude-nos!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,19,3	script	barricada#bat_a01_b	CLEAR_NPC,{
OnEnable:
	for( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@TierraBG1_id2,"bat_a01",.@i,129,"Barricada",1906,"barricada#bat_a01_b::OnMyMobDead";
	setwall "bat_a01",170,129,16,6,1,"bat_a01_g1";
	end;

OnKill:
	killmonster "bat_a01","barricada#bat_a01_b::OnMyMobDead";
	delwall "bat_a01_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","barricada#bat_a01_b::OnMyMobDead") < 17) {
		killmonster "bat_a01","barricada#bat_a01_b::OnMyMobDead";
		delwall "bat_a01_g1";
		enablenpc "Ferreiro de Croix#fc_01a";
		mapannounce "bat_a01", "Armada de Croix: A Barricada no vale foi destruída pelo bárbaro do Guillaume! Precisamos consertá-la. Ajude-nos! Ajude-nos!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a01,15,20,3	script	OBJ#bat_a01_n	CLEAR_NPC,{
OnEnable:
	monster "bat_a01",273,203,"Ponto Estratégico",1911,1,"OBJ#bat_a01_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","OBJ#bat_a01_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a01","OBJ#bat_a01_n::OnMyMobDead") < 17) {
		bg_team_setxy getcharid(4),56,212;
		if (getcharid(4) == $@TierraBG1_id1) {
			donpcevent "NOBJ_mob#bat_a01_a::OnEnable";
			mapannounce "bat_a01", "Guillaume obteve um Ponto Estretégico, portanto está com vantagem.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a01_b::OnEnable";
			mapannounce "bat_a01", "Croix obteve um Ponto Estretégico, portanto está com vantagem.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a01,15,21,3	script	NOBJ_mob#bat_a01_a	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_b::OnKill";
	bg_monster $@TierraBG1_id1,"bat_a01",272,204,"Guardião de Guillaume",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id1,"bat_a01",272,213,"Guardião de Guillaume",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id1,"bat_a01",273,197,"Guardião de Guillaume",1950,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,15,22,3	script	NOBJ_mob#bat_a01_b	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a01_a::OnKill";
	bg_monster $@TierraBG1_id2,"bat_a01",272,204,"Guardião de Croix",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id2,"bat_a01",272,213,"Guardião de Croix",1949,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	bg_monster $@TierraBG1_id2,"bat_a01",273,197,"Guardião de Croix",1950,"NOBJ_mob#bat_a01_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a01","NOBJ_mob#bat_a01_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a01,185,270,1	script	Ferreiro de Guillaume#fg	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG1_id1) {
		mes "[Ferreiro de Guillaume]";
		mes "Estamos com urgência! A Barricada foi destruída!";
		mes "Podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouros.^000000";
		mes "Temos tudo, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Não Reparar.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro de Guillaume]";
				mes "Você trouxe pedras suficientes! Vamos reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combine os Ouros e Pedras na porcentagem adequada.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada, em seguida, adicione os Minérios de Elunium para torná-la mais forte.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada, em seguida, adicione os Minérios de Elunium para torná-la mais forte. Agora, decore com os Pedaços de Metal.";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada, em seguida, adicione os Minérios de Elunium para torná-la mais forte. Agora, decore com os Pedaços de Metal, tançe as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro de Guillaume]";
				mes "Uau! Está pronto.";
				mes "Estamos aliviados.";
				delitem 7049,50; //Stone
				donpcevent "barricada#bat_a01_a::OnEnable";
				close2;
				disablenpc "Ferreiro de Guillaume#fg";
				end;
			}
			else {
				mes "[Ferreiro de Guillaume]";
				mes "Você não tem Pedras suficientes!";
				next;
				mes "[Ferreiro de Guillaume]";
				mes "^3131FFPrecisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, por isso, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro de Guillaume]";
			mes "There are enemies coming! Let's evacuate from here!";
			close;
		}
	}
	else {
		mes "[Ferreiro de Guillaume]";
		mes "Há inimigos vindo! Vamos evacuar a área!";
		close;
	}

OnInit:
	disablenpc "Ferreiro de Guillaume#fg";
	end;
}

bat_a01,170,121,5	script	Ferreiro de Croix#fc_01a	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG1_id2) {
		mes "[Ferreiro de Croix]";
		mes "Estamos com urgência! A Barricada foi destruída!";
		mes "Podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouros.^000000";
		mes "Temos tudo, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Não Reparar.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro de Croix]";
				mes "Você trouxe pedras suficientes! Vamos reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combine os Ouros e Pedras na porcentagem adequada.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada, em seguida, adicione os Minérios de Elunium para torná-la mais forte.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada, em seguida, adicione os Minérios de Elunium para torná-la mais forte. Agora, decore com os Pedaços de Metal.";
				next;
				mes "[Ferreiro de Croix]";
				mes "Combine as Pedras e os Ouros na porcentagem adequada e forme a Barricada, em seguida, adicione os Minérios de Elunium para torná-la mais forte. Agora, decore com os Pedaços de Metal, tançe as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro de Croix]";
				mes "Uau! Está pronto.";
				mes "Estamos aliviados.";
				delitem 7049,50; //Stone
				donpcevent "barricada#bat_a01_b::OnEnable";
				close2;
				disablenpc "Ferreiro de Croix#fc_01a";
				end;
			}
			else {
				mes "[Ferreiro de Croix]";
				mes "Você não tem Pedras suficientes!";
				next;
				mes "[Ferreiro de Croix]";
				mes "^3131FFPrecisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, por isso, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro de Croix]";
			mes "Há inimigos vindo! Vamos evacuar a área!";
			close;
		}
	}
	else {
		mes "[Ferreiro de Croix]";
		mes "Lá, o inimigo está chegando!";
		close;
	}

OnInit:
	disablenpc "Ferreiro de Croix#fc_01a";
	end;
}

bat_a01,53,377,3	script	Curandeira#a01_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Curandeira]";
	mes "Agora, feche os olhos, e respire fundo.";
	mes "Vou curar sua dor.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Curandeira#a01_a";
	end;

OnStop:
	disablenpc "bat_a01_rp1_a_warp";
	disablenpc "Curandeira#a01_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_a_warp";
	end;

OnTimer26500:
	donpcevent "Curandeira#a01_a::OnEnable";
	end;
}

bat_a01,51,375,0	script	bat_a01_rp1_a_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_a_warp";
	end;

OnTouch_:
	percentheal 100,100;
	warp "bat_a01",352,342;
	end;
}

bat_a01,45,19,3	script	Curandeira#a01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Curandeira]";
	mes "Agora, feche os olhos, e respire fundo.";
	mes "Vou curar sua dor.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Curandeira#a01_b";
	end;

OnStop:
	disablenpc "bat_a01_rp1_b_warp";
	disablenpc "Curandeira#a01_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_b_warp";
	end;

OnTimer26500:
	donpcevent "Curandeira#a01_b::OnEnable";
	end;
}

bat_a01,43,17,0	script	bat_a01_rp1_b_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_b_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",353,52;
	end;
}

bat_a01,60,216,3	script	Fantasma#bat_a01_n	4_GHOSTRING,{
	specialeffect2 EF_HEAL;
	mes "[Fantasma]";
	mes "Boo... Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26000:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTimer26500:
	donpcevent "Fantasma#bat_a01_n::OnEnable";
	end;
}

bat_a01,55,213,0	script	bat_a01_rp1_n_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a01_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a01",301,209;
	end;
}

bat_a01,194,267,0	script	barri_warp_up#bat_a01_a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id1)
		warp "bat_a01",194,261;
	end;
}

bat_a01,194,265,0	script	barri_warp_down#bat_a01a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id1)
		warp "bat_a01",194,270;
	end;
}

bat_a01,177,130,0	script	barri_warp_up#bat_a01_b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id2)
		warp "bat_a01",178,125;
	end;
}

bat_a01,177,128,0	script	barri_warp_down#bat_a01b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG1_id2)
		warp "bat_a01",178,134;
	end;
}

bat_a01,352,342,0	script	A_CODE#bat_a01	-1,5,5,{
OnTouch:
	set .@chk_bfquest,checkquest(2069);
	if ((.@chk_bfquest == 0) || (.@chk_bfquest == 1))
		setquest 2069;
	end;
}

bat_a01,353,52,0	script	B_CODE#bat_a01	-1,5,5,{
OnTouch:
	set .@chk_bfquest,checkquest(2069);
	if ((.@chk_bfquest == 0) || (.@chk_bfquest == 1))
		setquest 2069;
	end;
}

bat_a01,169,227,0	script	underladd#bat_a01_1	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a01",178,228;
	end;
}

bat_a01,208,164,0	script	underladd#bat_a01_2	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a01",200,171;
	end;
}

bat_a01,171,309,3	script	Campo Guillaume#flag1	1_FLAG_LION,{ end; }
bat_a01,149,310,3	script	Campo Guillaume#flag2	1_FLAG_LION,{ end; }
bat_a01,119,336,3	script	Campo Guillaume#flag3	1_FLAG_LION,{ end; }
bat_a01,118,357,3	script	Campo Guillaume#flag4	1_FLAG_LION,{ end; }
bat_a01,150,380,3	script	Campo Guillaume#flag5	1_FLAG_LION,{ end; }
bat_a01,173,380,3	script	Campo Guillaume#flag6	1_FLAG_LION,{ end; }
bat_a01,210,344,3	script	Campo Guillaume#flag7	1_FLAG_LION,{ end; }
bat_a01,350,325,3	script	Campo Guillaume#flag8	1_FLAG_LION,{ end; }
bat_a01,358,325,3	script	Campo Guillaume#flag9	1_FLAG_LION,{ end; }

bat_a01,138,12,3	script	Campo Croix#flag1	1_FLAG_EAGLE,{ end; }
bat_a01,108,36,3	script	Campo Croix#flag2	1_FLAG_EAGLE,{ end; }
bat_a01,108,63,3	script	Campo Croix#flag3	1_FLAG_EAGLE,{ end; }
bat_a01,136,87,3	script	Campo Croix#flag4	1_FLAG_EAGLE,{ end; }
bat_a01,167,86,3	script	Campo Croix#flag5	1_FLAG_EAGLE,{ end; }
bat_a01,199,49,3	script	Campo Croix#flag6	1_FLAG_EAGLE,{ end; }
bat_a01,168,16,3	script	Campo Croix#flag7	1_FLAG_EAGLE,{ end; }
bat_a01,357,74,3	script	Campo Croix#flag8	1_FLAG_EAGLE,{ end; }
bat_a01,348,74,3	script	Campo Croix#flag9	1_FLAG_EAGLE,{ end; }

bat_a01,53,377,3	script	Armada de Guillaume#ag01	4_M_KY_HEAD,{
	if (getcharid(4) == $@TierraBG1_id1) {
		if ($@TierraBG1_Victory == 1) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	else {
		if ($@TierraBG1_Victory == 2) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Armada de Guillaume#ag01";
	end;
}

bat_a01,45,19,3	script	Armada de Croix#ac_01b	4_M_CRU_HEAD,{
	if (getcharid(4) == $@TierraBG1_id2) {
		if ($@TierraBG1_Victory == 2) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	else {
		if ($@TierraBG1_Victory == 1) {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			set .@your_medal,countitem(7828);
			set .@medal_gap, 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Armada de Croix#ac_01b";
	end;
}

bat_a01,1,5,3	script	countdown#bat_a01	CLEAR_NPC,{
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a01", "Soldado Axl Rose: Vamos atacar para botar fogo no armazém de comida de Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a01", "Armada de Croix Swandery: Mestres de Valhalla! Dêem-nos a benção da fé e da coragem infalíveis!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a01", "Marollo VII: Guillaume Marollo, Croix Marollo! E seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a01", "Marollo VII: Ambos os lados competiram bem, e é difícil julgar qual deles é superior.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a01", "Marollo VII: Esta batalha de Tierra é uma perda de tempo. Eu decidirei quem será o vitorioso de acordo com seu progresso.",bc_map,"0x99CC00";
	end;

//OnTimer1822000:
//	mapannounce "bat_a01", "Marollo VII : Hold your royalty and faith for a moment, and let's settle up the battle of Tierra Gorge.",bc_map,"0x99CC00";
//	end;

OnTimer1825000:
	mapannounce "bat_a01", "Soldado Axl Rose: Sim, senhor.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	mapwarp "bat_a01","bat_room",154,150;
	donpcevent "countdown#bat_a01::OnStop";
	end;
}

/*
bat_a01,351,75,3	script	Soldado do Campo Croix#bat_a01_guide	4_M_RASWORD,{
	mes "Carregando...";
	close;
}

bat_a01,356,326,3	script	Soldado do Campo Guillaume#bat_a01_guide	4_M_RASWORD,{
	mes "Carregando...";
	close;
}
*/

bat_a01,1,1,3	script	Liberar todos#a01	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Posso ajudá-lo?";
		next;
		switch(select("Liberar todos.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_a01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}
