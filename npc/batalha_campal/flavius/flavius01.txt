//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//- Batalha Campal - Flavius                                  -
//-------------------------------------------------------------
//- Por:                                                      -
//- L0ne_W0lf                                                 -
//-------------------------------------------------------------
//- Versão Atual:                                             -
//- 1.6                                                       -
//-------------------------------------------------------------
//- Compatível Com:                                           -
//- Cronus                                                    -
//-------------------------------------------------------------
//- Descrição:                                                -
//- [Conversão Oficial]                                       -
//- Batalha Campal de Flavius                                 -
//- - Time Vencedor: 9 Emblemas                               -
//- - Time Perdedor: 3 Emblemas                               -
//-------------------------------------------------------------
//- Comentários Adicionais:                                   -
//- 1.0 First Version.                                        -
//- 1.1 Fixed pink crystal spawning as blue. [L0ne_W0lf]      -
//- 1.2 Updated 'waitingroom' to,                             -
//    - support required zeny/lvl. [Kisuka]                   -
//- 1.3 Removed MaxLvl check in waitingrooms.                 -
//    - Replaced setwall with setcell.                        -
//- 1.4 Label standardization. [Euphy]                        -
//- 1.5 Added GM management function. [Euphy]                 -
//- 1.6 Aplicada tradução para português(pt_BR). [Cainho]     -
//-------------------------------------------------------------

// Waiting Room NPCs
//============================================================
bat_room,86,227,4	script	Tenente Ator	4_M_KY_KNT,{
	end;
OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_b01::OnReadyCheck",1,0,80;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",10,290,"start#bat_b01::OnGuillaumeQuit","");
	end;
}

bat_room,85,204,0	script	Tenente Thelokus	4_M_CRU_KNT,{
	end;
OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_b01::OnReadyCheck",1,0,80;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",390,10,"start#bat_b01::OnCroixQuit","");
	end;
}

bat_room,85,220,0	warp	back_bgroomb01a	1,1,bat_room,154,150
bat_room,85,211,0	warp	back_bgroomb01b	1,1,bat_room,154,150

bat_room,2,151,3	script	#bat_b01_timer	CLEAR_NPC,{
	end;

OnInit:
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	initnpctimer;
	set .@chk_bat_a01,getmapusers("bat_b01");
	if (.@chk_bat_a01 < 1) {
		set $@FlaviusBG1, 0;
		if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; set $@FlaviusBG1_id1, 0; }
		if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; set $@FlaviusBG1_id2, 0; }
		donpcevent "start#bat_b01::OnReadyCheck";
	}
	end;
}


// Flavius Battleground Engine
//============================================================
bat_b01,15,15,3	script	start#bat_b01	CLEAR_NPC,{
OnInit:
	mapwarp "bat_b01","bat_room",154,150;
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Tenente Ator");
	set .@Croix, getwaitingroomstate(0,"Tenente Thelokus");
	if( !.@Guillaume && !.@Croix ) {
		donpcevent "#bat_b01_timer::OnStop";
		end;
	}
	if( .@Guillaume < 10 || .@Croix < 10 )
		end;
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set $@Croix_ScoreBG1, 0;
	set $@Guill_ScoreBG1, 0;
	bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;

	donpcevent "Tenente Ator::OnEnterBG";
	donpcevent "Tenente Thelokus::OnEnterBG";
	donpcevent "OBJ#bat_b1_a::OnKill";
	donpcevent "OBJ#bat_b1_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardião#bat_b1_a::OnKill";
	donpcevent "guardião#bat_b01_b::OnKill";
	donpcevent "guardião#bat_b1_a::OnEnable";
	donpcevent "guardião#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b1_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Armada de Guillaume#b1_a";
	disablenpc "Armada de Croix#b01_b";
	disablenpc "Armada#bat_b1_aover";
	disablenpc "Armada#bat_b01_bover";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	donpcevent "countdown#bat_b01::OnEnable";
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b1_a::OnKill";
	donpcevent "OBJ#bat_b1_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardião#bat_b1_a::OnKill";
	donpcevent "guardião#bat_b01_b::OnKill";
	donpcevent "guardião#bat_b1_a::OnEnable";
	donpcevent "guardião#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b1_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Armada de Guillaume#b1_a";
	disablenpc "Armada de Croix#b01_b";
	disablenpc "Armada#bat_b1_aover";
	disablenpc "Armada#bat_b01_bover";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	end;

OnGuillaumeQuit:
OnCroixQuit:
	bg_leave;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_b01_timer::OnEnable";
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b1_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",61,150,"Cristal Vermelho",1915,"OBJ#bat_b1_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b1_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b1_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
		if ($@Croix_ScoreBG1 > 0) {
			set $@FlaviusBG1_Victory,2;
			set $@Croix_ScoreBG1,$@Croix_ScoreBG1+1;
			enablenpc "Armada de Guillaume#b1_a";
			enablenpc "Armada de Croix#b01_b";
			donpcevent "time#bat_b01::OnStop";
		}
		else {
			set $@Croix_ScoreBG1,1;
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
		}
		donpcevent "#bat_b01_timer::OnStop";
		bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		donpcevent "#bat_b01_timer::OnEnable";
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",328,150,"Cristal Azul",1914,"OBJ#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
		if ($@Guill_ScoreBG1 > 0) {
			set $@FlaviusBG1_Victory,1;
			set $@Guill_ScoreBG1,$@Guill_ScoreBG1+1;
			enablenpc "Armada de Guillaume#b1_a";
			enablenpc "Armada de Croix#b01_b";
			donpcevent "time#bat_b01::OnStop";
		}
		else {
			set $@Guill_ScoreBG1,1;
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
		}
		donpcevent "#bat_b01_timer::OnStop";
		bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		donpcevent "#bat_b01_timer::OnEnable";
	}
	end;
}

bat_b01,1,3,3	script	guardião#bat_b1_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,159,"Guardião de Guillaume",1949,"guardião#bat_b1_a::OnMyMobDead";
	bg_monster $@FlaviusBG1_id1,"bat_b01",108,141,"Guardião de Guillaume",1949,"guardião#bat_b1_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardião#bat_b1_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardião#bat_b1_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b1_a::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,3,3	script	guardião#bat_b01_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,160,"Guardião de Croix",1949,"guardião#bat_b01_b::OnMyMobDead";
	bg_monster $@FlaviusBG1_id2,"bat_b01",307,138,"Guardião de Croix",1949,"guardião#bat_b01_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_b01","guardião#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardião#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b1_a	CLEAR_NPC,{
OnRed:
	setcell "bat_b01",62,149,60,151,cell_basilica,1;
	setcell "bat_b01",62,149,60,151,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",62,149,60,151,cell_basilica,0;
	setcell "bat_b01",62,149,60,151,cell_walkable,1;
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	CLEAR_NPC,{
OnRed:
	setcell "bat_b01",327,151,329,149,cell_basilica,1;
	setcell "bat_b01",327,151,329,149,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",327,151,329,149,cell_basilica,0;
	setcell "bat_b01",327,151,329,149,cell_walkable,1;
	end;
}

bat_b01,1,6,1	script	time#bat_b01	CLEAR_NPC,{
OnEnable:
	donpcevent "Curandeira#b1_a::OnEnable";
	donpcevent "Curandeira#b01_b::OnEnable";
	end;

OnStop:
	donpcevent "Curandeira#b1_a::OnStop";
	donpcevent "Curandeira#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Curandeira#b1_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Curandeira]";
	mes "Agora, feche os olhos, e respire fundo.";
	mes "Vou curar sua dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b01_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Curandeira#b1_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Curandeira#b1_a";
	end;

OnStop:
	disablenpc "bat_b01_rp1_a_warp";
	disablenpc "Curandeira#b1_a";
	stopnpctimer;
	end;
}

bat_b01,10,290,0	script	bat_b01_rp1_a_warp	WARPNPC,10,10,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",87,73;
	end;
}

bat_b01,389,14,3	script	Curandeira#b01_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Curandeira]";
	mes "Agora, feche os olhos, e respire fundo.";
	mes "Vou curar sua dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_b01_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_b01_rp1_b_warp";
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Curandeira#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Curandeira#b01_b";
	end;

OnStop:
	disablenpc "bat_b01_rp1_b_warp";
	disablenpc "Curandeira#b01_b";
	stopnpctimer;
	end;
}

bat_b01,389,10,0	script	bat_b01_rp1_b_warp	WARPNPC,9,9,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",312,225;
	end;
}

bat_b01,87,76,0	script	A_CODE#bat_b01	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		setquest 2070;
	end;
}

bat_b01,312,224,0	script	B_CODE#bat_b01	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		setquest 2070;
	end;
}

bat_b01,10,294,3	script	Armada de Guillaume#b1_a	4_M_RASWORD,{
	if ($@FlaviusBG1_id1 == getcharid(4)) {
		if ($@FlaviusBG1_Victory == 1) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Armada de Guillaume#b1_a";
	end;
}

bat_b01,389,14,3	script	Armada de Croix#b01_b	4_M_RASWORD,{
	if ($@FlaviusBG1_id2 == getcharid(4)) {
		if ($@FlaviusBG1_Victory == 2) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Armada de Croix#b01_b";
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	CLEAR_NPC,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_b01", "Soldado Axl Rose: Vamos atacar para destruir aquele Cristal!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_b01", "Soldado de Croix: Mesmo que Guillaume se esforce para nos derrotar, a vitória será nossa. Vamos mostrar a eles nosso poder.",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_b01", "Marollo VII: Guillaume Marollo, Croix Marollo! E seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_b01", "Marollo VII: Ambos os lados competiram bem, e é difícil julgar qual deles é superior.",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_b01", "Marollo VII: Esta batalha de Flavius é uma perda de tempo. Eu decidirei quem será o vitorioso de acordo com seu progresso.",bc_map,"0x99CC00";
	end;

//OnTimer1822000:
//	mapannounce "bat_b01", "Marollo VII : If you can't accept the results, try again in another valley battle!",bc_map,"0x99CC00";
//	end;

OnTimer1825000:
	mapannounce "bat_b01", "Soldado Axl Rose: Sim, senhor.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	donpcevent "time#bat_b01::OnStop";
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	enablenpc "Armada#bat_b1_aover";
	enablenpc "Armada#bat_b01_bover";
	end;

OnTimer1900000:
	mapwarp "bat_b01","bat_room",154,150;
	donpcevent "countdown#bat_b01::OnStop";
	end;
}

bat_b01,81,83,3	script	Campo Guillaume#flag21	1_FLAG_LION,{ end; }
bat_b01,94,83,3	script	Campo Guillaume#flag22	1_FLAG_LION,{ end; }
bat_b01,81,66,3	script	Campo Guillaume#flag23	1_FLAG_LION,{ end; }
bat_b01,94,66,3	script	Campo Guillaume#flag24	1_FLAG_LION,{ end; }
bat_b01,139,142,3	script	Campo Guillaume#flag25	1_FLAG_LION,{ end; }
bat_b01,139,158,3	script	Campo Guillaume#flag26	1_FLAG_LION,{ end; }
bat_b01,110,161,3	script	Campo Guillaume#flag27	1_FLAG_LION,{ end; }
bat_b01,110,137,3	script	Campo Guillaume#flag28	1_FLAG_LION,{ end; }
bat_b01,63,135,3	script	Campo Guillaume#flag29	1_FLAG_LION,{ end; }
bat_b01,63,165,3	script	Campo Guillaume#flag30	1_FLAG_LION,{ end; }
bat_b01,10,296,3	script	Campo Guillaume#flag31	1_FLAG_LION,{ end; }

bat_b01,306,233,3	script	Campo Croix#flag21	1_FLAG_EAGLE,{ end; }
bat_b01,317,233,3	script	Campo Croix#flag22	1_FLAG_EAGLE,{ end; }
bat_b01,306,216,3	script	Campo Croix#flag23	1_FLAG_EAGLE,{ end; }
bat_b01,317,216,3	script	Campo Croix#flag24	1_FLAG_EAGLE,{ end; }
bat_b01,257,158,3	script	Campo Croix#flag25	1_FLAG_EAGLE,{ end; }
bat_b01,257,141,3	script	Campo Croix#flag26	1_FLAG_EAGLE,{ end; }
bat_b01,297,164,3	script	Campo Croix#flag27	1_FLAG_EAGLE,{ end; }
bat_b01,297,136,3	script	Campo Croix#flag28	1_FLAG_EAGLE,{ end; }
bat_b01,336,161,3	script	Campo Croix#flag29	1_FLAG_EAGLE,{ end; }
bat_b01,336,139,3	script	Campo Croix#flag30	1_FLAG_EAGLE,{ end; }
bat_b01,389,16,3	script	Campo Croix#flag31	1_FLAG_EAGLE,{ end; }

bat_b01,10,294,3	script	Armada#bat_b1_aover	4_M_KY_HEAD,{
	set .@A_B_gap,$@Guill_ScoreBG1 - $@Croix_ScoreBG1;
	if ($@FlaviusBG1_id1 == getcharid(4)) {
		if (.@A_B_gap > 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Bênçãos para Guillaume!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else if (.@A_B_gap == 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Axl Rose]";
				mes "Você perdeu, mas se dedicou à batalha.";
				mes "Aqui está uma recompensa pela sua grande dedicação a Guillaume Marollo!";
				mes "Aceite esta derrota como lição, e vencerá no futuro.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	else {
		mes "[Axl Rose]";
		mes "Porque você está aqui, mercenário Croix? Estou definitivamente certo da vitória contra o Croix tolo como você. Ha!";
		close;
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Armada#bat_b1_aover";
	end;
}

bat_b01,389,14,3	script	Armada#bat_b01_bover	4_M_CRU_HEAD,{
	set .@A_B_gap,$@Guill_ScoreBG1 - $@Croix_ScoreBG1;
	if ($@FlaviusBG1_id2 == getcharid(4)) {
		if (.@A_B_gap > 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else if (.@A_B_gap == 0) {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7829,3; //BF_Badge2
			}
			else {
				mes "[Armada]";
				mes "Ah, " + strcharinfo(0) + " não fique triste.";
				mes "Mesmo sem vencer, fizemos nosso melhor.";
				mes "Aqui está um presente real de Croix, para que não se esqueça dessa batalha. Venceremos a próxima.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
		else {
			set .@your_medal,countitem(7829);
			set .@medal_gap,500 - .@your_medal;
			if (.@medal_gap > 8) {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,9; //BF_Badge2
			}
			else {
				mes "[Armada]";
				mes "Bênçãos para Croix!";
				mes "Vamos aproveitar nossa gloriosa vitória!";
				mes "" + strcharinfo(0) + ", aqui está o emblema da sua vitória.";
				close2;
				getitem 7829,.@medal_gap; //BF_Badge2
			}
		}
	}
	else {
		mes "[Swandery]";
		mes "Por que você vem aqui como Guillaume? Você será enviado para onde você deveria estar!";
		close;
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Armada#bat_b01_bover";
	end;
}

bat_b01,1,10,3	script	Liberar todos#b01	4_DOG01,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Posso ajudá-lo?";
		next;
		switch (select ("Liberar todos.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_b01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}
