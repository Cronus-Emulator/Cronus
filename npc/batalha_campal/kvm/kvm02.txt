//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//- Batalha Campal Sistema- KvM 1-59                          -
//-------------------------------------------------------------
//- Por:                                                      -
//- ???, L0ne_W0lf                                            -
//-------------------------------------------------------------
//- Versão Atual:                                             -
//- 1.4                                                       -
//-------------------------------------------------------------
//- Compatível Com:                                           -
//- Cronus                                                    -
//-------------------------------------------------------------
//- Descrição:                                                -
//- [Official Conversion]                                     -
//- Kreiger Von Midgard Battleground for levels 1 to 59       -
//- - Winning Team: 1 point                                   -
//- - Losing Team: 0 points                                   -
//-------------------------------------------------------------
//- Comentários Adicionais:                                   -
//- 1.0 First Version.                                        -
//- 1.1 Updated using official Aegis files. [L0ne_W0lf]       -
//- 1.2 Upated some announces and dialogs from iRO.           -
//-     Changed how the scoreboard works slightly.            -
//-     Removed the areapercentheals, and minor things.       -
//- 1.3 Fixed wrong names for disablenpc/enablenpc. [Ai4rei]  -
//-     Fixed points in text and actual points differing.     -
//- 1.4 Aplicada Tradução (PT-BR). [SkyHD]                    -
//-------------------------------------------------------------

// Sala de Espera
//============================================================
bat_room,197,226,5	script	Sala de Espera do KVM#a2::KvM02R_Guillaume	4_M_KY_KNT,{
	end;

OnInit:
	waitingroom "Estação de batalha 5 Jogadores",6,"KvM02_BG::OnGuillaumeJoin",1;
	end;
	
OnEnterBG:
	set $@KvM02BG_id1, waitingroom2bg("bat_c02",52,129,"KvM02_BG::OnGuillaumeQuit","KvM02_BG::OnGuillaumeDie");
	end;
}

bat_room,197,220,0	warp	#kvm601	2,2,bat_room,154,150

bat_room,197,205,3	script	Sala de Espera do KVM#b2::KvM02R_Croix	4_M_CRU_KNT,{
	end;

OnInit:
	waitingroom "Estação de batalha 5 Jogadores",6,"KvM02_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM02BG_id2, waitingroom2bg("bat_c02",147,55,"KvM02_BG::OnCroixQuit","KvM02_BG::OnCroixDie");
	end;
}

bat_room,197,211,0	warp	#kvm602	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM02::CellEffect2	-1,{
	end;

OnKvM02One:
	specialeffect EF_CHANGECOLD;
	end;

OnKvM02Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c02,54,124,3	duplicate(CellEffect2)	#RedcellA1-2	HIDDEN_WARP_NPC
bat_c02,55,124,3	duplicate(CellEffect2)	#RedcellA2-2	HIDDEN_WARP_NPC
bat_c02,56,124,3	duplicate(CellEffect2)	#RedcellA3-2	HIDDEN_WARP_NPC
bat_c02,57,124,3	duplicate(CellEffect2)	#RedcellA4-2	HIDDEN_WARP_NPC
bat_c02,57,125,3	duplicate(CellEffect2)	#RedcellA5-2	HIDDEN_WARP_NPC
bat_c02,57,126,3	duplicate(CellEffect2)	#RedcellA6-2	HIDDEN_WARP_NPC
bat_c02,57,127,3	duplicate(CellEffect2)	#RedcellA7-2	HIDDEN_WARP_NPC

bat_c02,145,59,3	duplicate(CellEffect2)	#RedcellB1-2	HIDDEN_WARP_NPC
bat_c02,144,59,3	duplicate(CellEffect2)	#RedcellB2-2	HIDDEN_WARP_NPC
bat_c02,143,59,3	duplicate(CellEffect2)	#RedcellB3-2	HIDDEN_WARP_NPC
bat_c02,142,59,3	duplicate(CellEffect2)	#RedcellB4-2	HIDDEN_WARP_NPC
bat_c02,142,56,3	duplicate(CellEffect2)	#RedcellB5-2	HIDDEN_WARP_NPC
bat_c02,142,57,3	duplicate(CellEffect2)	#RedcellB6-2	HIDDEN_WARP_NPC
bat_c02,142,58,3	duplicate(CellEffect2)	#RedcellB7-2	HIDDEN_WARP_NPC

bat_c02,54,128,3	script	#A_camp_start02	HIDDEN_WARP_NPC,4,4,{
	end;

OnInit:
	disablenpc "#A_camp_start02";
	end;

OnEnable:
	enablenpc "#A_camp_start02";
	end;

OnDisable:
	disablenpc "#A_camp_start03";
	end;

OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c02,146,56,3	script	#B_camp_start02	HIDDEN_WARP_NPC,4,4,{
	end;

OnInit:
	disablenpc "#B_camp_start02";
	end;

OnEnable:
	enablenpc "#B_camp_start02";
	end;

OnDisable:
	disablenpc "#B_camp_start02";
	end;

OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM02_BG	-1,{
	end;

OnInit:
	setwall "bat_c02",54,122,6,7,0,"batc02wall_a";
	setwall "bat_c02",55,122,5,7,0,"batc02wall_b";
	setwall "bat_c02",140,56,6,7,0,"batc02wall_c";
	setwall "bat_c02",140,57,5,7,0,"batc02wall_d";
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM02_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM02BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM02_BG::OnCroixWin";
		else {
			mapannounce "bat_c02", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Croixes é  "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM02BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM02_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c02", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O número de Croixes é  "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM02BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM02R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM02R_Croix");

	if( .@Guillaume < 5 || .@Croix < 5 )
		end;

	set $@KvM02BG, 1; // Starting
	donpcevent "KvM02R_Croix::OnEnterBG";
	donpcevent "KvM02R_Guillaume::OnEnterBG";
	donpcevent "KvM02_BG::OnStart";
	end;

OnStart:
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	set $@KvM02BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Em 1 minuto, KVM vai começar.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O tempo máximo para uma batalha KVM é de 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c02", "Por favor, se prepare para a batalha KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Você já pode abençoar seus aliados.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start02::OnEnable";
	donpcevent "#B_camp_start02::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start02::OnDisable";
	donpcevent "#B_camp_start02::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c02", "30 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c02", "15 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c02", "10 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c02", "5 segundos restantes para começar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c02", "KVM está agora começando.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM02BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM02BG_id2, 0);
	if( .Guillaume_Count < 5 || .Croix_Count < 5 )
	{
		set $@KvM02BG_Victory, 3;
		set $@KvM02BG, 3;
		mapannounce "bat_c02","Não há jogadores suficientes para iniciar a batalha",1,0x808080;
		stopnpctimer;
		donpcevent "KvM02_BG::OnStop";
		end;
	}
	set $@KvM02BG, 2; // Playing
	bg_warp $@KvM02BG_id1,"bat_c02",62,119;
	bg_warp $@KvM02BG_id2,"bat_c02",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c02", "1 minutos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c02", "30 minutos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c02", "15 minutos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c02", "10 minutos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c02", "5 minutos restantes para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c02", "A batalha KVM acabou.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM02_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM02_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		mapannounce "bat_c02", "O número de Guillaumes é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "O número de Croixes é  "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "Esta batalha terminou em um empate.",bc_map,"0x00ff00";
		donpcevent "KvM02_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	mapannounce "bat_c02", "Guillaume venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros de Guillaume.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão enviados ao ponto de início.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	mapannounce "bat_c02", "Croix venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros de Croix.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão enviados ao ponto de início.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "KVM Officer#KVM02A";
	enablenpc "KVM Officer#KVM02B";
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	donpcevent "KvM02_BG_Out::OnBegin";
	end;

OnReset:
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set $@KvM02BG_Victory, 0;
	if( $@KvM02BG_id1 ) { bg_destroy $@KvM02BG_id1; set $@KvM02BG_id1, 0; }
	if( $@KvM02BG_id2 ) { bg_destroy $@KvM02BG_id2; set $@KvM02BG_id2, 0; }
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	mapwarp "bat_c02","bat_room",154,150;
	maprespawnguildid "bat_c02",0,3; // Just in case someone else
	bg_updatescore "bat_c02",5,5;
	set $@KvM02BG, 0;
	donpcevent "KvM02_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM02_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Por favor fale com o Oficial para adquirir seus Pontos do KvM.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O Oficial irá conceder-lhe os pontos por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Em 30 segundos, o oficial será mandado embora.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c02", "A menos que você falar com o oficial, você não pode ganhar os pontos.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Por favor, tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c02", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	donpcevent "KvM02_BG::OnReset";
	end;
}

// Battleground rewards
//============================================================
bat_c02,51,130,5	script	KVM Officer#KVM02A	4_M_KY_HEAD,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM02BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 1;
			mes "[KVM Officer]";
			mes "Ótima batalha!";
			mes "Que a glória do KvM esteja com você.";
			mes "Você adquiriu os pontos dos vencedores: 1";
			close2;
		} else {
			mes "[KVM Officer]";
			mes "Eu sinto muito."; 
			mes "Desejo-lhe sorte na próxima vez."; 
			mes "Você adquiriu os pontos dos perdedores: 0";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}

bat_c02,148,53,1	script	KVM Officer#KVM02B	4_M_CRU_HEAD,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM02BG_Victory == Bat_Team )
		{ // Victory
			set kvm_point,kvm_point + 1;
			mes "[KVM Officer]";
			mes "Ótima batalha!";
			mes "Que a glória do KvM esteja com você.";
			mes "Você adquiriu os pontos dos vencedores: 1";
			close2;
		} else {
			mes "[KVM Officer]";
			mes "Eu sinto muito."; 
			mes "Desejo-lhe sorte na próxima vez."; 
			mes "Você adquiriu os pontos dos perdedores: 0";
			close2;
		}
		bg_leave;
		set Bat_Team,0;
		warp "bat_room",154,150;
		end;
	}
	end;
}
