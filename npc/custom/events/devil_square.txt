//===== Cronus Script ========================================
//= Devil Square
//===== Por: =================================================
//= Evanio
//===== Versão Atual: ========================================
//= 1.0
//===== Descrição: ===========================================
//= Evento Devil Square.
//===== Comentários Adicionais: ==============================
//= 1.0 Script Criado. [Evanio]
//============================================================

-	script	DevilConfs	-1,{
 
// Configurações
OnInit:
	deletearray $@PremioDS;
	deletearray $@QtdDS;
	deletearray $@TimerDS;
	deletearray $@ChatMsgsDS$;
	deletearray $@LevelEnterDSList;
	deletearray $@DSMap$;
	deletearray $@DSMapFlags;
	setarray $@PremioDS[0],674,7549;              // Premiações. Irá sortear um único ID [ID1,ID2,ID3,...]
	setarray $@QtdDS[0],1,2;         	      // Quantidade referente a premiação à cima. [quantidade1,quantidade2,quantidade3,...]
	setarray $@TimerDS[0],10,30,15,30,20,30;	      // Configuração dos horários de incio do evento. Exemplo: "hora1,minutos1,hora2,minutos2,hora3,minutos3..."
	set $@EntradaDS,1;                            // Anuncia para o servidor que o jogador entrou no Devil Square [0 = Não, 1 = Sim]
	set $@PlayersDS,60;                            // Máximo de jogadores que podem participar do evento
	set $@ChatOnDS,1;                             // Mostra a sala de espera em cima do npc [0 = Não, 1 = Sim]
	setarray $@ChatMsgsDS$[0],"O Devil Square está aberto!","Venha e derrote todos os Chefes!","Só falta você nessa luta!";  // Mensagens que aparecerão no chat ["msg","msg2",...]
	set $@LevelEnterDS,1;                         // Ativa a limitação de level para entrada [0 = Não, 1 = Sim]
	setarray $@LevelEnterDSList[0],50,150;        // Limite de level (Min e Max) [LvLMin,LvLMax]

//MapFlags
	setarray $@DSMap$[0],"bossnia_01","guild_vs5","1@def02","guild_vs4","1@def03","1@tnm3";
	setarray $@DSMapFlags[0],0,2,3,13,34,49;
		for(.@i=0;.@i<getarraysize($@DSMapFlags); .@i++){
			setmapflag $@DSMap$[.@i],$@DSMapFlags[.@i];
		}
}

prontera,146,171,3	script	Devil Square	998,{

OnStart:
	mes "[ Guarda Devil Square ]";
	mes "Bem vindo ao portal do ^FF0000Devil Square^000000 !";
	if(!$DSIniciado) {
		mes "O Devil Square está fechado agora.";
		mes "Aguarde o anúncio do Evento.";
		close;
	}
		mes "o Devil Square está aberto!";
		mes "Está pronto para a batalha? ";
		next;
		switch(select("Estou pronto!","Informações","Não!")) {

		case 1:
			if(getmapusers("ordeal_1-1")>=$@PlayersDS) {
				mes "[ Guarda Devil Square ]";
				mes "Desculpe, o Devil Square está cheio";
				close;
			}
			if($@LevelEnterDS) {
			if(BaseLevel < $@LevelEnterDSList[0] || BaseLevel > $@LevelEnterDSList[1]) {
				mes "[ Guarda Devil Square ]";
				mes "Desculpe, você não pode entrar no Devil Square pois não preenche os requesitos necessários.";
				mes " ";
				mes "Nível mínimo para entrar é "+$@LevelEnterDSList[0];
				mes "Nível máximo para entrar é "+$@LevelEnterDSList[1];
				close;
				}
			}
			if($@EntradaDS) announce "[ Devil Square ] : [ "+strcharinfo(0)+" ] entrou no Devil Square.",8;
			$@CharReg[$@dcn] = getcharid(3);
			$@dcn++;
			percentheal 100,100;
			warp "bossnia_01",136,118;
			end;
		case 2:
			mes "[ Guarda Devil Square ]";
			mes "No Devil Square há 6 'rounds' de dificuldade.";
			mes "Cada 'round' ultrapassado, a dificuldade aumenta.";
			mes "Lembrando que não é fácil. Por isso, prepare-se com poções, itens e cartas.";
			mes "Caso você termine o Devil Square, fale com o NPC 'Saída' para ganhar seu devido prêmio!";
			close;

		case 3:
			mes "[ Guarda Devil Square ]";
			mes "Eu lhe aguardarei. Volte assim que estiver preparado.";
			close;
	}

OnInit:
	if($@ChatOnDS==1 && $DSIniciado==1) {
		if(getarraysize($@ChatMsgsDS$)<2)
			{ waitingroom $@ChatMsgsDS$[.ChatC],0; end;}
		else
			set .ChatC,0;
			waitingroom $@ChatMsgsDS$[.ChatC],0;
				
		}
OnTimer4000:
	if($@ChatOnDS==1 && $DSIniciado==1){
		delwaitingroom;
			if(.ChatC==(getarraysize($@ChatMsgsDS$)-1)) set .ChatC,0;
		else
			set .ChatC,.ChatC+1;
   			waitingroom $@ChatMsgsDS$[.ChatC],0;	
	}
	if($DSIniciado!=1)delwaitingroom;
	initnpctimer;
}

-	script	LoopDS	-1,{

OnInit:			
OnTimer1000:
	for(.@i=0;.@i<getarraysize($@TimerDS); .@i++){
		if(gettime(3)==$@TimerDS[.@i] && gettime(2)==$@TimerDS[.@i+1]) {	
			disablenpc "1DevilGate"; disablenpc "2DevilGate"; disablenpc "3DevilGate"; disablenpc "4DevilGate"; disablenpc "5DevilGate"; disablenpc "6DevilGate";
			$@dcn = 0; deletearray $@CharReg; $DSIniciado = 1; $@time = 1; donpcevent "#DSCtrl1::OnTimer60000"; end;
		}
	}
	initnpctimer;
}

-	script	#DSCtrl1	-1,{

OnTimer60000:
	if(!$@time) {
		announce "[ Devil Square ] : O Devil Square COMEÇOU!",8;
		$DSIniciado = 0; 
		donpcevent "#DSCtrl2::OnWave1";
		end;
	}
	.@Ann$ = "[ Devil Square ] : O portal para o Devil Square está aberto, e em "+$@time+" minuto"+(($@time>1)?"s":"")+" ele começará!";
	announce .@Ann$,8;
	$@time = $@time - 1;
	initnpctimer;
}

-	script	#DSCtrl2	-1,{

	OnWave1:
		$@DSMob = 75;
		callfunc "Devil_Monster",1,"bossnia_01","#DSCtrl2::OnMobDead";
		end;

	OnWave2:
		$@DSMob = 25;
		callfunc "Devil_Monster",2,"guild_vs5","#DSCtrl2::OnMobDead","Invasão Sombria";
		end;

	OnWave3:
		$@DSMob = 21;
		callfunc "Devil_Monster",3,"1@def02","#DSCtrl2::OnMobDead","Torre Maldita";
		end;

	OnWave4:
		$@DSMob = 36;
		callfunc "Devil_Monster",4,"guild_vs4","#DSCtrl2::OnMobDead","Era do Gelo";
		end;

	OnWave5:
		$@DSMob = 20;
		callfunc "Devil_Monster",5,"1@def03","#DSCtrl2::OnMobDead","Vulcão Furioso";
		end;

	OnWave6:
		$@DSMob = 29;
		callfunc "Devil_Monster",6,"1@tnm3","#DSCtrl2::OnMobDead","Ressurreição do Demônio";
		end;

	OnMobDead:
		if($@level == 6) {
			$@DSMob = $@DSMob-1;
			if($@DSMob == 25) mapannounce $@map$,"Devil Square: 25 monstros para vitória!",8;
			if($@DSMob == 5) mapannounce $@map$,"Devil Square: 5 monstros para vitória!",8;
			if($@DSMob == 0) {
				.@AnnE$ = "[ Devil Square ] : Parabéns! Você"+((getmapusers($@map$)>1)?"s sobreviveram":" sobreviveu")+" ao Devil Square! Aqui está seu prêmio!";
				mapannounce $@map$,.@AnnE$,8;
				enablenpc $@level+"DevilGate";
				for(.@i=0;.@i<getarraysize($@CharReg); .@i++){
					attachrid($@CharReg[.@i]);
					if($@map$ == strcharinfo(3)) {
						.@r = rand(getarraysize($@PremioDS));
						getitem $@PremioDS[.@r],$@QtdDS[.@r];
					}
				}
				for(.@j=0;.@j<getarraysize($@DSMap$); .@j++){ mapannounce $@DSMap$[.@j],"[ Devil Square ] : Em instantes todos os jogadores que estão no mapa irão ser teleportados para fora dele.",8; }
				sleep 30000;
				for(.@k=0;.@k<getarraysize($@DSMap$); .@k++){ mapwarp $@DSMap$[.@k],"prontera",156,191,0; }
			}
		end;
		}
		$@DSMob = $@DSMob-1;
		if($@DSMob == 5) mapannounce $@map$,"[ Devil Square ] : 5 monstros para começar o "+($@level+1)+"º round",8;
		if($@DSMob == 0) {
			mapannounce $@map$,"[ Devil Square ] : O Portal para o próximo Nível está aberto.",8;
			enablenpc $@level+"DevilGate";
			sleep 20000;
			donpcevent strnpcinfo(3)+"::OnWave"+($@level+1);
		}
}

bossnia_01,130,118,0	script	1DevilGate	WARPNPC,2,2,{
	end;

OnTouch:
	callfunc "Devil_Warp",
		atoi(replacestr(strnpcinfo(0),"DevilGate","")) + 1,
		$@DSMap$[atoi(replacestr(strnpcinfo(0),"DevilGate",""))];
}

guild_vs5,49,77,0	duplicate(1DevilGate)	2DevilGate	WARPNPC,2,2
1@def02,52,67,0	duplicate(1DevilGate)	3DevilGate	WARPNPC,2,2
guild_vs4,49,91,0	duplicate(1DevilGate)	4DevilGate	WARPNPC,2,2
1@def03,45,35,0	duplicate(1DevilGate)	5DevilGate	WARPNPC,2,2
1@tnm3,97,90,0	duplicate(1DevilGate)	6DevilGate	WARPNPC,2,2

function	script	Devil_Warp	{

	$@level1 = getarg(0);
	$@map$ = getarg(1);

	switch($@level1) {
	case 2: warp $@map$,50,23; break;
	case 3: warp $@map$,29,35; break;
	case 4: warp $@map$,50,7; break;
	case 5: warp $@map$,79,110; break;
	case 6: warp $@map$,97,7; break;
	case 7: warp "prontera",156,191; break;
	}
	return;	
}


function	script	Devil_Monster	{

	$@level = getarg(0);
	$@map$ = getarg(1);
	$@label$ = getarg(2);
	if($@level != 1) mapannounce $@map$,"Devil Square: começou o "+$@level+"º round! Nível "+getarg(3),8;

	switch($@level) {
	case 1:
		monster $@map$,0,0,"Gárgula",1253,15,$@label$;
		monster $@map$,0,0,"Alarme",1193,15,$@label$;
		monster $@map$,0,0,"Injustiçado",1257,15,$@label$;
		monster $@map$,0,0,"Mímico",1191,15,$@label$;
		monster $@map$,0,0,"Alma Penada",1192,15,$@label$;
		break;
	case 2:
		monster $@map$,0,0,"Egnigem Cenia",1652,4,$@label$;
		monster $@map$,0,0,"Wickebine Tres",1653,4,$@label$;
		monster $@map$,0,0,"Armeyer Dinze",1654,4,$@label$;
		monster $@map$,0,0,"Errende Ebecee",1655,4,$@label$;
		monster $@map$,0,0,"Kavach Icarus",1656,4,$@label$;
		monster $@map$,0,0,"Laurell Weinder",1657,4,$@label$;
		monster $@map$,46,72,"Arquimaga Kathryne",1621,1,$@label$;
		break;
	case 3:
		monster $@map$,0,0,"Odium",1704,5,$@label$;
		monster $@map$,0,0,"Despero",1705,5,$@label$;
		monster $@map$,0,0,"Maero",1706,5,$@label$;
		monster $@map$,0,0,"Pesar",1707,5,$@label$;
		monster $@map$,45,70,"Memória de Thanatos",1708,1,$@label$;
		break;
	case 4:
		monster $@map$,0,0,"Yeti",1775,5,$@label$;
		monster $@map$,0,0,"Siroma",1776,5,$@label$;
		monster $@map$,0,0,"Titã de Gelo",1777,5,$@label$;
		monster $@map$,0,0,"Gazeti",1778,5,$@label$;
		monster $@map$,0,0,"Filhote de Hatii",1515,15,$@label$;
		monster $@map$,47,52,"Ktullanux",1779,1,$@label$;
		break;
	case 5:
		monster $@map$,0,0,"Espadachim Guardião",1829,2,$@label$;
		monster $@map$,0,0,"Arqueiro Guardião",1830,2,$@label$;
		monster $@map$,0,0,"Salamandra",1831,5,$@label$;
		monster $@map$,0,0,"Kasa",1833,5,$@label$;
		monster $@map$,0,0,"Imp",1837,5,$@label$;
		monster $@map$,46,42,"Ifrit",1832,1,$@label$;
		break;
	case 6:
		areamonster $@map$,51,76,74,41,"Sombra da Inveja",1918,7,$@label$;
		areamonster $@map$,51,76,74,41,"Sombra da Gula",1919,7,$@label$;
		areamonster $@map$,121,74,143,42,"Sombra da Vaidade",1920,7,$@label$;
		areamonster $@map$,121,74,143,42,"Sombra da Ira",1921,7,$@label$;
		areamonster $@map$,93,78,103,72,"Imperador Morroc",1916,1,$@label$;
		break;
	}
	return;
}