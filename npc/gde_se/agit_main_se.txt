//-------------------------------------------------------------
//-     _________                                             -
//-     \_   ___ \_______  ____   ____  __ __  ______         -
//-     /    \  \/\_  __ \/    \ /    \|  |  \/  ___/         -
//-     \     \____|  | \(  ( ) )   |  \  |  /\___ \          -
//-      \______  /|__|   \____/|___|  /____//____  >         -
//-             \/                   \/           \/          -
//-                 www.cronus-emulator.com                   -
//-------------------------------------------------------------
//-            Estrutura da Guerra do Emperium SE             -
//-------------------------------------------------------------
//- Por:                                                      -
//-  L0ne_W0lf, Zephyrus, Brian, Euphy e Cookie.              -
//-------------------------------------------------------------
//- Versão: 1.4b                                              -
//-------------------------------------------------------------
//- Descrição:                                                -
//-   Como agit_main, este arquivo é nescessário para as      -
//-   funções dos castelos SE.                                -
//-------------------------------------------------------------
//- Desenvolvimento:                                          -
//-     1.4b - Realizado tradução do script e adaptações.     -
//-            nescesárias. [SoulBlaker]                      -
//-------------------------------------------------------------

// Núclero, ativa todos os outros eventos
//-------------------------------------------------------------
-	script	Manager#template	-1,{
OnAgitInit2:
OnRecvCastle2:
	if (strnpcinfo(2) == "template") end;
	if (!getcastledata(strnpcinfo(2),1)) {
		donpcevent strnpcinfo(0)+"::OnStart";
		// Invoca todos os monstros iguais para todos os castelos.
		monster strnpcinfo(2),0,0,"Druida Maligno",1117,10;
		monster strnpcinfo(2),0,0,"Khalitzburg",1132,4;
		monster strnpcinfo(2),0,0,"Cavaleiro do Abismo",1219,3;
		monster strnpcinfo(2),0,0,"Executor",1205,1;
		monster strnpcinfo(2),0,0,"Penomena",1216,10;
		monster strnpcinfo(2),0,0,"Alarme",1193,18;
		monster strnpcinfo(2),0,0,"Relógio",1269,9;
		monster strnpcinfo(2),0,0,"Raydric Arqueiro",1276,12;
		monster strnpcinfo(2),0,0,"Andarilho",1208,3;
		monster strnpcinfo(2),0,0,"Alice",1275,1;
		monster strnpcinfo(2),0,0,"Cavaleiro Sanguinário",1268,2;
		monster strnpcinfo(2),0,0,"Senhor das Trevas",1272,2;
		monster strnpcinfo(2),0,0,"Gerente",1270,4;
	}
	if (getcastledata(strnpcinfo(2),9) < 1)
		disablenpc "Funcionária Kafra#"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	end;

OnAgitStart2:
	if (strnpcinfo(2) == "template") end;
	if (agitcheck2()) {
		maprespawnguildid strnpcinfo(2),getcastledata(strnpcinfo(2),1),2;
		gvgon strnpcinfo(2);
		donpcevent strnpcinfo(0)+"::OnStart";
	}
	else for(set .@i,0; .@i<4; set .@i,.@i+1)
		donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnDisable";
	end;

OnAgitEnd2:
	if (strnpcinfo(2) == "template") end;
	gvgoff strnpcinfo(2);
	if (getcastledata(strnpcinfo(2),1)) {
		set .@str$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
		killmonster strnpcinfo(2),"Gerente#"+.@str$+"::OnStartArena";
		donpcevent strnpcinfo(0)+"::OnReset";
		donpcevent "Gerente#"+.@str$+"::OnStop";
	}
	end;

OnGuildBreak:
	if (strnpcinfo(2) == "template") end;
	killmonster strnpcinfo(2),"gard1#"+strnpcinfo(2)+"::OnGuardianDied";
	killmonster strnpcinfo(2),"gard2#"+strnpcinfo(2)+"::OnGuardianDied";
	disablenpc "Kafra Employee#"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setcastledata strnpcinfo(2),1,0;
	sleep 7000;
	announce "A base do Clã ["+getcastlename(strnpcinfo(2))+"] foi abandonado.",0;
	donpcevent strnpcinfo(0)+"::OnRecvCastle2";
	end;

OnStart:
	// $agit_ar0x[] - $agit_sc0x[]
	// 1ª Runa Guardiã, 2ª Runa Guardiã, Barricada 1, Barricada 2, Barricada 3, Invocar Guardiões
	// Configurações para todas invocações de Guardiões: 0 = Está bem | 1 = Destruído | 2 = Reparando
	// Invocação de Guardiões: 0 = Não Invocar | 1 = Invocar
	if (getcastledata(strnpcinfo(2),1)) {
		setarray getd("$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"[0]"),0,0,0,0,0,0;
		donpcevent "df1#"+strnpcinfo(2)+"::OnEnable";
		donpcevent "df2#"+strnpcinfo(2)+"::OnEnable";
		for(set .@i,0; .@i<4; set .@i,.@i+1)
			donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnEnable";
	}

OnEmpSpawn:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	if (mobcount(strnpcinfo(2),"Gerente#"+.@str$+"::OnStartArena")) end;
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") setarray .@i[0],87,219;
		else if (strnpcinfo(2) == "arug_cas02") setarray .@i[0],89,256;
		else setarray .@i[0],141,293;	// Castelos 3, 4 e 5 é identicos.
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") setarray .@i[0],162,193;
		else if (strnpcinfo(2) == "schg_cas03") setarray .@i[0],338,202;
		else setarray .@i[0],120,272;	// Castelos 1, 4 e 5 é identicos.
	}
	monster strnpcinfo(2),.@i[0],.@i[1],"Emperium",1288,1,"Gerente#"+.@str$+"::OnStartArena";
	end;

OnReset:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	donpcevent "df1#"+strnpcinfo(2)+"::OnDisable";
	donpcevent "df2#"+strnpcinfo(2)+"::OnDisable";
	donpcevent "gard1#"+strnpcinfo(2)+"::OnReset";
	donpcevent "gard2#"+strnpcinfo(2)+"::OnReset";
	donpcevent "1ª Runa Guardiã#"+.@str$+"::OnDisable";
	donpcevent "2ª Runa Guardiã#"+.@str$+"::OnDisable";
	for(set .@i,1; .@i<4; set .@i,.@i+1)
		donpcevent "Disp. de Controle 0"+.@i+"#"+.@str$+"::OnDisable";
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		donpcevent "RL"+.@i+"#"+strnpcinfo(2)+"::OnDisable";
	if (agitcheck2())
		setarray getd("$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"[0]"),0,0,1,1,1,0;
	end;

OnChange:
	set .@str$, substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setarray getd("$agit_"+.@str$+"[0]"),2,2,1,1,2,0;
	donpcevent strnpcinfo(0)+"::OnEmpSpawn";
	donpcevent "Disp. de Controle 03#"+.@str$+"::OnEnable";
	donpcevent "1ª Runa Guardiã#"+.@str$+"::OnEnable";
	donpcevent "2ª Runa Guardiã#"+.@str$+"::OnEnable";
	end;

OnClock0001:
	// Invoca Arcas do Tesouro com base na Economia do castelo.
	if (strnpcinfo(2) == "template") end;
	if (!getcastledata(strnpcinfo(2),1)) end;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnTreasureDied";
	if (getcastledata(strnpcinfo(2),4)) {
		set .@Economy,getcastledata(strnpcinfo(2),2);
		setcastledata strnpcinfo(2),2,.@Economy+getcastledata(strnpcinfo(2),4)+(rand(2) && getgdskilllv(getcastledata(strnpcinfo(2),1),10014));
		if (getcastledata(strnpcinfo(2),2) > 100) setcastledata strnpcinfo(2),2,100;
		setcastledata strnpcinfo(2),4,0;
	}
	if (getcastledata(strnpcinfo(2),5)) {
		set .@defence,getcastledata(strnpcinfo(2),3);
		setcastledata strnpcinfo(2),3,.@defence+getcastledata(strnpcinfo(2),5);
		if (getcastledata(strnpcinfo(2),3) > 100) setcastledata strnpcinfo(2),3,100;
		setcastledata strnpcinfo(2),5,0;
	}
	set .@Treasure,getcastledata(strnpcinfo(2),2)/5+4;
	if (!.@Treasure) end;
	freeloop(1);
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") {
			set .@treasurebox,1943;
			setarray .@treasurex[0],251,252,253,254,255,256,257,258,251,252,253,254,255,256,257,258,251,252,253,254,255,256,257,258;
			setarray .@treasurey[0],369,369,369,369,368,368,368,368,367,367,367,367,366,366,366,366,365,365,365,365,364,364,364,364;
		}
		else if (strnpcinfo(2) == "arug_cas02") {
			set .@treasurebox,1944;
			setarray .@treasurex[0],382,383,384,385,386,387,384,385,386,387,388,389,382,383,384,385,386,387,384,385,386,387,388,389;
			setarray .@treasurey[0],231,231,231,231,231,231,230,230,230,230,230,230,225,225,225,225,225,225,224,224,224,224,224,224;
		}
		else { 	// Castelos 3, 4 e 5 são identicos, exceto o tesouro 4.
			set .@treasurebox,(strnpcinfo(2) == "arug_cas04")?1946:1945;
			setarray .@treasurex[0],291,292,293,294,295,296,293,294,295,296,297,298,291,292,293,294,295,296,293,294,295,296,297,298;
			setarray .@treasurey[0],276,276,276,276,276,276,274,274,274,274,274,274,272,272,272,272,272,272,269,269,269,269,269,269;
		}
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") {
			set .@treasurebox,1939;
			setarray .@treasurex[0],249,250,251,252,253,246,247,248,249,250,250,251,252,253,246,247,248,249,250,249,250,251,252,253;
			setarray .@treasurey[0],378,378,378,378,378,376,376,376,376,376,374,374,374,374,372,372,372,372,372,370,370,370,370,370;
		}
		else if (strnpcinfo(2) == "schg_cas03") {
			set .@treasurebox,1940;
			setarray .@treasurex[0],189,190,191,192,193,194,189,190,191,192,193,194,189,190,191,192,193,194,189,190,191,192,193,194;
			setarray .@treasurey[0], 21, 21, 21, 21, 21, 21, 19, 19, 19, 19, 19, 19, 17, 17, 17, 17, 17, 17, 15, 15, 15, 15, 15, 15;
		}
		else {	// Castelos 1, 4, 5 são identicos, exceto os tesouros.
			if (strnpcinfo(2) == "schg_cas01") set .@treasurebox,1938;
			else if (strnpcinfo(2) == "schg_cas04") set .@treasurebox,1941;
			else set .@treasurebox,1942;
			setarray .@treasurex[0],388,388,388,387,386,385,384,384,384,384,384,384,385,386,387,388,389,390,390,390,389,388,387,386;
			setarray .@treasurey[0],388,389,390,390,390,390,389,388,387,386,385,384,384,384,384,384,384,384,385,386,386,386,386,386;
		}
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Arca do Tesouro",(.@i%2)?.@treasurebox:1324,1,strnpcinfo(0)+"::OnTreasureDied";
	for(set .@i,4; .@i<24; set .@i,.@i+1) {
		if (.@Treasure < .@i+1) break;
		monster strnpcinfo(2),.@treasurex[.@i],.@treasurey[.@i],"Arca do Tesouro",(.@i%2)?.@treasurebox:1324,1,strnpcinfo(0)+"::OnTreasureDied";
	}
	freeloop(0);
	end;

OnTreasureDied:
	end;
}

// Gerente do Castelo
//-------------------------------------------------------------
-	script	Steward#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),1);
	if (!.@GID) {
		mes "[ Gerente ]";
		mes "Estou esperando por um mestre";
		mes "no qual o destino vai escolher";
		mes "por mim. Você acha que";
		mes "tem coragem e força para";
		mes "conquistar esta fortaleza?";
		close;
	}
	if (getcharid(2) != .@GID || strcharinfo(0) != getguildmaster(.@GID)) {
		mes "[ Gerente ]";
		mes "Hmpf. Suas ameaças não";
		mes "não me assustam! Guardiões, conduzem";
		mes "esse infiel daqui!";
		mes "Eu vou ser sempre leal ao";
		mes "mestre desta fortaleza,";
		mes "o primeiro e único ^FF0000"+getguildmaster(.@GID)+"^000000.";
		close;
	}
	mes "[ Gerente ]";
	mes "Ah, Mestre ^FF0000"+getguildmaster(.@GID)+"^000000...";
	mes "Como posso servi-lo hoje?";
	mes "Havia um vista sobre a";
	mes "manutenção desta fortaleza,";
	mes "você gostária de descutir?";
	next;
	switch(select("Instruções da Fortaleza:Investir no Crescimento Comercial:Investir no Crescimento da Defesa:Contratar/Demitir Funcionária Kafra:Ír para Sala do Mestre")) {
	case 1:
		mes "[ Gerente ]";
		mes "O Nível de Crescimento";
		mes "Comercial da fortaleza é ^0000ff"+getcastledata(strnpcinfo(4),2)+".";
		if (getcastledata(strnpcinfo(4),4) > 0) {
			mes "Dá última vez, você investiu";
			mes getcastledata(strnpcinfo(4),4)+" no Crescimento Comercial.";
		}
		next;
		mes "[ Gerente ]";
		mes "O Nível de segurança de";
		mes "nossa fortaleza é "+getcastledata(strnpcinfo(4),3)+".";
		if (getcastledata(strnpcinfo(4),5) > 0) {
			mes "Dá última vez, você investiu";
			mes getcastledata(strnpcinfo(4),5)+" no Crescimento de Defesa.";
		}
		mes " ";
		mes "Mestre, isso é tudo.";
		close;
	case 2:
		set .@Economy,getcastledata(strnpcinfo(4),2);
		setarray .@cost[0],5000,10000,20000,35000,55000,80000,110000,145000,185000,230000,280000,335000,395000,460000,530000,605000,685000,770000,860000,955000;
		set .@j,0;
		for(set .@i,6; .@i<101; set .@i,.@i+5) {
			if (.@Economy < .@i) {
				set .@eco_invest,.@cost[.@j];
				break;
			}
			set .@j, .@j+1;
		}
		// Quadruplicar o custo de ivestimento se você já investiu uma vez.
		if (getcastledata(strnpcinfo(4),4)) set .@eco_invest,.@eco_invest*4;
		mes "[ Gerente ]";
		mes "Aumentar o crescimento";
		mes "comercial da fortaleza";
		mes "vai atumentar a quantidade de";
		mes "tesouros produzidos para o Clã.";
		mes "Investir no crescimento comercial";
		mes "vai ajudar no futuro do clã.";
		next;
		mes "[ Gerente ]";
		mes "Você pode fazer um investimento";
		mes "por dia, mais você pode fazer";
		mes "dois investimentos se você pagar";
		mes "mais Zeny: isto irá acelerar";
		mes "o desenvolvimento comercial,";
		mes "mas pode ser muito caro.";
		next;
		if (.@Economy == 100) {
			mes "[ Gerente ]";
			mes "No entanto, o nível de";
			mes "crescimento da nossa fortaleza";
			mes "está em 100%. Não é possível";
			mes "desenvolver o crescimento comercial";
			mes "mais do que isso.";
			close;
		}
		if (getcastledata(strnpcinfo(4),4) >= 2) {
			mes "[ Gerente ]";
			mes "Você já fez dois investimento";
			mes "hoje, então você deve";
			mes "esperar até amanhã";
			mes "para fazer outro investimento";
			close;
		}
		if (getcastledata(strnpcinfo(4),4) == 0) {
			mes "[ Gerente ]";
			mes "Você tem que pagar ^FF0000"+.@eco_invest+"^000000 Zeny";
			mes "para fazer um investimento.";
			mes "Você vai investir no";
			mes "no desenvolvimento comercial";
			mes "da fortaleza agora?";
		}
		else {
			mes "[ Gerente ]";
			mes "Você tem que pagar mais";
			mes "^FF0000"+.@eco_invest+"^000000 Zeny para investir pela";
			mes "segunda vez hoje. Você vai";
			mes "investir mais uma vez?";
		}
		next;
		switch(select("Investir no Crescimento Comercial:Cancelar")) {
		case 1:
			if (getcastledata(strnpcinfo(4),4) >= 2) {
				mes "[ Gerente ]";
				mes "Você já fez dois";
				mes "investimentos hoje, então você vai";
				mes "ter que esperar até amanhã";
				mes "para fazer outro investimento.";
				close;
			}
			if (Zeny < .@eco_invest) {
				mes "[ Gerente ]";
				mes "Mestre, lamento, mas";
				mes "mais você não tem Zeny suficiente";
				mes "para fazer um investimento";
				mes "para o clã hoje.";
				close;
			}
			Zeny -= .@eco_invest;
			setcastledata strnpcinfo(4),4,getcastledata(strnpcinfo(4),4)+1;
			mes "[ Gerente ]";
			mes "Mestre, foi feito utilização";
			mes "racional dos recrusos do clã. Podemos";
			mes "esperar até amanhã para ver os resultados";
			mes "deste investimento.";
			close;
		case 2:
			mes "[ Gerente ]";
			mes "Mestre, às suas ordens.";
			close;
		}
	case 3:
		set .@defence,getcastledata(strnpcinfo(4),3);
		setarray .@cost[0],10000,20000,40000,70000,110000,160000,220000,290000,370000,460000,560000,670000,790000,920000,1060000,1210000,1370000,1540000,1720000,1910000;
		set .@j,0;
		for(set .@i,6; .@i<101; set .@i,.@i+5) {
			if (.@defence < .@i) {
				set .@def_invest,.@cost[.@j];
				break;
			}
			set .@j, .@j+1;
		}
		// Quadruplicar o custo de ivestimento se você já investiu uma vez
		if (getcastledata(strnpcinfo(4),5)) set .@def_invest,.@def_invest*4;
		mes "[ Gerente ]";
		mes "Investir na defesa da nossa";
		mes "fortaleza irá melhorar a";
		mes "a durabilidade dos Guardiões";
		mes "e do Emperium. Vamos precisar";
		mes "de toda vantagem para nos";
		mes "proteger de nossos inimigos.";
		next;
		mes "[ Gerente ]";
		mes "Você pode investir na defesa,";
		mes "uma vez por dia, mas se você pagar";
		mes "mais Zeny, você pode investir";
		mes "no máximo de duas vezes por dia.";
		next;
		mes "[ Gerente ]";
		if (getcastledata(strnpcinfo(4),3) == 100) {
			mes "O Nível de defesa desta";
			mes "fortaleza é de 100%, e";
			mes "não se pode ser aumentado.";
			close;
		}
		if (getcastledata(strnpcinfo(4),5) >= 2) {
			mes "Mestre, você já";
			mes "investiu duas vezes";
			mes "hoje. Você terá que esperar";
			mes "até amanhã, se você realmente";
			mes "quiser aumentar as nossas defesas.";
			close;
		}
		if (getcastledata(strnpcinfo(4),5) == 0) {
			mes "Precisamos de ^FF0000"+.@def_invest+"^000000";
			mes "Zeny para investir nas";
			mes "defesas de nossa fortaleza.";
			mes "Você vai investir agora?";
		}
		else {
			mes "Precismos de ^FF0000"+.@def_invest+"^000000";
			mes "Zeny para investir em";
			mes "defesas da nossa fortaleza";
			mes "pela segunda vez de hoje.";
			mes "Você vai investir agora?";
		}
		next;
		switch(select("Investir na Defesa:Cancelar")) {
		case 1:
			if (getcastledata(strnpcinfo(4),5) >= 2) {
				mes "[ Gerente ]";
				mes "Mestre, você já";
				mes "investiu duas vezes";
				mes "hoje. Você vai ter que esperar";
				mes "até amanhã, se você realmente";
				mes "quiser aumentar nossas defesas.";
				close;
			}
			if (Zeny < .@def_invest) {
				mes "[ Gerente ]";
				mes "Mestre, lamento, mas";
				mes "mais você não tem Zeny suficiente";
				mes "para fazer um investimento";
				mes "para o clã hoje.";
				close;
			}
			Zeny -= .@def_invest;
			setcastledata strnpcinfo(4),5,getcastledata(strnpcinfo(4),5)+1;
			mes "[ Gerente ]";
			mes "Mestre, foi feito utilização";
			mes "racional dos recrusos do clã. O aumento";
			mes "da frequência de tesouros";
			mes "produzidos pelo clã vai";
			mes "ajudar definitivamente a nós todos.";
			close;
		case 2:
			mes "[ Gerente ]";
			mes "Mestre, às suas ordens.";
			close;
		}
	case 4:
		if (getcastledata(strnpcinfo(4),9) == 1) {
			mes "[ Gerente ]";
			mes "Você deseja demitir";
			mes "a Funcionária Kafra que";
			mes "contratamos para o clãn?";
			next;
			switch(select("Demitir:Cancelar")) {
			case 1:
				cutin "kafra_01",2;
				mes "[ Funcionária Kafra Contratada ]";
				mes "Mestre, por favor reconsidere!";
				mes "Eu tenho trabalhado muito duro";
				mes "para o sucesso do clã!";
				mes "Você me esforçar mais para servir";
				mes "os membros do clãn desta";
				mes "fortaleza, eu prometo!";
				next;
				switch(select("Demitir:Cancelar")) {
				case 1:
					mes "[ Funcionária Kafra Contratada ]";
					mes "Por quê?! O que eu fiz";
					mes "para merecer isso? Waaah~!";
					next;
					cutin "kafra_01",255;
					break;
				case 2:
					mes "[ Funcionária Kafra Contratada ]";
					mes "Mestre, obrigado!";
					mes "Eu vou obedecer todos os seus";
					mes "comandos da melhor maneira que posso!";
					mes "Você não vai se arrepender!";
					close;
				}
				break;
			case 2:
				mes "[ Gerente ]";
				mes "Na minha opnião, ela";
				mes "trabalha muito duro. Foi em";
				mes "todos os nossos melhores interesses";
				mes "para permitir que ela fique conosco.";
				close;
			}
			disablenpc "Funcionária Kafra#"+strnpcinfo(2);
			setcastledata strnpcinfo(4),9,0;
			mes "[ Gerente ]";
			mes "A Funcionária Kafra";
			mes "foi demitida.";
			mes "Estava realmente insatisfeito";
			mes "com a qualidade de seus serviços?";
			close;
		}
		else {
			mes "[ Gerente ]";
			mes "Você vai contratar uma";
			mes "Funcionária Kafra para servir";
			mes "nossa fortaleza? Você deve";
			mes "pagar ^FF000010,000 Zeny^000000 para contratar uma.";
			next;
			switch(select("Contratar:Cancelar")) {
			case 1:
				if (getgdskilllv(.@GID,10001) == 0) {
					mes "[ Gerente ]";
					mes "Mestre, não podemos contratar";
					mes "uma Funcionária Kafra porque";
					mes "você não tenha obtido";
					mes "a habilidade de clãn";
					mes "^FF0000Contrato com Kafra^000000.";
					mes "guild skill.";
					close;
				}
				if (Zeny < 10000) {
					mes "[ Gerente ]";
					mes "Mestre, não podemos contratar";
					mes "uma Funcionária Kafra porque";
					mes "não temos fundos suficientes";
					mes "para pagar a taxa de contrato.";
					close;
				}
				Zeny -= 10000;
				enablenpc "Funcionária Kafra#"+strnpcinfo(2);
				setcastledata strnpcinfo(4),9,1;
				mes "[ Gerente ]";
				mes "Muito bem. Nós formamos";
				mes "um contrato com o Escritório";
				mes "Central Kafra, e contratamos";
				mes "uma Funcionária Kafra para";
				mes "a fortaleza. Aqui está ela~";
				next;
				cutin "kafra_01",2;
				mes "[ Funcionária Kafra Contratada ]";
				mes "Como vai? Fui";
				mes "enviada pelo Escritório";
				mes "Kafra Central Kafra para";
				mes "servir o seu clã.";
				mes "Mestre, eu vou fazer o meu";
				mes "melhor para seguir suas ordens.";
				next;
				cutin "kafra_01",255;
				mes "[ Gerente ]";
				mes "Nosso contrato vai expirar";
				mes "depois de um mês, por isso temos";
				mes "que pagar taxas adicionais para manter";
				mes "a Funcionária Kafra a";
				mes "serviço de nosso clã.";
				close;
			case 2:
				mes "[ Gerente ]";
				mes "Mestre, as suas ordens.";
				mes "No entanto, sugiro a contratação";
				mes "de uma Funcionária Kafra logo";
				mes "que possível, pois nosso clã";
				mes "iria beneficiariam muito com";
				mes "as facilidades do serviço Kafra.";
				close;
			}
		}
	case 5:
		mes "[ Gerente ]";
		mes "Você gostária de entrar na";
		mes "Sala do Tesouro do Clã?";
		mes "Só você, o Mestre do Clã";
		mes "está autorizado a entrar.";
		next;
		mes "[ Gerente ]";
		mes "Por favor, lembre-se";
		mes "de abri as Arcas do Tesouro no";
		mes "momento adequeado. Caso contráriom, o";
		mes "tesouro pode desaparecer se";
		mes "algo inesperado acontecer.";
		next;
		switch(select("Ír para Sala do Tesouro:Cancelar")) {
		case 1:
			mes "[ Gerente ]";
			mes "Me permita guiá-lo";
			mes "ao caminho secreto para";
			mes "a Sala do Tesouro.";
			mes "Pressione o interruptor secreto";
			mes "quando você quiser retornar aqui.";
			close2;
			if (compare(strnpcinfo(4),"arug")) {
				if (strnpcinfo(4) == "arug_cas01") setarray .@i[0],250,363;
				else if (strnpcinfo(4) == "arug_cas02") setarray .@i[0],382,227;
				else setarray .@i[0],292,266;	// Castelos 3, 4, e 5 são identicos.
			}
			else {
				if (strnpcinfo(4) == "schg_cas02") setarray .@i[0],249,373;
				else if (strnpcinfo(4) == "schg_cas03") setarray .@i[0],190,16;
				else setarray .@i[0],381,381;	// Castelo 1, 4 e 5 são identicos.
			}
			warp strnpcinfo(4),.@i[0],.@i[1];
			end;
		case 2:
			mes "[ Gerente ]";
			mes "Os itens da Sala do Tesouro";
			mes "são produzidos uma vez por dia.";
			mes "No entanto, você deve obter";
			mes "os itens do tesouro todos os dias.";
			mes "Para o bem do clã,";
			mes "priorize a colheita de tesouro!";
			close;
		}
	}

OnStop:
	awake strnpcinfo(0);
	end;

OnStartArena:
	set .@GID,getcharid(2);
	set .@region$, (compare(strnpcinfo(4),"arug"))?"Valfreyja":"Nithafjoll";
	// Menor Economia no castelo
	set .@Economy,getcastledata(strnpcinfo(4),2)-5;
	if (.@Economy < 0) set .@Economy, 0;
	setcastledata strnpcinfo(4),2,.@Economy;
	// Menor Defesa no castelo
	set .@defence,getcastledata(strnpcinfo(4),3)-5;
	if (.@defence < 0) set .@defence, 0;
	setcastledata strnpcinfo(4),3,.@defence;
	// Defenir novo propriétario
	setcastledata strnpcinfo(4),1,.@GID;
	// Limpar dados do castelo.
	for(set .@i,4; .@i<10; set .@i,.@i+1)
		setcastledata strnpcinfo(4),.@i,0;
	// Desativar Kafra
	disablenpc "Funcionária Kafra#"+strnpcinfo(2);

	announce "O clã ["+getguildname(.@GID)+"] conquistou a fortaleza "+getcastlename(strnpcinfo(4))+" de ["+.@region$+" "+charat(strnpcinfo(2),3)+"]!",bc_all|bc_woe;
	mapannounce strnpcinfo(4),"O Emperium foi destruído!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	donpcevent "Manager#"+strnpcinfo(4)+"::OnReset";
	maprespawnguildid strnpcinfo(4),getcastledata(strnpcinfo(4),1),2;
	donpcevent "Manager#"+strnpcinfo(4)+"::OnRecvCastle2";
	donpcevent "::OnRecvCastle"+ strtoupper( substr( strnpcinfo(2), 0, 0 ) ) + substr( strnpcinfo(2), 1, getstrlen( strnpcinfo(2) ) -1 );
	sleep 10000;
	if (agitcheck2()) {
		donpcevent "Manager#"+strnpcinfo(4)+"::OnChange";
		mapannounce strnpcinfo(4),"Reconstruir Guardiões e Portões de nossa fortaleza para garantir a segurança do seu Clã!",bc_map,"0x00FF00",FW_NORMAL,20,0,40;
	}
	end;
}

// Guardiões do Castelo
//-------------------------------------------------------------
-	script	Guardian#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),1);
	set .@n$, "["+strnpcinfo(1)+"]";
	if (!.@GID) {
		mes .@n$;
		mes "Excelente trabalho. Agora tudo que você";
		mes "tem que fazer é destruír este";
		mes "Emperium para ganhar a posse";
		mes "desta fortaleza.";
		close;
	}
	if (getcharid(2) == .@GID) {
		if (strcharinfo(0) != getguildmaster(.@GID)) {
			mes .@n$;
			mes "Como guardião desta";
			mes "fortaleza, eu só vou responder";
			mes "ao mestre do clã que controla";
			mes "este lugar.";
			close;
		}
		else {
			if (!agitcheck2()) {
				mes .@n$;
				mes "Eu sou "+strnpcinfo(1)+", guardião desta";
				mes "fortaleza. Por enquanto,";
				mes "tudo está calmo neste lugar.";
				next;
				switch(select("Conversar:Cancelar")) {
				case 1:
					mes .@n$;
					mes "Você tem alguma dúvida";
					mes "sobre esta fortaleza?";
					next;
					switch(select("Runas Guardiãs:Portões da Fortaleza:Bandeiras de Ligação:Estratégia de Batalha:Cancelar")) {
					case 1:
						mes .@n$;
						mes "Há um Emperium";
						mes "e duas Runas Guardiãs em";
						mes "cada fortaleza. Estas pedras";
						mes "são a primeira linha de defesa,";
						mes "e deve ser destruída antes";
						mes "para os inimigos poder entrar.";
						next;
						mes .@n$;
						mes "As pedras estão localizadas no";
						mes "^4D4DFFPortão da Casa^000000 que deve ser";
						mes "protegidas para previnir que os";
						mes "inimigos cheguem ao Emperium.";
						mes "Runas Guardiões ^4D4DFFconvocam";
						mes "seus Guardiões^000000 para proteção.";
						next;
						mes .@n$;
						mes "Fortalezas com o nível maiores níveis";
						mes "de defesa podem chamar mais";
						mes "Guardiões: é por isso que é";
						mes "tão importante para os Clãs";
						mes "investir no Aumento da Defesa.";
						next;
						mes .@n$;
						mes "Runas Guardiãs que foram";
						mes "destruídas podem ser resconstruídas";
						mes "após de um certo tempo, mas um dos^FFFFFF ^000000 memberos do clã deve dar";
						mes "a ordem. Eu também posso reletar ^FFFFFF ^000000 o estado das Runas Guardiãs.";
						close;
					case 2:
						mes .@n$;
						mes "^4D4DFFPortões da Fortaleza^000000 são a segunda ^FFFFFF ^000000 linha de defesa da fortaleza do clã,";
						mes "e são protegidas por barricadas extras ativadas pelas Runas Guardiãs.";
						mes "Estes portões estão localiados em três partes diferentes da fortaleza.";
						next;
						mes .@n$;
						mes "Barricadas são protegidas pelas";
						mes "Runas Guardiões, e são";
						mes "restauradas quando a Runa";
						mes "Guardiãs são reconstruídas. Contudo,";
						mes "não é fácil de reconstruir";
						mes "Portões da Fortaleza destruídos.";
						next;
						mes .@n$;
						mes "Portões da Fortaleza só pode ser";
						mes "reconstruído quando o ^4D4DFFmuda";
						mes "o mestre de uma fortaleza^000000,";
						mes "ou se a ^4D4DFFrestauração";
						mes "é solicitada pelo mestre";
						mes "do clã da fortaleza^000000.";
						close;
					case 3:
						mes .@n$;
						mes "Nas Fortalezas há muitas";
						mes "bandeiras de ligação que permitem que";
						mes "você tenha acesso a áreas vitais dentro";
						mes "de restrições impostas pelas";
						mes "Barricadas. Normalmente, ^4D4DFFUma badeira";
						mes "é ligada ao Portão da Casa^000000.";
						next;
						mes .@n$;
						mes "Muitas bandeiras é ligada diretamente";
						mes "para as bandeiras perto do Emperium.";
						mes "A bandeira final, numerada é";
						mes "está ligada com ao Mecanismo";
						mes "de Conveniência da fortaleza";
						mes "do possessor. Tenha isso em mente.";
						close;
					case 4:
						mes .@n$;
						mes "Estratégia? Seria melhor";
						mes "para desenvolver seu plano de batalha para";
						mes "explorar as vantagens do seu clã";
						mes "e os pontos fracos do seus inimigos.";
						mes "Use e reconstrua os Portões e Barricadas, o mais rápido que puder.";
						close;
					case 5:
						mes .@n$;
						mes "Você não tem perguntas";
						mes "pra fazer pra mim? Bem, estou";
						mes "para servir suas nescessidades.";
						close;
					}
				case 2:
					mes .@n$;
					mes "Estou sempre aqui, então";
					mes "sinta-se avontade para solicitar";
					mes "minha assistencia sempre que";
					mes "necessário.";
					close;
				}
			}
			else {
				mes .@n$;
				mes "Saudações, "+strcharinfo(0)+".";
				mes "Quais são as suas ordens?";
				next;
				switch(select("Aumentar a defesa da Fortaleza:Relatório da Situação:Cancelar")) {
				case 1:
					if (!getd("$agit_"+strnpcinfo(2)+"[5]")) {
						if (getgdskilllv(.@GID,10002) == 0) {
							mes .@n$;
							mes "Sinto muito, mas as Runas";
							mes "Guardiões não são poderosas o suficiente";
							mes "para convocar os Guardiões ainda.";
							mes "Precisamos acumular mais";
							mes "conhecimentos antes de";
							mes "convocar qualquer Guardião.";
							close;
						}
						else {
							mes .@n$;
							mes "Você pode tentar convocar";
							mes "um Guardião atráves de uma Runa";
							mes "Guardiã. Por tanto, tenha em mente";
							mes "que isso não vai funcionar se a";
							mes "Runa Guardiã está destruída.";
							setd "$agit_"+strnpcinfo(2)+"[5]",1;
							if (!getd("$agit_"+strnpcinfo(2)+"[0]"))
								donpcevent "gard1#"+strnpcinfo(4)+"::OnEnable";
							if (!getd("$agit_"+strnpcinfo(2)+"[1]"))
								donpcevent "gard2#"+strnpcinfo(4)+"::OnEnable";
							close;
						}
					}
					else {
						mes .@n$;
						mes "Você já me mandou";
						mes "convocar um Guardião";
						mes "para defender a fortaleza.";
						close;
					}
				case 2:
					mes .@n$;
					mes "Nosso estado de defesa é....";
					setarray .@status$[0],"^4D4DFFOperacional","^FF0000Destruído","^008000Reparando";
					mes "1ª Runa Guardiã: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[0]")]+"^000000";
					mes "2ª Runa Guardiã: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[1]")]+"^000000";
					mes "1º Portão da Fortaleza: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[2]")]+"^000000";
					mes "2º Portão da Fortaleza: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[3]")]+"^000000";
					mes "3º Portão da Fortaleza: "+.@status$[getd("$agit_"+strnpcinfo(2)+"[4]")]+"^000000";
					close;
				case 3:
					mes .@n$;
					mes "Estarei de prontidão,";
					mes "aguardando suas ordens.";
					close;
				}
			}
		}
	}
	else {
		mes .@n$;
		mes "Que é você? Canhalha!";
		mes "Saia dessa fortaleza agora!";
		close;
	}

OnInit:
	setarray getd("$agit_"+strnpcinfo(2)+"[0]"),0,0,0,0,0,0;
	end;
}

// Kafras do Clã
//-------------------------------------------------------------
-	script	Kafra#template	-1,{
	cutin "kafra_01",2;
	set .@GID, getcastledata(strnpcinfo(4),1);
	if (getcharid(2) == .@GID && getgdskilllv(.@GID,10001)) {
		mes "[Funcionária Kafra]";
		mes "Bem-vindo, orgulhoso membro";
		mes "do Clã ^FF0000"+getguildname(.@GID)+"^000000!";
		mes "A Corporação Kafra está";
		mes "pronta para ajudá-lo aonde quer que você vá!";
		next;
		switch(select("Uar Armazém:Usar Serviço de Teleporte:Alugar um Carrinho:Cancelar")) {
		case 1:
			if (basicskillcheck() && getskilllv("NV_BASIC") < 6) {
				mes "[Funcionária Kafra]";
				mes "Sinto muito, mas você";
				mes "deve ter pelo menos Habildiades Básicas";
				mes "Nv.6 para usar o Armazém.";
			}
			else openstorage;
			break;
		case 2:
			mes "[Funcionária Kafra]";
			mes "Por favor, me diga o seu";
			mes "destino de teleporte.";
			next;
			switch(select("Rachel -> 200 z:Cancel")) {
			case 1:
				if (Zeny < 200) {
					mes "[Funcionária Kafra]";
					mes "Sinto muito, mas você não";
					mes "tem Zeny o suficiente para pagar";
					mes "a taxa de teleporte. Será que";
					mes "você pode checar seu dinheiro novamente?";
					close2;
					cutin "kafra_01",255;
					end;
				}
				Zeny -= 200;
				warp "rachel",115,125;
				end;
			case 2:
				cutin "kafra_01",255;
				break;
			}
			break;
		case 3:
			if (BaseClass != Job_Merchant) {
				mes "[Funcionária Kafra]";
				mes "Sinto muito, mas o aluguel";
				mes "de Carrinho só pode ser";
				mes "usados para personagens da classe";
				mes "Mercadores, Ferreiros e Alquimistas.";
			}
			else if (checkcart() == 1) {
				mes "[Funcionária Kafra]";
				mes "Hm? Você já alugou";
				mes "um Carrinho.";
			}
			else {
				mes "[Funcionária Kafra]";
				mes "Para alugar um carrinho";
				mes "é preciso de 800 Zeny. Gostaria";
				mes "de alugar um Carrinho?";
				next;
				switch(select("Alugar um Carrinho:Cancelar")) {
				case 1:
					if (Zeny < 800) {
						mes "[Funcionária Kafra]";
						mes "Sinto muito, mas você não";
						mes "tem Zeny o suficiente para alugar";
						mes "um dos nossos Carrinhos.";
						close2;
						cutin "kafra_01",255;
						end;
					}
					Zeny -= 800;
					setcart;
					break;
				case 2:
					break;
				}
			}
			break;
		case 4:
			mes "[Funcionária Kafra]";
			mes "Obrigado por usar os";
			mes "Serviços Kafra. Onde quer que";
			mes "você vá, Kafra vai estar";
			mes "lá para apoiá-lo!";
			close2;
			cutin "kafra_01",255;
			end;
		}
		close2;
		cutin "kafra_01",255;
		end;
	}
	else {
		mes "[Funcionária Kafra]";
		mes "Sinto muito, mas eu fui";
		mes "contratada exclusivamente";
		mes "para os membros do";
		mes "Clã ^FF0000"+getguildname(.@GID)+"^000000.";
		mes "Você vai ter que pedir ajuda";
		mes "a outra Funcionária Kafra...";
		close2;
		cutin "kafra_01",255;
		end;
	}
}

// Runas Guardiãs (2)
//-------------------------------------------------------------
-	script	Guardian Stone#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),1);
	set .@num, atoi(charat(strnpcinfo(1),0));
	set .@var$,"$agit_"+strnpcinfo(2);
	if (getcharid(2) == .@GID) {
		mes "^3355FFVocê vai precisar dos";
		mes "seguintes materiais para";
		mes "reconstrúir Runas";
		mes "Guardiões destruídas.^000000";
		next;
		mes "1 Oridecon";
		mes "1 Elunium";
		mes "30 Pedras";
		mes "5 Gemas Azuis";
		mes "5 Gemas Amarelas";
		mes "5 Gemas Vermelhas";
		next;
		mes "^3355FFDeseja continuar?^000000";
		next;
		if(select("Não:Continuar") == 1) {
			mes "^3355FFTrabalho cancelado.^000000";
			close;
		}
		if ((countitem(984) > 0) && (countitem(985) > 0) && (countitem(7049) > 29) && (countitem(717) > 4) && (countitem(715) > 4) && (countitem(716) > 4)) {
			mes "^3355FFOrganize nessa ordem,";
			mes "Pedras, Elunium, e Oridecon, no";
			mes "centro. Em seguida, você deve organizar";
			mes "as Gemas encantadas para reconstruír";
			mes "a Runa Guardiã.^000000";
			next;
			setarray .@stone$[0],"Elunium","Oridecon","Pedra";
			set .@i, select("Elunium:Oridecon:Pedra")-1;
			if (.@i == 2) set .@nice,.@nice+10;
			mes "^3355FF"+.@stone$[.@i]+" foi";
			mes "colocada no centro.^000000";
			next;
			set .@i, select("Elunium:Oridecon:Pedra")-1;
			if (.@i == 0) set .@nice,.@nice+10;
			mes "^3355FFVocê revestiu";
			mes "o lado de fora do centro";
			mes "com alguns "+.@stone$[.@i]+".^000000";
			next;
			set .@i, select("Elunium:Oridecon:Pedra")-1;
			if (.@i == 1) set .@nice,.@nice+10;
			mes "^3355FFVocê cobriu o";
			mes "resto dos materiais";
			mes "com alguns "+.@stone$[.@i]+".^000000";
			next;
			mes "^3355FFAgora você precisa organizar";
			mes "as Gemas de acordo com o";
			mes "encantamento. Você pode identificar";
			mes "as suas propriedades mágicas";
			mes "pelo efeito lançado.^000000";
			next;
			setarray .@effect[0],56,54,225;
			setarray .@color$[0],"Vermelha","Amarela","Azul";
			while(1) {
				if (.@roof0 > 7) break;
				set .@i, rand(3);
				specialeffect .@effect[.@i];
				mes "^3355FFAs Gemas devem";
				mes "ser postas na ordem";
				mes "correta de acordo com seus";
				mes "poderes e propriedades mágicas.^000000";
				next;
				set .@j, select("Gema Vermelha:Gema Amarela:Gema Azul")-1;
				mes "^3355FFVocê colocou a Gema "+.@color$[.@j]+".^000000";
				if (.@i == .@j) {
					mes "^3355FFNo entanto, o Sistema de Reparação";
					mes "de Runa Guardiã falhou devido a";
					mes "um conflito de poder mágico.^000000";
					close;
				}
				set .@nice,.@nice+10;
				set .@roof0,.@roof0+1;
				specialeffect EF_STEAL;
				next;
			}
			if (.@nice > 90) {
				if (!getd(.@var$+"["+(.@num-1)+"]")) {
					mes "^3355FFO Sistema de Reparação";
					mes "de Runa Guardiã";
					mes "já foi concluído.^000000";
					close;
				}
				else {
					if (!agitcheck2()) {
						mes "^3355FFÉ impossível";
						mes "reconstruir a Runa";
						mes "Guardiã porque o";
						mes "Emperium não está presente.^000000";
						close;
					}
					else {
						mes "^3355FFAs Gemas foram";
						mes "ornganizadas, e a Runa";
						mes "Guardiã foi reconstruida.^000000";
						delitem 984,1; // Oridecon
						delitem 985,1; // Elunium
						delitem 7049,30; // Pedra
						delitem 717,5; // Gema_Azul
						delitem 715,5; // Gema_Amarela
						delitem 716,5; // Gema_Vermelha
						close2;
						donpcevent "df"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
						specialeffect EF_ICECRASH;
						disablenpc strnpcinfo(0);
						setd .@var$+"["+(.@num-1)+"]",0;
						set .@df_all,getd(.@var$+"[0]")+getd(.@var$+"[1]");
						if (!.@df_all) {
							mapannounce strnpcinfo(4),"As Runas Guardiãs foram erguidas, reforçando a defesa da fortaleza!",bc_map,"0x00ff00";
							donpcevent "RL0#"+strnpcinfo(4)+"::OnEnable";
						}
						else mapannounce strnpcinfo(4),"A "+strnpcinfo(1)+" foi reparada com sucesso.",bc_map,"0x00ff00";
						if (getd(.@var$+"[5]") == 1)
							donpcevent "gard"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
						end;
					}
				}
			}
			else {
				mes "^3355FFDepois de todo esse trabalho...";
				mes "Parece que você não conseguiu";
				mes "corrigir a Runa Guardiã,";
				mes "e perdeu todos alguns materiais.^000000";
				delitem 7049,10; // Pedra
				delitem 717,2; // Gema_Azul
				delitem 715,2; // Gema_Amarela
				delitem 716,2; // Gema_Veremalha
				close;
			}
		}
		else {
			mes "^3355FFVocê não tem materiais";
			mes "suficientes para reconstruir";
			mes "a Runa Guardiã.^000000";
			close;
		}
	}
	end;

OnInit:
OnDisable:
	disablenpc strnpcinfo(0);
	end;

OnEnable:
	enablenpc strnpcinfo(0);
	specialeffect EF_MAPPILLAR2;
	end;
}

// Dispositivos de Controle (3)
//-------------------------------------------------------------
-	script	Control#template	-1,{
	set .@GID, getcastledata(strnpcinfo(4),1);
	set .@num, atoi(charat(strnpcinfo(1),19));
	set .@var$,"$agit_"+strnpcinfo(2);
	if (getcharid(2) == .@GID) {
		if (strcharinfo(0) == getguildmaster(.@GID)) {
			if (getd(.@var$+"["+(.@num+1)+"]") == 2) {
				mes "^3355FFPortões Demolidos da";
				mes "Fortaleza podem ser reconstruídos,";
				mes "mas você terá que reunir os";
				mes "seguintes materiais.^000000";
				next;
				mes "^4D4DFF10 Aço^000000,";
				mes "^4D4DFF30 Tronco^000000,";
				mes "^4D4DFF5 Oridecon^000000, e";
				mes "^4D4DFF10 Emveretarcon^000000.";
				next;
				select("Continuar");
				if ((countitem(1019) > 29) && (countitem(999) > 9) && (countitem(1011) > 9) && (countitem(984) > 4)) {
					mes "^3355FFVocê vai precisar de Troncos";
					mes "para reparar a moldura de suporte,";
					mes "Oridecon para reforçar";
					mes "resistência do portão, e";
					mes "Emveretarcon par assegurar";
					mes "que o portão está seguro.^000000";
					next;
					set .@ro_of01,rand(10,15);
					while(1) {
						if (.@ro_of02 == .@ro_of01) break;
						else {
							switch(rand(1,4)) {
							case 1:
								mes "^3355FFA moldura de suporte";
								mes "esta bastante danificada:";
								mes "fixar esta parte é";
								mes "a maior prioridade.^000000";
								next;
								switch(select("Tronco:Aço:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFA moldura";
									mes "foi reforçada com madeira.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 2:
									mes "^3355FFVocê tentou usar aço,";
									mes "mas ele não está funcionando muito";
									mes "bem. Você vai ter que tentar";
									mes "outra coisa.^000000";
									close;
								case 3:
									mes "^3355FFVocê tentou usar emveretarcon";
									mes "para reforçar o portão, mas ele";
									mes "não está funcionando muito bem.";
									mes "Você vai ter que começar de novo.^000000";
									close;
								case 4:
									mes "^3355FFVocê tentou usar oridecon,";
									mes "mas ele não está funcionando muito";
									mes "bem. Você vai ter que tentar";
									mes "outra coisa.^000000";
									close;
								}
								break;
							case 2:
								mes "^3355FFParece que a resistência";
								mes "geral do portão precisa";
								mes "ser reforçado com alguma coisa.^000000";
								next;
								switch(select("Tronco:Aço:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFVocê tentou usar madeira";
									mes "para reforçar o portão.^000000";
									set .@ro_of02,.@ro_of02+1;
									next;
									break;
								case 2:
									mes "^3355FFVocê tentou usar aço";
									mes "para reforçar o portão, mas";
									mes "ele não está funcionando muito bem.";
									mes "Você vai ter que começar de novo.^000000";
									close;
								case 3:
									mes "^3355FFVocê tentou usar emveretarcon";
									mes "para reforçar o portão, mas";
									mes "ele não está funcionando muito bem.";
									mes "Você vai ter que começar de novo.^000000";
									close;
								case 4:
									mes "^3355FFVocê martelou o";
									mes "oridecon: parece";
									mes "que isso vai funcionar.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								}
								break;
							case 3:
								mes "^3355FFO dano ao portão";
								mes "causou todas essas";
								mes "rachaduras. Você vai precisar";
								mes "soldá-las de alguma forma.^000000";
								next;
								switch(select("Tronco:Aço:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFVocê tentou usar mandeira para";
									mes "corrigir esse problema, mas parece";
									mes "que até piorou.";
									mes "Você vai ter que começar de novo.^000000";
									close;
								case 2:
									mes "^3355FFVocê usou aço para soldar";
									mes "todas as rachaduras: o portão";
									mes "está começando a ficar mais sólido.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 3:
									mes "^3355FFVocê tentou usar emveretarcon";
									mes "para reforçar o portão, mas ele";
									mes "ele não está funcionando muito bem.";
									mes "Você vai ter que começar de novo.^000000";
									close;
								case 4:
									mes "^3355FFVocê tentou usar oridecon,";
									mes "mas ele não está funcionando muito";
									mes "bem. Você vai ter que";
									mes "tentar outra coisa.^000000";
									close;
								}
								break;
							case 4:
								mes "^3355FFAgora você precisa ter certeza";
								mes "que o portã estão seguro";
								mes "e ao mesmo tempo solido.^000000";
								next;
								switch(select("Tronco:Aço:Emveretarcon:Oridecon")) {
								case 1:
									mes "^3355FFVocê tentou usar madeira";
									mes "para corrigir esse problema, mas parece";
									mes "que até piorou.";
									mes "Você vai ter que começar de novo.^000000";
									close;
								case 2:
									mes "^3355FFVocê tentou usar aço,";
									mes "mas ele não está funcionando muito";
									mes "bem. Você vai ter que tentar";
									mes "outra coisa.^000000";
									close;
								case 3:
									mes "^3355FFVocê usou com sucesso";
									mes "o emveretarcon para reparar";
									mes "o dano do portão.^000000";
									set .@rp_temp,.@rp_temp+1;
									set .@ro_of02,.@ro_of02+1;
									specialeffect2 EF_REPAIRWEAPON;
									next;
									break;
								case 4:
									mes "^3355FFVocê tentou usar oridecon,";
									mes "mas ele não está funcionando muito";
									mes "bem. Você vai ter que tentar";
									mes "outra coisa.^000000";
									close;
								}
							}
						}
					}
					mes "^3355FFBem, parece que";
					mes "você terminou de";
					mes "reparar o portão.^000000";
					next;
					if (!agitcheck2()) {
						mes "^3355FFInfelizmente, o Portão";
						mes "da Fortaleza não pode ser reconstruído:";
						mes "O Emperium não está aqui.^000000";
						close;
					}
					else {
						if (.@rp_temp == .@ro_of01) {
							mes "^3355FFO Portão da Fortaleza";
							mes "foi reparado com sucesso!^000000";
							delitem 1019,30; // Tronco
							delitem 999,10; // Aço
							delitem 1011,10; //Emveretarcon
							delitem 984,5; //Oridecon
							close2;
							donpcevent "RL"+.@num+"#"+strnpcinfo(4)+"::OnEnable";
							disablenpc strnpcinfo(0);
							if (.@num == 1) set .@str$,"1º";
							else if (.@num == 2) set .@str$,"2º";
							else if (.@num == 3) set .@str$,"3º";
							mapannounce strnpcinfo(4),"O "+.@str$+" Portão da Fortaleza foi reconstruído!",bc_map,"0x00ff00";
							if (.@num == 1) setd .@var$+"[2]",0;
							else {
								setarray getd(.@var$+"["+.@num+"]"),2,0;
								donpcevent "Disp. de Controle 0"+(.@num-1)+"#"+strnpcinfo(2)+"::OnEnable";
							}
							end;
						}
						else {
							mes "^3355FFO muro foi quebrado,";
							mes "e a tentativa de reparar";
							mes "o Portão da Fortaleza falhou.";
							mes "Você perdeu alguns de seus";
							mes "recuros na reparação...^000000";
							delitem 984,2; // Oridecon
							delitem 999,4; // Aço
							delitem 1019,14; // Tronco
							delitem 1011,3; // Emveretarcon
							close;
						}
					}
				}
				else {
					mes "^3355FFVocê não pode tentar reparar";
					mes "o Portão da Fortaleza se você não";
					mes "tem todos materiais necessários.^000000";
					close;
				}
			}
		}
	}
	end;

OnInit:
OnDisable:
	disablenpc strnpcinfo(0);
	end;

OnEnable:
	enablenpc strnpcinfo(0);
	end;
}

// Invocar Guardiões (2)
//-------------------------------------------------------------
-	script	gard#template	-1,{
OnEnable:
	// .@x[i],.@y[i]: Coordenadas normais, #0-21.
	// .@w[x],.@w[y]: Coordenadas especial se a 'defesa' está em 11.
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") {
			setarray .@w[0],195,250,292,188;
			setarray .x[0],233,252,232,201,224,196,269,252,201,224,222, 294,256,240,246,235,235,246,240,256,254,242;
			setarray .y[0], 83, 81,108,130,168,137, 89, 81,130,168,129, 210,203,133, 92,132,132, 92,133,203, 95,151;
		}
		else if (strnpcinfo(2) == "arug_cas02") {
			setarray .@w[0],20,169,268,169;
			setarray .x[0],104,67,67,113,122,67, 90, 91,122, 20,67, 175,204,211,209,161,186,183,150,161,209,211;
			setarray .y[0], 32,36,85, 87,112,60,167,119,112,169,85,  31, 32, 63, 88, 91,170,121,110, 91, 88, 63;
		}
		else {	// Castelos 3, 4, 5 são identicos.
			setarray .@w[0],66,157,211,159;
			setarray .x[0],130,128,128,128,110,91,65, 65,110,128,128, 156,172,154,156,155,187,212,211,155,156,172;
			setarray .y[0], 60, 77, 90,100, 96,53,71,103, 96,100, 77, 101, 95, 90, 77, 60, 54, 67,105, 60, 77, 95;
		}
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") {
			setarray .@w[0],337,95,307,222;
			setarray .x[0],326,337,334,296,285,236,285,296,334,337,334, 359,300,337,317,307,300,337,317,307,359,236;
			setarray .y[0], 83, 95,119, 82, 40, 41, 40, 82,119, 95,119,  85,119,154,183,222,119,154,183,222, 85, 41;
		}
		else if (strnpcinfo(2) == "schg_cas03") {
			setarray .@w[0],306,325,364,305;
			setarray .x[0],323,273,288,306,323,323,273,288,306,273,273, 338,364,365,317,338,338,364,365,317,364,329;
			setarray .y[0],308,309,306,326,308,308,309,306,325,309,309, 309,305,261,318,310,309,305,261,318,305,314;
		}
		else {	// Castelos 1, 4, e 5 são identicos.
			setarray .@w[0],108,32,128,42,187,15;	// Contém um par extra, por qualquer motivo.
			setarray .x[0],111,109,65,110,88,64,47,109,111,112,120, 130,129,151,187,128,152,187,128,130,130,151;
			setarray .y[0], 18, 44,22, 40,20,40,43, 48, 18, 32, 37,  22, 47, 18, 15, 42, 43, 15, 42, 22, 28, 18;
		}
	}
	if (charat(strnpcinfo(1),4) == "2") set .@z,11;
	freeloop(1);
	set .@defence,getcastledata(strnpcinfo(2),3);
	callsub OnSummon,.@z;
	if (.@defence > 70) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),5;
	else if (.@defence > 50) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),4;
	else if (.@defence > 30) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),3;
	else if (.@defence > 10) set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),2;
	if (.@w[4] && .@z)
		guardian strnpcinfo(2),.@w[4],.@w[5],"Guardião Soldado",1899,strnpcinfo(0)+"::OnGuardianDied";
	else if (.@defence < 11) {
		set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),2;
		set .@i,(.@z)?2:0;
		guardian strnpcinfo(2),.@w[.@i],.@w[.@i+1],"Guardião Soldado",1899,strnpcinfo(0)+"::OnGuardianDied";
	}
	else for(set .@i,1; .@i<getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)); set .@i,.@i+1)
		callsub OnSummon,.@i+.@z;
	freeloop(0);
	copyarray getd(".x_"+strnpcinfo(2)+"[0]"),.@x[0],22;
	copyarray getd(".y_"+strnpcinfo(2)+"[0]"),.@y[0],22;
	setd ".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2),4+.@z;
	setarray .count$[5],"1st","2nd","3rd","4th","5th";
	initnpctimer;
	end;

OnTimer300000:
OnTimer900000:
OnTimer1800000:
OnTimer2700000:
OnTimer3600000:
	if (charat(strnpcinfo(1),4) == "2") end;
	set .@var$,".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2);
	setd .@var$, getd(.@var$)+1;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
	callsub OnSummon,getd(.@var$);
	setarray .count$[5],"1º","2º","3º","4º","5º";
	mapannounce strnpcinfo(2),"O "+.count$[getd(.@var$)]+" Guardião foi convocado para o Portão.",bc_map,"0xff4500";
	if (getd(.@var$) == 9) {
		setd .@var$,0;
		stopnpctimer;
	}
	end;

OnTimer600000:
OnTimer1200000:
OnTimer2100000:
OnTimer3000000:
OnTimer3900000:
	if (!(charat(strnpcinfo(1),4) == "2")) end;
	set .@var$,".timer_"+charat(strnpcinfo(1),4)+strnpcinfo(2);
	setd .@var$, getd(.@var$)+1;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
	callsub OnSummon,getd(.@var$);
	if (getd(.@var$) == 20) {
		setd .@var$,0;
		stopnpctimer;
	}
	end;

OnSummon:
	guardian strnpcinfo(2),getd(".x_"+strnpcinfo(2)+"["+getarg(0)+"]"),getd(".y_"+strnpcinfo(2)+"["+getarg(0)+"]"),"Guardião Soldado",1899,strnpcinfo(0)+"::OnGuardianDied";
	return;

OnGuardianDied:
	if (charat(strnpcinfo(1),4) == "2") set .@z,11;
	set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))-1;
	if (getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)) < 2) {
		set getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2)),getd(".MyMobCount_"+charat(strnpcinfo(1),4)+strnpcinfo(2))+1;
		callsub OnSummon,10+.@z;
	}
	end;

OnReset:
	stopnpctimer;
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianDied";
	deletearray getd(".x_"+strnpcinfo(2)+"[0]"),22;
	deletearray getd(".y_"+strnpcinfo(2)+"[0]"),22;
	end;
}

// Invocações das Runas Guardiões (2)
//-------------------------------------------------------------
-	script	df#template	-1,{
OnEnable:
	if (compare(strnpcinfo(2),"arug")) {
		if (strnpcinfo(2) == "arug_cas01") setarray .@i[0],210,234,308,189;
		else if (strnpcinfo(2) == "arug_cas02") setarray .@i[0],33,168,245,168;
		else setarray .@i[0],65,171,212,149;	// Castelos 3, 4 e 5 são identicos.
	}
	else {
		if (strnpcinfo(2) == "schg_cas02") setarray .@i[0],231,58,335,230;
		else if (strnpcinfo(2) == "schg_cas03") setarray .@i[0],242,309,376,251;
		else setarray .@i[0],27,35,207,75;	// Castelos 1, 4 e 5 são identicos.
	}
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@j,(.@num == 1)?0:2;
	guardian strnpcinfo(2),.@i[.@j],.@i[.@j+1],((.@num == 1)?"1ª":"2ª")+" Runa Guardiã",1906+.@num,strnpcinfo(0)+"::OnGuardianStoneDied";
	end;

OnDisable:
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnGuardianStoneDied";
	setd "$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"["+(atoi(charat(strnpcinfo(1),2))-1)+"]",1;
	stopnpctimer;
	end;

OnGuardianStoneDied:
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@var$,"$agit_"+substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	setd .@var$+"["+(.@num-1)+"]",1;
	if (getd(.@var$+"[0]") == 1 || getd(.@var$+"[0]") == 2) set .@destroyed, .@destroyed+1;
	if (getd(.@var$+"[1]") == 1 || getd(.@var$+"[1]") == 2) set .@destroyed, .@destroyed+1;
	if (.@destroyed == 2) {
		mapannounce strnpcinfo(2),"Todas as Runas Guardiãs foram destruídas!",bc_map,"0x00ff00";
		donpcevent "RL0#"+strnpcinfo(2)+"::OnDisable";
	}
	else mapannounce strnpcinfo(2),"A "+((.@num == 1)?"1ª":"2ª")+" Runa Guardiã foi destruída!",bc_map,"0x00ff00";
	donpcevent "gard"+.@num+"#"+strnpcinfo(2)+"::OnReset";
	initnpctimer;
	end;

OnTimer300000:
	set .@num, atoi(charat(strnpcinfo(1),2));
	set .@str$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
	donpcevent ((.@num == 1)?"1ª":"2ª")+" Runa Guardiã#"+.@str$+"::OnEnable";
	setd "$agit_"+.@str$+"["+(atoi(charat(strnpcinfo(1),2))-1)+"]",2;
	stopnpctimer;
	end;
}

// Invocações das Barricadas (4)
//-------------------------------------------------------------
-	script	RL#template	-1,{
OnEnable:
	set .@num, atoi(charat(strnpcinfo(1),2));
	if (.@num == 0) {
		if (compare(strnpcinfo(2),"arug")) {
			if (strnpcinfo(2) == "arug_cas01") {
				setarray .@wall[0],238,74,8,6,0;
				setarray .@x[0],239,241,243,245;
				setarray .@y[0], 73, 73, 73, 73;
			}
			else if (strnpcinfo(2) == "arug_cas02") {
				setarray .@wall[0],136,136,8,6,0;
				setarray .@x[0],137,139,141,143;
				setarray .@y[0],137,137,137,137;
			}
			else {	// Castelos 3, 4 e 5 são identicos.
				setarray .@wall[0],138,110,8,6,0;
				setarray .@x[0],139,141,143,145;
				setarray .@y[0],111,111,111,111;
			}
		}
		else {
			if (strnpcinfo(2) == "schg_cas02") {
				setarray .@wall[0],290,98,8,0,0;
				setarray .@x[0],289,289,289,289;
				setarray .@y[0], 98,100,102,104;
			}
			else if (strnpcinfo(2) == "schg_cas03") {
				setarray .@wall[0],326,301,6,6,0;
				setarray .@x[0],326,328,330;
				setarray .@y[0],300,300,300;
			}
			else {	// Castelos 1, 4 e 5 são identicos.
				setarray .@wall[0],114,48,13,6,0;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0], 49, 49, 49, 49, 49, 49;
			}
		}
	}
	else if (.@num == 1) {
		if (compare(strnpcinfo(2),"arug")) {
			if (strnpcinfo(2) == "arug_cas01") {
				setarray .@wall[0],239,53,8,6,1;
				setarray .@x[0],239,241,243,240,242,244;
				setarray .@y[0], 55, 55, 55, 54, 54, 54;
			}
			else if (strnpcinfo(2) == "arug_cas02") {
				setarray .@wall[0],150,223,12,6,1;
				setarray .@x[0],151,153,155,157,159,161;
				setarray .@y[0],222,222,222,222,222,222;
			}
			else {	// Castelos 3, 4 e 5 são identicos.
				setarray .@wall[0],139,158,6,6,1;
				setarray .@x[0],140,142,144,139,141,143;
				setarray .@y[0],157,157,157,156,156,156;
			}
		}
		else {
			if (strnpcinfo(2) == "schg_cas02") {
				setarray .@wall[0],279,98,8,0,1;
				setarray .@x[0],280,280,280,281,281,281;
				setarray .@y[0], 98,100,102, 99,101,103;
			}
			else if (strnpcinfo(2) == "schg_cas03") {
				setarray .@wall[0],325,277,8,6,1;
				setarray .@x[0],326,328,330,327,329,331;
				setarray .@y[0],278,278,278,279,279,279;
			}
			else {	// Castelos 1, 4 e 5 são identicos.
				setarray .@wall[0],114,51,13,6,1;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0], 50, 50, 50, 50, 50, 50;
			}
		}
	}
	else if (.@num == 2) {
		if (compare(strnpcinfo(2),"arug")) {
			if (strnpcinfo(2) == "arug_cas01") {
				setarray .@wall[0],107,124,6,6,1;
				setarray .@x[0],107,109,111,108,110,112;
				setarray .@y[0],122,122,122,123,123,123;
			}
			else if (strnpcinfo(2) == "arug_cas02") {
				setarray .@wall[0],125,342,8,0,1;
				setarray .@x[0],126,126,126,127,127,127;
				setarray .@y[0],343,345,347,344,346,348;
			}
			else {	// Castelos 3, 4 e 5 são identicos.
				setarray .@wall[0],138,210,8,6,1;
				setarray .@x[0],140,142,144,139,141,143;
				setarray .@y[0],209,209,209,208,208,208;
			}
		}
		else {
			if (strnpcinfo(2) == "schg_cas02") {
				setarray .@wall[0],230,213,6,0,1;
				setarray .@x[0],231,231,231,232,232,232;
				setarray .@y[0],213,215,217,213,215,217;
			}
			else if (strnpcinfo(2) == "schg_cas03") {
				setarray .@wall[0],200,230,8,0,1;
				setarray .@x[0],201,201,201,202,202,202;
				setarray .@y[0],231,233,235,232,234,236;
			}
			else {	// Castelos 1, 4 e 5 são identicos.
				setarray .@wall[0],114,154,13,6,1;
				setarray .@x[0],115,117,119,121,123,125;
				setarray .@y[0],153,153,153,153,153,153;
			}
		}
	}
	else {
		if (compare(strnpcinfo(2),"arug")) {
			if (strnpcinfo(2) == "arug_cas01") {
				setarray .@wall[0],84,171,8,6,1;
				setarray .@x[0], 84, 86, 88, 90;
				setarray .@y[0],170,170,170,170;
			}
			else if (strnpcinfo(2) == "arug_cas02") {
				setarray .@wall[0],38,314,12,6,1;
				setarray .@x[0], 40, 42, 44, 46;
				setarray .@y[0],315,315,315,315;
			}
			else {	// Castelos 3, 4 e 5 são identicos.
				setarray .@wall[0],138,263,8,6,1;
				setarray .@x[0],139,141,143,145;
				setarray .@y[0],262,262,262,262;
			}
		}
		else {
			if (strnpcinfo(2) == "schg_cas02") {
				setarray .@wall[0],160,141,6,6,1;
				setarray .@x[0],160,162,164,166;
				setarray .@y[0],140,140,140,140;
			}
			else if (strnpcinfo(2) == "schg_cas03") {
				setarray .@wall[0],285,198,8,0,1;
				setarray .@x[0],284,284,284,284;
				setarray .@y[0],199,201,203,205;
			}
			else {	// Castelos 1, 4 e 5 são identicos.
				setarray .@wall[0],116,241,11,6,1;
				setarray .@x[0],116,118,120,122;
				setarray .@y[0],240,240,240,240;
			}
		}
	}
	if (.@num == 3) set getd(".MyMobCount_"+.@num+strnpcinfo(2)),4;
	else if (.@num) set getd(".MyMobCount_"+.@num+strnpcinfo(2)),6;
	setwall strnpcinfo(2),.@wall[0],.@wall[1],.@wall[2],.@wall[3],.@wall[4],substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"_"+strnpcinfo(1);
	set .@j,(getd(".MyMobCount_"+.@num+strnpcinfo(2)))?getd(".MyMobCount_"+.@num+strnpcinfo(2)):getarraysize(.@x);
	for(set .@i,0; .@i<.@j; set .@i,.@i+1)
		guardian strnpcinfo(2),.@x[.@i],.@y[.@i]," ",1905,strnpcinfo(0)+"::OnBarrierDestroyed";
	end;

OnBarrierDestroyed:
	set .@num, atoi(charat(strnpcinfo(1),2));
	if (!.@num) end;
	set getd(".MyMobCount_"+.@num+strnpcinfo(2)),getd(".MyMobCount_"+.@num+strnpcinfo(2))-1;
	if (getd(".MyMobCount_"+.@num+strnpcinfo(2)) == 0) {
		set .@var$,substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9);
		setd "$agit_"+.@var$+"["+(.@num+1)+"]",1;
		setarray .@count$[0],"1º","2º","3º";
		mapannounce strnpcinfo(2),"O "+.@count$[.@num-1]+" Portão da Fortaleza foi destruído.",bc_map,"0x00ff00";
		delwall .@var$+"_"+strnpcinfo(1);
	}
	end;

OnDisable:
	delwall substr(strnpcinfo(2),0,1)+substr(strnpcinfo(2),8,9)+"_"+strnpcinfo(1);
	killmonster strnpcinfo(2),strnpcinfo(0)+"::OnBarrierDestroyed";
	end;
}

// Bandeiras de Ligação (função)
//-------------------------------------------------------------
function	script	LinkFlag	{
	if (!getcharid(2) || getcharid(2) != getcastledata(strnpcinfo(4),1)) end;
	if (getarg(0) == "Facilidade de Conveniência") {
		mes "^3355FFEste é o Serviço de";
		mes "Teleporte da Fortaleza. Gostaria";
		mes "de ser teleportado para";
		mes "a Facilidade de Conveniência de";
		mes "membros do clã?^000000";
		if(select("Ír para Facilidade de Conveniência:Cancel") == 1)
			warp strnpcinfo(4),getarg(1),getarg(2);
		close;
	}
	if (getarg(0) == "Central do Emperium") {
		mes "^3355FFEste é o Serviço de";
		mes "Teleporte da Fortaleza. Gostaria";
		mes "de ser teleportado para";
		mes "a Central do Emperium?^000000";
		if(select("Teleportar:Cancelar") == 1)
			warp strnpcinfo(4),getarg(1),getarg(2);
		close;
	}
	mes "^3355FFEste é o Serviço de";
	mes "Teleporte da Fortaleza. Por favor,";
	mes "escolha um destino";
	mes "dentro da fortaleza.^000000";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+3)
		set .@menu$, .@menu$+getarg(.@i)+":";
	set .@menu$, .@menu$+"Cancel";
	set .@i, select(.@menu$)-1;
	if (.@i != getargcount()/3)
		warp strnpcinfo(4),getarg(.@i*3+1),getarg(.@i*3+2);
	close;
}

// Bandeiras de Retorno (função)
//-------------------------------------------------------------
function	script	ReturnFlag	{
	set .@str$, (compare(strnpcinfo(4),"aru"))?"Arunafeltz":"Schwaltzvalt";
	set .@GID, getcastledata(getarg(0),1);
	if (!.@GID) {
		mes "[ Decreto Real de "+.@str$+" ]";
		mes "O Santo Rei de";
		mes .@str$+" declara que";
		mes "esta fortaleza não esta";
		mes "sobe dominio. Quem quebrar";
		mes "esse Emperium será reconhecido";
		mes "como seu novo proprietário.";
		close;
	}
	if (getcharid(2) == .@GID && getarg(1,0)) {
		mes "[ Voz sussurrando ]";
		mes "Um corajoso,";
		mes "você deseja retonar";
		mes "a sua fortaleza?";
		next;
		if(select("Retornar a Fortaleza:Cancelar") == 1 && getcharid(2) == getcastledata(getarg(0),1)) {
			if (compare(getarg(0),"arug")) {
				if (getarg(0) == "arug_cas01") setarray .@i[0],67,193;
				else if (getarg(0) == "arug_cas02") setarray .@i[0],43,256;
				else setarray .@i[0],121,318;	// Castelos 3, 4 e 5 são identicos.
			}
			else {
				if (getarg(0) == "schg_cas02") setarray .@i[0],136,188;
				else if (getarg(0) == "schg_cas03") setarray .@i[0],308,202;
				else setarray .@i[0],120,290;	// Castelos 1, 4 e 5 são identicos.
			}
			warp getarg(0),.@i[0],.@i[1];
		}
		close;
	}
	mes "[ Decreto Real de "+.@str$+" ]";
	mes "O Santo Rei de";
	mes .@str$+" declara que";
	mes "esta Fortaleza está sobe";
	mes "dominio do Clã ^FF0000"+getguildname(.@GID)+"^000000.";
	next;
	mes "[ Decreto Real de "+.@str$+" ]";
	mes "^FF0000"+getguildmaster(.@GID)+"^000000 é";
	mes "o Mestre do Clã ^FF0000"+getguildname(.@GID)+"^000000.";
	mes "Qualquer objeção deve reivindicar";
	mes "esta fortaleza atráves da força do aço";
	mes "e da mágia e assim ser nomeado";
	mes "mestre com o seu Clã.";
	close;
}

// Interruptores da Sala do Tesouro
//-------------------------------------------------------------
-	script	Switch#template	-1,{
	mes " ";
	mes "^3355FFVocê vai puxar";
	mes "essa pequena alavanca?^000000";
	next;
	if(select("Puxar Alavanca:Cancelar") == 2) close;
	if (compare(strnpcinfo(4),"arug")) {
		if (strnpcinfo(4) == "arug_cas01") setarray .@i[0],121,357;
		else if (strnpcinfo(4) == "arug_cas02") setarray .@i[0],387,323;
		else setarray .@i[0],321,57;	// Castelos 3, 4 e 5 são identicos.
	}
	else {
		if (strnpcinfo(4) == "schg_cas02") setarray .@i[0],339,79;
		else if (strnpcinfo(4) == "schg_cas03") setarray .@i[0],57,13;
		else setarray .@i[0],275,244;	// Castelos 1, 4 e 5 são identicos.
	}
	warp strnpcinfo(4),.@i[0],.@i[1];
	close;
}

// Teleporte do Calabouço do Clã
//-------------------------------------------------------------
-	script	Sunflower#template	-1,{
	if (getcharid(2) == getcastledata(strnpcinfo(4),1)) {
		mes "- É um girassol fanstaticamente grande; tão grande quando um ser humano!... Você sente algo misterioso que emanando da flor. -";
		next;
		switch(select("Segurar a haste.:Não fazeer nada.")) {
		case 1:
			if (compare(strnpcinfo(4),"arug")) {
				set .@map$,"arug_dun01";
				setarray .@mapx[0],350,350,50, 50,200;
				setarray .@mapy[0],350, 50,50,350,386;
			}
			else {
				set .@map$,"schg_dun01";
				setarray .@mapx[0],262, 94, 79,212,322;
				setarray .@mapy[0],314,284,140, 70,166;
			}
			set .@i, atoi(charat(strnpcinfo(4),9))-1;
			warp .@map$,.@mapx[.@i],.@mapy[.@i];
			close;
		case 2:
			mes "É muito assutador para tocar em coisas desconhecidas.";
			close;
		}
	}
}