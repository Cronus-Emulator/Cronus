//===== Hercules Script ======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.8
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Improved compatibility with Zonda Staff NPCs. Modernized syntax. [Haru]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
// Traduzido por: Lucas Campos
//==========================================================================

function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch (getarg(0)) {
		default:
		case 0:
			// Default message (obsolete)
			mes "[Empregada Kafra]";
			mes "Bem vindo a";
			mes "Corporação Kafra.";
			mes "Os serviços da Kafra";
			mes "estão sempre ao seu lado.";
			mes "Como eu posso ajudar você?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Empregada Kafra]";
			mes "^666666W-bemmm-vindoo";
			mes "A Coor-portaçao";
			mes "Kaaa-fraa...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			@GID = getcharid(2);
			mes "[Empregada Kafra]";
			mes "Bem vindo. Membro da GUIDA ^ff0000" + getguildname(@GID) + "^000000.";
			mes "A Corportação Kafra vai estar com você onde você for.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Empregada Kafra]";
			mes "Então, você veio de um lugar tão longe estudar nossa cultura, ou apenas para turismo?";
			mes "Em ambos os casos, Porque não fica mais um pouco?";
			mes "O ar é eterno passado com o";
			mes "agradável aroma das flores silvestres.";
			break;
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Empregada Kafra]";
			mes "Com outros muitos serviços da";
			mes "locação Kafras, você nunca mais";
			mes "vai andar tanto.";
			break;
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (true) {
		deletearray @K_Menu0$;
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"Usar Armazém","Usar Armazém da Guild","Alugar Carrinho","Usar Serviço de Teletransporte","Cancelar";
		} else {
			switch (getarg(1)) {
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"Usar Armazém","Cancelar"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Checar Outros Serviços","Cancelar"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Checar Outros Serviços","Cancelar"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"Usar Armazém","Checar Outros Serviços","Cancelar"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Checar Outros Serviços","Cancelar"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Usar Serviço de Teletransporte","Checar Outros Serviços","Cancelar"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"Usar Armazém","Salvar","Alugar Carrinho","Cancelar"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teletransporte","Alugar Carrinho","Checar Outros Serviços","Cancelar"; break;
			}
		}
		.@menu$ = "";
		for (.@i = 0; .@i < getarraysize(@K_Menu0$); ++.@i)
			.@menu$ += @K_Menu0$[.@i] + ":";
		.@j = select(.@menu$) - 1;
		if (@K_Menu0$[.@j] == "Salvar") {
			return;
		} else if (@K_Menu0$[.@j] == "Usar Armazém") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2)
				callfunc "F_KafStor",2,0,0,"[Empregada Kafra]";
			else
				callfunc "F_KafStor",0,getarg(3),getarg(0),"[Empregada Kafra]";
			next;
		} else if (@K_Menu0$[.@j] == "Usar Serviço de Teletransporte") {
			// Display Einbroch "Sem serviço de teletransporte" notice.
			if (getarg(1) != 4) {
				callfunc "F_KafTele",getarg(0),"[Empregada Kafra]";
			} else {
				mes "[Empregadas Kafra]";
				mes "Por causa do ^FF0000Acordo";
				mes "Transporte Limitado^000000, A";
				mes "Corporação Kafra não pode";
				mes "providênciar o serviço de teletransporte";
				mes "para a república Schwaltzvalt.";
				next;
				mes "[Empregadas Kafra]";
				mes "Nos pedimos para o senhor, por favor";
				mes "para usar o serviço de aviação";
				mes "Obrigado pela sua paciência, ";
				mes "entendimento e cooperação.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "Alugar Carrinho") {
			if(callfunc("F_KafCart",getarg(0),getarg(4),"[Empregada Kafra]") == 1)
				next;
		} else if (@K_Menu0$[.@j] == "Checar Outros Serviços") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "Cancelar"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		} else if (@K_Menu0$[.@j] == "Usar Armazém da Guild") {
			callfunc "F_KafStor",1,0,0,"[Empregada Kafra]";
			next;
		}
	}
}

//===================================================
// Storage Function
//=   arg(0): Specifies the type of storage that has been requested:
//            0 = Regular storage
//            1 = Guild storage
//            2 = Regular storage, in a castle (free of charge)
//=   arg(1): Cost to use storage.
//=   arg(2): The type of NPC this is being called from. Notable values:
//            0,2-5 = Regular Kafra
//            1 = Niflheim Kafra (special messages)
//            6 = Zonda/Cool Event Staff (no RESRVPTS)
//=   arg(3): NPC title to display.
//===================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if (getarg(0) == 1) {
		if (guildopenstorage()) {
			mes getarg(3);
			mes "Me desculpe, mas outro membro da guild está usando o armazém";
			mes "nesse exato momento. Por favor, aguarde até que seja finalizado.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (basicskillcheck() && getskilllv(NV_BASIC) < 6) {
		mes getarg(3);
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-S-sscorro,";
			mes "V-ocê é umm-mmm";
			mes "novato... Pree-cisa";
			mes "de habilidades B-asica L-level 6...^000000";
			return;
		}
		mes "Me desculpe, mas você";
		mes "precisa de habilidades";
		mes "básicas Nivel 6 para";
		mes "usar o serviço de Armazém.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (getarg(0) != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(Cargo_Free_Ticket)) {
			delitem Cargo_Free_Ticket, 1;
		} else {
			if (Zeny < getarg(1)) {
				mes getarg(3);
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-more z-zeny...!";
					mes "N-neeed 150... zeny...";
					mes "Ergh! T-taking bl-blood~!^000000";
					return;
				}
				// Standard Message
				mes "Me desculpe, mas você";
				mes "não tem zeny suficiente para";
				mes "usar o serviço de Armazém. Our";
				mes "A taxa é de "+getarg(1)+" zeny.";
				return;
			}
			Zeny -= getarg(1);
			if (getarg(2) != 6)
				RESRVPTS += getarg(1) / 5;
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		if (getarg(2) == 1) {
			// Niflheim specific Message
			percentheal 0,-10;
			mes getarg(3);
			for (.@i = 0; .@i < 5; ++.@i)
				mes "^666666Thank you.. for... using...^000000";
		} else if (getarg(2) == 6) {
			// Zonda
			mes getarg(3);
			mes "Let me open your personal";
			mes "storage for you right away.";
			mes "Thanks for supporting Cool";
			mes "Event Corp. by using our";
			mes "services. Have a good day~";
		} else {
			// Normal message
			mes getarg(3);
			mes "Here, let me open";
			mes "your Storage for you.";
			mes "Thank you for using";
			mes "the Kafra Service.";
		}
	}
	callfunc("F_CheckKafCode"); //check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//=   arg(2): The type of NPC this is being called from. Notable values:
//            2 = Guild Castle Kafra (don't consume tickets)
//            6 = Zonda/Cool Event Staff (no RESRVPTS)
//=   arg(1): NPC title to display.
//===================================================
function	script	F_KafTele	{
	mes getarg(1);
	mes "Por favor";
	mes "Escolha seu destino.";
	next;
	.@menu$ = "";
	for (.@i = 0; .@i <= getarraysize(@wrpD$); ++.@i)
		.@menu$ += @wrpC$[.@i] + ":";
	.@j = select(.@menu$) - 1;
	if (@wrpC$[.@j] == "Cancelar")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(Warp_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Warp_Free_Ticket, 1;
	} else {
		if (Zeny < @wrpP[.@j]) {
			mes getarg(1);
			mes "Me desculpe, mas você não tem";
			mes "zeny suficiente para o teletransporte";
			mes "A taxa";
			mes "para "+@wrpD$[.@j]+" é de "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		Zeny -= @wrpP[.@j];
		if (getarg(0) != 6)
			RESRVPTS += @wrpP[.@j] / 16;
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (RENEWAL) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	else if (@wrpD$[.@j] == "Veins") warp "veins",205,101;
	else if (@wrpD$[.@j] == "Rachel") warp "rachel",115,125;
	end;
}


//===================================================
// Cart Function
//=   arg(0): The type of NPC this is being called from. Notable values:
//            0,1,3-5 = Regular Kafra
//            2 = Guild Castle Kafra (don't consume tickets)
//            6 = Zonda/Cool Event Staff (no RESRVPTS)
//=   arg(1): Cost to rent a cart.
//=   arg(2): NPC title to display.
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes getarg(2);
		mes "Me desculpe, mas o serviço";
		mes "de alugar carrinho";
		mes "é apenas disponivel para Comerciantes,";
		mes "Ferreiros, Master Smiths,";
		mes "Alquimistas e Bioquímicos.";
		return 1;
	}
	if (getskilllv(MC_PUSHCART) == 0) {
		mes getarg(2);
		mes "Você só pode alugar um carrinho depois";
		mes "que aprender a habilidade Carrinho de Mão.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	if (checkcart() == 1) {
		mes getarg(2);
		mes "Você já tem um";
		mes "carrinho equipado.";
		mes "Infelizmente, Não podemos";
		mes "emprestar mais que um";
		mes "para cada cliente.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(Cart_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Cart_Free_Ticket, 1;
	} else {
		mes getarg(2);
		mes "O aluguel do carrinho é de";
		mes ""+getarg(1)+" zeny. Você";
		mes "gostaria de alugar?";
		next;
		if (select("Alugar Carrinho.:Cancelar") == 2)
			return 0;
		if (Zeny < getarg(1)) {
			mes getarg(2);
			mes "Me desculpe, mas você";
			mes "não tem zeny";
			mes "suficiente para pagar o Aluguel";
			mes "do Carrinho, a taxa é de "+getarg(1)+" zeny.";
			return 1;
		}
		Zeny -= getarg(1);
		if (getarg(0) != 6)
			RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//.@block = 1;
	setarray .@m$, "Verificar Reserva Especial de Pontos","Serviço de Senha do Armazém","Localizações dos Empregados da Kafra","Cancelar";
	if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight: remove Storage Password and Kafra Employee Locations
	else if (.@block) deletearray .@m$[1],1; // Remove Storage Password
	while (true) {
		.@j = select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "Verificar Reserva Especial de Pontos") {
			mes "[Empregada Kafra]";
			mes "Deixe-me ver...";
			mes strcharinfo(0) + "...";
			mes "Ah, Você tem no total ";
			mes RESRVPTS+ " Reserva especial de pontos.";
			next;
			mes "[Empregada Kafra]";
			mes "Você pode trocar os seus";
			mes "Pontos de Reserva Especial por";
			mes "recompensas no Escritório Principal Kafra em toda Al De Baran. Por favor,";
			mes "utilize nosso conveniênte serviço para ver o benefícios de nossos programas de recompensas.";
			next;
			if(getarg(0) == 1)
				return;
		} else if (.@m$[.@j] == "Localizações dos Empregados da Kafra") {
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "Serviço de Senha do Armazém") {
			callfunc("F_SetKafCode","[Empregada Kafra]","Serviços Kafra");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Empregada Kafra]";
	if (getarg(1) == 1) {
		// Save
		mes "Seu ponto de renascimento";
		mes "foi salvo aqui";
		mes getarg(2)+".";
		mes "Obrigado por usar";
		mes "os serviços Kafra.";
	} else if (getarg(0) == 0 || getarg(0) == 5) {
		// Generic End
		mes "Nós, aqui da Corportação Kafra,";
		mes "estamos sempre nos esforçando para providênciar a você o melhor. Nós esperamos atender as suas necessidades e padrões de excelência de aventura.";
	} else if (getarg(0) == 1) {
		// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra nun-ca";
		mes "morrrr-e...";
		mes "On se--mpree aoooo se-uuu laaaddoo~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Entre com sua senha:";
	@code_ = 0;
	input @code_;
	if (@code_ != #kafra_code) {
		dispbottom "Senha do armazém errada.";
		close2;
		cutin "",255;
		end;
	}
	@kafcode_try = 0;
	@code_ = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	mes getarg(0);
	if (#kafra_code) {
		mes "Seu armazém está protegido por senha. O que você gostaria de fazer agora?";
		next;
		switch (select("Mudar senha antiga -> 5000z:Remover senha do armazém -> 1000z:Cancelar")) {
			case 1:
				mes getarg(0);
				mes "At first, please enter your ^0000FFold password^000000.";
				@code = callfunc("F_EntKafCode");
				if (!@code || @code != #kafra_code) {
					mes "Senha errada. Você não entrou com uma nova senha.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			case 2:
				mes getarg(0);
				mes "Por favor, Digite sua senha para ser removida.";
				@code = callfunc("F_EntKafCode");
				if (!@code) {
					mes "A senha foi removida.";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if (Zeny < 1000) {
					mes "Você não tem zeny suficiente.";
					emotion e_cash;
					break;
				}
				Zeny -= 1000;
				if (@code == #kafra_code) {
					#kafra_code = 0;
					mes "Você removeu com sucesso a senha do Armazém.";
					mes "Obrigado por usar "+getarg(1)+".";
					emotion e_thx;
				} else {
					mes "Senha errada. Nos vamos retornar seus 1000z.";
					mes "Obrigado, na próxima vez entre com sua senha correta.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes getarg(1)+" orgulhosamente apresenta um novo serviço:";
		mes "Armazém protegido por senha.";
		next;
		switch (select("Digite uma nova senha -> 5000z:Cancelar")) {
			case 1: callsub S_SET,getarg(0),getarg(1); break;
			case 2: break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "Agora entre com uma ^FF0000nova senha^000000 para proteger seu armazém de possiveis ladrões";
	@code = callfunc("F_EntKafCode");
	if (!@code) {
		mes "A senha não foi alterada.";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if (Zeny < 5000) {
		mes "Você não tem zeny suficiente.";
		emotion e_cash;
		return;
	}
	Zeny -= 5000;
	#kafra_code = @code;
	mes "Você protegeu seu armazém com uma senha secreta.";
	mes "Obrigado por usar "+getarg(1)+".";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Entre com o número 1000~10000000:";
	@code_ = 0;
	++@kafcode_try;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Hack: Tentou descobrir a senha do armazém.";
	}
	if (input(@code_) == 1) {
		mes "Você não pode usar senhas grandes.";
		return 0;
	}
	if (@code_ < 1000) {
		mes "Você não deveria usar senhas curtas.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$;
	deletearray @wrpD$;
	deletearray @wrpP$;
	deletearray @viewpX;
	deletearray @viewpY;
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$, "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		setarray @wrpP, 600, 1200, 1200, 1200, 1200, 1800;
		setarray @viewpX, 151, 29, 282, 152;
		setarray @viewpY, 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP, 1200, 1800, 1800;
		setarray @wrpD$, "Payon", "Morroc", "Prontera";
		setarray @viewpX, 28, 113, 0, 0;
		setarray @viewpY, 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP, 1200, 1200, 1800, 1700;
		setarray @wrpD$, "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$, "Morroc", "Comodo Pharos Beacon", "Umbala";
		setarray @wrpP, 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$, "Comodo", "Morroc";
		setarray @wrpP, 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$, "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP, 1200, 1200, 1200, 1700;
		setarray @viewpX, 120, 203;
		setarray @viewpY, 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$, "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP, 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$, "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP, 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX, 156, 163, 28, 292;
		setarray @viewpY, 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$, "Comodo";
		setarray @wrpP, 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$, "Prontera", "Alberta", "Morroc";
		setarray @wrpP, 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$, "Al De Baran";
		setarray @wrpP, 1200;
		setarray @viewpX, 328, 278, 153, 0;
		setarray @viewpY, 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$, "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP, 600, 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "rachel") {
		setarray @wrpD$, "Veins";
		setarray @wrpP, 2200;
	} else if (strnpcinfo(4) == "veins") {
		setarray @wrpD$, "Rachel";
		setarray @wrpP, 2200;
	}
	for (.@i = 0; .@i < getarraysize(@wrpD$); ++.@i)
		@wrpC$[.@i] = @wrpD$[.@i]+" -> "+@wrpP[.@i];
	@wrpC$[getarraysize(@wrpC$)] = "Cancelar";
	return;
}
