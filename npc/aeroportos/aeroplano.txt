//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Nome do NPC:                                                ||
// - aeroplano.txt                                               ||
//===============================================================||
// = Criação:                                                    ||
// - eA/Herc/Cronus                                              ||
//===============================================================||
// = Versão:                                                     ||
// - 1.0                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Cabeçalho [SlexFire]                                        ||
// - Tradução [Revisões Cronus] / [SlexFire]                     ||
// - Func. Sex, adicionada em algumas partes [SlexFire]          ||
//===============================================================||
// = Descrição:                                                  ||
// - Sistema de Aeroplano usado em servidores oficiais           ||
//===============================================================||
// = Observações:                                                ||
// - Rota doméstica do Aeroplano                                 ||
//   Juno->Einbroch->Lighthalzen->Einbroch->Juno->Hugel          ||
//---------------------------------------------------------------||
// - Rota internacional do Aeroplano (RENEWAL)                   ||
//   ... -> Izlude -> Juno -> Rachel -> ...                      ||
//===============================================================//

airplane,243,73,0	script	#AirshipWarp-1	WARPNPC,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	WARPNPC,1,1

airplane,1,1,0	script	Aeroplano_Domestico	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Estamos indo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Logo chegaremos em Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation = 1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando. Nosso próximo destino é Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Estamos indo para Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Logo chegaremos em Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation = 2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Lighthalzen. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Atualmente estamos em Lighthalzen. O Aeroplano vai decolar em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está deixando o solo. Nosso próximo destino é Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Estamos indo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Logo chegaremos em Einbroch.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	$@airplanelocation = 1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Einbroch. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está decolando. Nosso próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Logo chegaremos em Juno.",bc_map,"0xff8200";
	end;
OnTimer300000:
	$@airplanelocation = 0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Atualmente estamos em Juno. O Aeroplano sairá em breve.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está deixando o solo. Nosso próximo destino é Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Estamos indo para Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Logo chegaremos em Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	$@airplanelocation = 3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Hugel. Tenha uma boa viagem.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Atualmente estamos em Hugel. O Aeroplano sairá em breve.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está deixando o solo. Nosso próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Logo chegaremos em Juno.",bc_map,"0xff8200";
	end;
OnTimer460000:
	$@airplanelocation = 0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Atualmente estamos em Juno. O Aeroplano sairá em breve.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Aeroplano está deixando o solo. Nosso próximo destino é Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Saída#airplane1a::ExitAirplane	4_BOARD3,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Saída#airplane1b	4_BOARD3
airplane,240,40,1	duplicate(ExitAirplane)	Saída#airplane2a	4_BOARD3
airplane,247,40,1	duplicate(ExitAirplane)	Saída#airplane2b	4_BOARD3

airplane,100,69,3	script	Tripulação do Aeroplano#ein-1	4_M_EIN_SOLDIER,{
	mes "[Tripulação do Aeroplano]";
	mes "Se já pousamos no";
	mes "seu destino e você";
	mes "gostaria de sair do";
	mes "Aeroplano, por favor use as";
	mes "escadas à frente. Obrigado";
	mes "por seu apoio.";
	close;
}

airplane,64,94,1	script	Criança de Umbala#ein_p	4_M_UMKID,{
	emotion e_swt2;
	mes "[Criança]";
	if (event_umbala >= 3) {
		mes "Uau, mãe!";
		mes "V-veja isso!";
		mes "Nós estamos voando! N-nós estamos...";
		mes "Estamos conhecendo o céu!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Senhora de Umbala#ein_p	4_F_UMWOMAN,{
	emotion e_dots;
	mes "[Senhora]";
	if (event_umbala >= 3) {
		mes "Shush...";
		mes "Querido, comporte-se~";
		mes "Não se anime tanto";
		mes "enquanto não sairmos";
		mes "deste lugar público!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Homem de Umbala#ein_p	4_M_UMSOLDIER,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Ei você~";
		mes "Pela sua aparência,";
		mes "percebo que você";
		mes "consegue me entender.";
		mes "^333333*Legal...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Tenho ajudado a equipe";
		mes "do Aeroplano, auxiliando";
		mes "seus Tripulantes que têm";
		mes "medo de altura os levanto para pular";
		mes "de bungee jumping em Umbala. Legal, em?";
		next;
		mes "[Chrmlim]";
		mes "Mas...";
		mes "Alguns deles não podem";
		mes "superar seus medos.";
		mes "Devem participar de";
		mes "algumas experiências, ^333333em Nifflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Aeroplano.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Aeroplano kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Equipe do Aeroplano#airplane	1_F_02,{
	// Adição da Quest de Hugel
	if (hg_ma1 == 3) {
		mes "[Equipe do Aeroplano]";
		mes "Bem-vind"+(Sex?"o":"a")+"";
		mes "ao Aeroplano.";
		mes "Como posso ajudá-l"+(Sex?"o?":"a?")+"";
		next;
		select("Você tem um passageiro chamado Thierry?");
		mes "[Equipe do Aeroplano]";
		mes "Me desculpe, mas não conheço nenhum dos passageiros por esse nome.";
		close;
	}
	// Fim da Quest de Hugel
	mes "[Assistente do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+"";
	mes "ao Aeroplano.";
	mes "Como posso ajudá-l"+(Sex?"o?":"a?")+"";
	next;
	switch (select("Usando o Aeroplano:Cabine do Capitão:Instalações:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver uma mensagem";
		mes "anunciando que nós";
		mes "chegamos ao seu destino";
		mes "por favor, use uma das saídas";
		mes "localizadas ao Norte";
		mes "e ao Sul do aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Se acontecer de você perder";
		mes "sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente";
		mes "na rota e você terá";
		mes "outra chance de descer";
		mes "em seu destino.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão";
		mes "fica na parte frontal";
		mes "do Aeroplano. Lá, você";
		mes "pode conhecer o capitão e";
		mes "o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui";
		mes "vários mini-games para";
		mes "o entretenimento de todos";
		mes "os passageiros. Você está";
		mes "convidad"+(Sex?"o":"a")+" a testar sua sorte";
		mes "nos Mini-Games do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa";
		mes "estadia durante o vôo em";
		mes "nosso Aeroplano. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	4_M_BUDDHIST,{
	mes "[Zerta]";
	mes "Oh, olá Aventureiro.";
	mes "Estou atualmente em uma";
	mes "Jornada Santa, orando";
	mes "em propósito do continente";
	mes "de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	4_F_06,{
	mes "[Maelin]";
	mes "Humm, este avião vai para";
	mes "Lutie, não? Estou";
	mes "esperando há muito tempo,";
	mes "mas até agora nada de";
	mes "anúncio sobre Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	4_F_GON,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "O céu tem uma aparência";
	mes "tão diferente e";
	mes "adorável, visto daqui de cima!";
	close;
}

airplane,221,158,2	script	Piloto#airplane	4_M_EIN_SOLDIER,{
	// Adição da quest de Hugel
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Espero que o drink esteja fresco e saboroso.";
		mes "Bebidas são a minha vida! Beber me dá energia!";
		mes "Não vivo sem bebidas!";
		next;
		mes "[Piloto]";
		mes "Mas! Dirigir sob influência da bebida não é bom.";
		mes "Porém! Preciso estar bebendo mais e mais!";
		emotion e_sob;
		next;
		select("Você conhece um passageiro chamado Thierry?");
		mes "[Piloto]";
		mes "Este uniforme";
		mes "é realmente bonito, mas";
		mes "não gosto de usá-lo";
		mes "enquanto estou no Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ninguém nunca realmente";
		mes "entrou neste quarto.";
		mes "E como sou o capitão.^FFFFFF ^000000 Eu poderia simplesmente ficar de cuecas.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ah!? O que é isto!";
		next;
		mes "- ...Ele não está ouvindo tudo que você fala. -";
		close;
	}
	// Fim da quest de Hugel
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Faz bastante tempo";
		mes "desde que eu";
		mes "experimentei um bom e gelado";
		mes "álcool. Mas a";
		mes "profissão requer que eu";
		mes "esteja sempre sóbrio!";
		next;
		mes "[Piloto]";
		mes "Sempre beba com responsabilidade!";
		mes "Ainda sim, não lembro a";
		mes "última vez que entrei de férias.";
		mes "Sim, uma bebida, uma TV";
		mes "e tudo ficará ótimo.";
		emotion e_sob;
		close;
	case 2:
		mes "[Piloto]";
		mes "Cara, o clima";
		mes "está ótimo hoje.";
		mes "Brilho, o céu está aberto";
		mes "com uma boa visibilidade";
		mes "e segurança, um vôo despreocupado.";
		close;
	case 3:
		mes "[Piloto]";
		mes "Você sabe, seu capitão é";
		mes "um homem respeitado. Ele e";
		mes "seu irmão são atualmente";
		mes "os mais conhecidos na indústria aérea.";
		mes "Quem diría que nos tornariamos";
		mes "capitães de vôo tão bons?";
		next;
		mes "[Piloto]";
		mes "Apenas entre nós,";
		mes "Tenho que dizer a você,";
		mes "o Papai Noel foi para algum";
		mes "lugar com renas e elfos";
		mes "que trabalham para ele.";
		mes "Aquele cara é um gênio!";
		close;
	default:
		mes "[Piloto]";
		mes "Você sabe, toda essa";
		mes "coisa de pilotar no ar,";
		mes "é algo bastante recente, você sabe não é?";
		mes "Sim, temos um Aeroplano";
		mes "voando a uma incrível velocidade.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ainda assim, há sempre algo sério,";
		mes "realmente acontecendo.";
		mes "Quer dizer, estávamos";
		mes "treinando enquanto o Aeroplano";
		mes "estava sendo inventado.";
		mes "Não é uma loucura?!";
		close;
	}
}

airplane,50,66,5	script	Mercador de Maçãs#airplane	4_M_04,{
	mes "[Fruitz]";
	mes "Bem-vind"+(Sex?"o":"a")+" a loja do Fruit'z";
	mes "onde você pode";
	mes "comprar maçãs ou triturá-las";
	mes "para fazer Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Por que você está aqui?:Cancelar.")) {
	case 1:
		mes "[Fruitz]";
		mes "Por favor me diga a quantidade";
		mes "de Maçãs que você deseja";
		mes "comprar. Cada Maçã custa 15 zeny";
		mes "e você pode comprar no máximo";
		mes "500 por vez. Digite '0' para cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Obrigado por parar";
				mes "na minha loja. Adeus!";
				mes "Venha a qualquer hora quando";
				mes "você sentir vontade de ter uma";
				mes "Maçã para fazer um lanche ~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Você digitou um número";
				mes "maior do que o valor máximo";
				mes "de 500. Por favor digite";
				mes "novamente o número de maçãs";
				mes "que você deseja comprar.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "que irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Fruitz]";
					mes "Obrigado por parar";
					mes "na minha loja. Adeus!";
					mes "Venha a qualquer hora quando";
					mes "você sentir vontade de ter uma";
					mes "Maçã para fazer um lanche ~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Sinto muito, mas você não";
			mes "tem dinheiro suficiente para";
			mes "comprar muitas Maçãs.";
			mes "Por favor, verifique o seu zeny ou";
			mes "compre menos Maçãs.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Hmmm, eu não acho que";
			mes "que você tenha espaço suficiente no";
			mes "seu inventário para levar";
			mes "muitas Maçãs. Por que você não libera algum espaço no seu inventário?";
			close;
		}
		else {
			Zeny -= .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Obrigado pela visita";
			mes "na minha loja. Espero que você goste";
			mes "do aroma dessas Maçãs ~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Tudo bem, precisarei de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você";
		mes "Gostaria de continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Me desculpe,mas você não possui";
				mes "os materiais suficientes para";
				mes "criar uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de 3 Maçãs";
				mes "e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Obrigado,";
				mes "por favor espere um momento.";
				next;
				mes "^3355FF*Grind grind*";
				mes "*Grind grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Maçã
				mes "[Fruitz]";
				mes "Aqui está~";
				mes "Espero que você goste!";
				mes "Por favor fique a vontade";
				mes "para comprar Maçãs";
				mes "ou fazer Sucos de Maçã quando";
				mes "você precisar, aventureiro~";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Obrigado por parar";
			mes "na minha loja. Adeus!";
			mes "Venha a qualquer hora quando";
			mes "você sentir vontade de ter uma";
			mes "Maçã para fazer um lanche ~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Eu costumava ser um andarilho";
		mes "vagabundo, até que um dia,";
		mes "Tirei uma soneca e algo";
		mes "atingiu minha cabeça e";
		mes "acordei do meu sono tranquilo.";
		next;
		mes "[Fruitz]";
		mes "Acontece que eu estava";
		mes "dormindo embaixo de uma Macieira";
		mes "e foi quando uma maçã caiu";
		mes "e atingiu minha cabeça.";
		mes "Eu estava morrendo de fome";
		mes "e estava prestes a comer aquela Maçã...";
		next;
		mes "[Fruitz]";
		mes "Mas de repente, Kain, meu antigo";
		mes "amigo desde os tempos de mineração,";
		mes "me pediu para ajudá-lo aqui";
		mes "no Aeroplano. Então eu aceitei, e";
		mes "foi lá que encontrei pessoas";
		mes "jogando um Jogo de Dados.";
		next;
		mes "[Fruitz]";
		mes "Estava entediado e curioso";
		mes "e acabei apostando algumas simples";
		mes "Maçãs no jogo dos dados.";
		mes "Mas por alguma razão,";
		mes "eu tive uma incrível sorte.";
		mes "Uma maçã se multiplicou em duas... ";
		next;
		mes "[Fruitz]";
		mes "Duas tornaram-se quatro";
		mes "e antes que eu percebesse, eu já tinha feito";
		mes "um mercado de Maçãs!";
		mes "Ganhei tantas maçãs, e";
		mes "acabei começando meu próprio negócio";
		mes "aqui no Aeroplano. Estranho, não acha?";
		next;
		mes "[Fruitz]";
		mes "Então As maçãs são boas";
		mes "para você. Elas foram";
		mes "certamente muito boas";
		mes "para mim. Hahahahaah~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Obrigado por";
		mes "usar minha loja";
		mes "Adeus~";
		close;
	}
}

//============================================================
//= Rota internacional do Aeroplano
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================

airplane_01,243,73,0	script	#AirshipWarp-3	WARPNPC,1,1,{
	end;

OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1:
		if (RENEWAL)
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; 
	    end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	WARPNPC,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Estamos indo para Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Logo chegaremos em Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation2 = 1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Izlude. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Estamos atualmente em Izlude. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando. Nosso próximo destino é Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Estamos indo para Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Logo chegaremos em Juno.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation2 = 2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Estamos atualmente em Juno. O Aeroplano sairá em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está deixando o solo. Nosso próximo destino é Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Estamos indo para Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Logo chegaremos em Rachel.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	$@airplanelocation2 = 0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Rachel. Tenha uma boa viagem.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Estamos atualmente em Rachel. O Aeroplano vai decolar em breve.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando. Nosso próximo destino é Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	++.moninv;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		.moninv = 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Saída#airplane_011a::ExitAirplane01	4_BOARD3,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Saída#airplane_011b	4_BOARD3
airplane_01,240,40,1	duplicate(ExitAirplane01)	Saída#airplane_012a	4_BOARD3
airplane_01,247,40,1	duplicate(ExitAirplane01)	Saída#airplane_012b	4_BOARD3

airplane_01,250,58,2	script	Equipe do Aeroplano#airplane01	1_F_02,{
	mes "[Equipe do Aeroplano]";
	mes "Bem-vind"+(Sex?"o":"a")+"";
	mes "ao Aeroplano.";
	mes "Como posso ajudá-l"+(Sex?"o?":"a?")+"";
	next;
	switch (select("Usando o Aeroplano:Cabine do Capitão:Instalações:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver o aviso";
		mes "anunciando que chegamos";
		mes "no seu destino desejado,";
		mes "por favor, use uma das saídas";
		mes "localizadas nas partes";
		mes "norte e sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Se acontecer de você perder";
		mes "sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente";
		mes "na rota e você terá";
		mes "outra chance para chegar";
		mes "ao seu destino pretendido.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão";
		mes "fica na parte frontal";
		mes "do Aeroplano. Lá, você";
		mes "pode conhecer o capitão e";
		mes "o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui";
		mes "vários mini-games para";
		mes "o entretenimento de todos";
		mes "os passageiros. Você está";
		mes "convidad"+(Sex?"o":"a")+" a testar sua sorte";
		mes "nos Mini-Games do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa";
		mes "estadia durante o vôo em";
		mes "nosso Aeroplano. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane_01,50,66,5	script	Mercador de Maçãs#air01	4_M_04,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	mes "[Meltz]";
	mes "Bem-vind"+(Sex?"o":"a")+" a loja do Meltz's";
	mes "onde você pode";
	mes "comprar maçãs ou triturá-las";
	mes "para fazer Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Cancelar.")) {
	case 1:
		mes "[Meltz]";
		mes "Por favor digite a quantidade";
		mes "de maçãs que você deseja";
		mes "comprar. Cada Maçã custa 15 zenys";
		mes "e você pode comprar um máximo";
		mes "de 500 de uma vez. Por favor digite";
		mes "'0' para cancelar seu pedido.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Obrigado por parar";
				mes "na minha loja. Adeus!";
				mes "Venha a qualquer hora quando";
				mes "você sentir vontade de ter uma";
				mes "Maçã para fazer um lanche ~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "Você digitou um número";
				mes "maior do que o valor máximo";
				mes "de 500. Por favor digite";
				mes "novamente o número de maçãs";
				mes "que você deseja comprar.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "que irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Meltz]";
					mes "Obrigado por parar";
					mes "na minha loja. Adeus!";
					mes "Venha a qualquer hora quando";
					mes "você sentir vontade de ter uma";
					mes "Maçã para fazer um lanche ~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Sinto muito, você não tem";
			mes "dinheiro suficiente com você.";
			mes "Por favor, verifique seus fundos ou";
			mes "compre menos maçãs.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, acho que você não tem";
			mes "espaço suficiente para transportar";
			mes "estas muitas maçãs. Você pode";
			mes "liberar espaço em seu inventário";
			close;
		}
		else {
			Zeny -= .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Obrigado pela visita";
			mes "na minha loja. Espero que você goste";
			mes "do aroma dessas Maçãs ~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Tudo bem, precisarei de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você";
		mes "Gostaria de continuar?";
		next;
		switch (select("SIm:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "Me desculpe,mas você não possui";
				mes "os materiais suficientes para";
				mes "criar uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de 3 Maçãs";
				mes "e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Thank you, please wait.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Maçã
				mes "[Meltz]";
				mes "Aqui está~";
				mes "Por favor, venha novamente.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Obrigado por parar";
			mes "na minha loja. Adeus!";
			mes "Venha a qualquer hora quando";
			mes "você sentir vontade de ter uma";
			mes "Maçã para fazer um lanche ~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Obrigado por parar";
		mes "na minha loja. Adeus!";
		mes "Venha a qualquer hora quando";
		mes "você sentir vontade de ter uma";
		mes "Maçã para fazer um lanche ~";
		close;
	}
}

airplane_01,221,158,2	script	Piloto#airplane_01	4_M_EIN_SOLDIER,{
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Longitude, 131 graus leste.";
		mes "Latitude, 37 graus norte.";
		mes "Estamos no curso certo, capitão.";
		close;
	case 2:
		mes "[Piloto]";
		mes "Parece ser realmente um";
		mes "dia nublado. Sempre é difícil";
		mes "navegar quando os céus";
		mes "não estão limpos. Acho que nós vamos";
		mes "precisar amplificar o radar.";
		close;
	case 3:
		mes "[Piloto]";
		mes "O Capitão é um bom";
		mes "homem e eu não consigo pensar em";
		mes "uma pessoa mais fina para comandar";
		mes "este Aeroplano. Ainda assim, ele é muito";
		mes "duro, um verdadeiro motorista servo.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Ei...!^000000";
		mes "^ff0000Menos bate-papo^000000";
		mes "^ff0000e mais pilotagem!^000000";
		next;
		mes "[Piloto]";
		mes "C-certo, senhor!";
		mes "(Viu o que eu quero dizer?)";
		close;
	default:
		mes "[Piloto]";
		mes "Este uniforme é";
		mes "bem elegante, mas";
		mes "é muito chamativo para";
		mes "usar pelo Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Realmente ninguém";
		mes "nunca entra neste quarto.";
		mes "E o capitão é uma rena.";
		mes "Eu podia tirar a minha cueca.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Oh...! Olá!";
		mes "E-e-está aproveitando o vôo?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	1_F_MARIA,2,2,{
	mes "[Dianne]";
	mes "É tão estranho!";
	mes "Eu venho visitar";
	mes "o Capitão do Aeroplano";
	mes "e tudo que vejo é esse";
	mes "alce estranho. Oh!";
	mes "Não esperava por essa...";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	1_M_JOBTESTER,{
	mes "[Mendel]";
	mes "Como eu esperava, as";
	mes "comidas do vôo são";
	mes "de qualidade 3 estrelas no melhor.";
	mes "*Harrrumph* Eu realmente deveria";
	mes "ter trazido meu cozinheiro";
	mes "Eu desfrutaria de uma verdadeira refeição.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	1_M_MOC_LORD,{
	mes "[Espadachim Shimizu]";
	mes "Finalmente, depois de cinco";
	mes "anos esperando...";
	mes "Terei minha vingança!";
	next;
	mes "[Espadachim Shimizu]";
	mes "Eu só...";
	mes "Tenho que me certificar";
	mes "de que não perderei minha";
	mes "parada. Mas logo, muito breve,";
	mes "terei minha vingança.";
	close;
}

//============================================================
//= Desafio de Escrita
//============================================================
airplane_01,32,61,4	script	Nils#ein	1_M_03,1,1,{
	mes "[Nils]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao";
	mes "^ff0000Desafio de Escrita RO^000000.";
	mes "Você gostaria de jogar";
	mes "um rápido jogo de escrita?";
	next;
	switch (select("Jogar ^ff0000Desafio de Escrita RO^000000:Informações:Ver recordes:Cancelar")) {
	case 1:
		mes "[Nils]";
		mes "Ok, nós temos";
		mes "um novo desafiante!";
		mes "Digite o seguinte";
		mes "texto o mais rápido";
		mes "que puder sem cometer";
		mes "nenhum erro! Vamos começar~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcEu sou o rei dos esquisitos! Agora^000000",
					"^3cbcbcVocê não me deu escolha. Eu aposto que^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcvocê conhece meu verdadeiro poder. Obey~!^000000",
					"^3cbcbchora de revelar meu segredo...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcDEUS-POING. EU NUNCA PERCO!^000000",
					"",
					"",
					"^3cbcbcCOBO LIGADO^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"Eu sou o rei dos esquisitos! Agora você conhece meu verdadeiro poder. Obey~!",
					"Você não me deu escolha. Eu aposto que é hora de revelar meu poder...";
		setarray .@line2_1$[0],	"^3cbcbciMpertuBáVEl AmÁVel SeCreTo E^000000",
					"^ff1493VeLoCiDaDe Da LuZ ViRaR a EsQuErDa^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493acreditar na sorte do amor suar careta correr^000000",
					"^800080abriR, Abrir!abr1r.abrirAbrirAbr1rAbRir4brir^000000",
					"^3cbcbclIberDadE êxtAse JoRnAlIsMo ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboLiChe cOnTrA O kArmA JoVem^000000",
					"^ff1493CeRtO QuEiMe VeRdAdEiRo GaNgSteR bEijo^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`abrir'4Brir abrir? abrir!111ABRAMESMO^000000",
					"^3cbcbcDesCobrir dOr de CaBeça RaiosLunArES jUsTiçA^000000";
		setarray .@word2$[0],	"iMpertuBáVEl AmÁvEl SeGrEdOs E boLiCHE cONtra o kArmA dos JoVeMs",
					"VeLoCiDaDe Da LuZ ViRaR a EsQuErDa QuEiMe VeRdAdEiRo GaNgSteR bEijo",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"acreditar na sorte do amor suar careta correr conto-de-fadas rodimus optimus bumblebee",
					"abriR, Abrir!abr1r.abrirAbrirAbr1rAbRir4brir`abrir'4Brir abrir? abrir!111ABRAMESMO",
					"lIberDadE êxtAse JoRnAlIsMo AxIla DesCobrir dOr de CaBeça RaiosLunArES jUsTiçA";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		.@wordtest = rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		.@start_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		.@end_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		.@total_time = .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		.@start_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		.@end_time = gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		.@total_time = .@total_time + (.@start_time - .@end_time);
		.@tasoo = (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Seu recorde é ^ff0000" + .@total_time + " segundos^000000 e";
			mes "o total de letras são " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Humm, não é possível";
				mes "humanamente falando, a menos que";
				mes "copie e cole toda a";
				mes "sentença. Por favor, jogue novamente.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "O recorde anterior foi";
				mes "feito por ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "com o total de ^0000ff" + $050320_ein_typing + "^000000 letras.";
				mes "Porém, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "você o novo top record";
				mes "desta vez. Parabéns!";
				$050320_minus1_typing$ = strcharinfo(0);
				$050320_ein_typing = .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "esse é o recorde";
				mes "atual conquistado com";
				mes "um total de ^0000ff" + $050320_ein_typing + "^000000";
				mes "caracteres. Tente bater";
				mes "esse record na próxima vez~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "Me desculpe, mas";
			mes "você digitou o";
			mes "texto incorreto...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "O ^ff0000Desafio de Escrita RO^000000";
		mes "é um jogo onde você deve digitar";
		mes "o texto exibido o mais rápido que você";
		mes "puder. O nome do jogador que possuir o";
		mes "recorde será gravado nos registros. Se você";
		mes "procura fama, essa é sua chance!";
		next;
		mes "[Nils]";
		mes "Eu apenas gostaria de";
		mes "dizer que você deve digitar";
		mes "todo o texto em uma simples";
		mes "linha que será apresentada a você.";
		mes "Então não pressione uma tecla, apenas clique em 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "esse é o recorde";
		mes "atual conquistado com";
		mes "um total de ^0000ff" + $050320_ein_typing + "^000000";
		mes "caracteres. Tente bater";
		mes "esse record na próxima vez~";
		close;
	case 4:
		mes "[Nils]";
		mes "Sinta-se livre para voltar quando quiser e jogar o";
		mes "^ff0000Desafio de Escrita RO^000000";
		mes "Até logo~";
		close;
	}
}

//============================================================
//= Jogo da Maçã
//============================================================
airplane_01,33,68,4	script	Clarice	1_F_MERCHANT_02,{
	mes "[Clarice]";
	mes "Oi, eu sou Clarice~";
	mes "Você gostaria de";
	mes "apostar algumas maçãs";
	mes "no amigável jogo dos Dados?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{

	switch (select("Jogar:Regras do Jogo:Cancelar")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "Eu estou pronta para jogar o jogo dos";
		mes "dados quando você quiser.";
		mes "Quando quiser jogar basta falar comigo, Ok?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "As regras do Jogo dos Dados";
		mes "são bastante simples. Primeiro, você";
		mes "deve saber que as apostas são Maçãs.";
		mes "Você pode apostar no máximo 50";
		mes "Maçãs por vez. Para que tudo ocorra";
		mes "bem, eu aceito apenas Maçãs.";
		next;
		mes "["+getarg(0)+"]";
		mes "Mas ei, se você possuir tantos zenys";
		mes "que já não cabem mais em seus bolsos,";
		mes "tire-os e você pode comprar quantas";
		mes "maçãs quiser no Vendedor de Maçãs~";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, nós iremos começar rolando";
		mes "dois dados de 6 lados.";
		mes "Enquanto for seu turno, você poderá";
		mes "rolar dois dados de 6 lados. Após";
		mes "isso, nós teremos a opção";
		mes "de rolar um terceiro dado.";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora aqui é a parte importante.";
		mes "Se ao total nos dados você atingir 12,";
		mes "você irá a falência";
		mes "e irá perder. Caso contrário, a";
		mes "a pessoa com a maior pontuação";
		mes "irá vencer. O que acha?";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, primeiro você deve";
		mes "decidir se quer jogar o";
		mes "terceiro dado ou não. Então,";
		mes "dependendo do seu resultado, eu irei";
		mes "rolar meu terceiro dado... Ou talvez não.";
		next;
		mes "["+getarg(0)+"]";
		mes "Enquanto você ganhar, você irá";
		mes "receber o dobro de Maçãs";
		mes "que você apostou.";
		mes "Mas se por acaso você empatar, você irá";
		mes "receber de volta apenas as Maçãs";
		mes "que você apostou. Justo, certo?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ah, Então você quer jogar comigo?";
	mes "Ótimo! Quantas Maçãs";
	mes "você deseja apostar?";
	mes "Lembre-se, seu limite são 50 Maçãs.";
	mes "Se você deseja cancelar, por favor digite '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "Mudou de idéia?";
			mes "Eu entendo. Bem então,";
			mes "espero que possamos jogar depois.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "Você não pode apostar mais do que";
			mes "50 Maçãs. Lembre-se,";
			mes "precisamos manter as apostas";
			mes "razoáveis. Por favor digite";
			mes "um valor não superior a 50.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "Então você estará";
		mes "apostando ^FF0000"+.@amount+"^000000 Maçãs.";
		mes "Certo?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+getarg(0)+"]";
			mes "Hum, cometi um erro?";
			mes "Certo, por favor digite";
			mes "o número de Maçãs que";
			mes "você deseja apostar.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "Me desculpe, mas você";
			mes "não parecem ter";
			mes "Maçãs suficientes para essa";
			mes "aposta... Você não pode apostar";
			mes "se não tem a aposta, você sabe.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "Bom!";
		mes "Agora podemos começar";
		mes "este jogo! Irei rolar primeiro~";
		break;
	}
	mes "^3355FF*Rolando e caindo*^000000";
	next;
	.@giveapple = .@amount*2;
	.@table1 = rand(1,6);
	.@table2 = rand(1,6);
	.@tablesub = .@table1 + .@table2;
	.@tabletotal = .@tablesub;
	mes "["+getarg(0)+"]";
	mes "Eu tenho um ^0000FF" + .@table1 + "^000000 e um ^0000FF" + .@table2 + "^000000.";
	mes "Isso é um total de ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, agora é sua vez.";
	next;
	select("Lançar Dado.");
	mes "^3355FF*Rolando e caindo*^000000";
	.@player1 = rand(1,6);
	.@player2 = rand(1,6);
	.@playersub = .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		.@player1 = rand(1,6);
		.@player2 = rand(1,6);
		.@playersub = .@player1 + .@player2;
	}
	.@playertotal = .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, você tem ^FF0000" + .@player1 + "^000000 e ^FF0000" + .@player2 + "^000000. O total é ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. Estamos empatados, gostaria de lançar o dado novamente?";
	} else if (.@playersub > .@tablesub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, atualmente você está ganhando o jogo, gostaria de lançar o dado novamente?";
	} else if(.@tablesub > .@playersub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. Estou vencendo o jogo. Gostaria de lançar o dado novamente?";
	}
	next;
	switch (select("Lançar Dado.:Cancelar.")) {
	case 1:
		mes "^3355FF*Rolando e caindo*^000000";
		.@player3 = rand(1,6);
		.@playertotal += .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Você perdeu o jogo. Me desculpe mas, por favor tente novamente.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Mesmo que você lance o dado novamente, seu total ainda será menor do que o meu.  Você perdeu o jogo. Sinto muito e por favor tente novamente.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Eu não quero correr nenhum risco, vamos acabar este jogo num empate. Vamos jogar de novo outra hora~";
				close2;
				getitem 512,.@amount; //Maçã
				end;
			}
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Agora é minha vez.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "Entendo, você não quer correr risco de perder o jogo. Ok, deixe-me lançar os dados novamente.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "Entendo, você não quer correr risco de perder este jogo. Nem eu, vamos acabar este jogo em um empate. Vamos jogar novamente outra hora~";
				close2;
				getitem 512,.@amount; //Maçã
				end;
			}
			mes "Certo.";
			mes "Vamos rolar os dados novamente.";
		}
		else {
			mes "Não faria mal tentar.";
			mes "Bem, eu venci dessa vez.";
			mes "Sinto muito, vamos tentar jogar";
			mes "novamente algum dia.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolando e caindo*^000000";
	.@table3 = rand(1,6);
	.@tabletotal += .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Perdi o jogo por que meu total excedeu 12. Deixe-me dar-lhe minhas maçãs. Parabéns, foi um grande jogo.";
		close2;
		getitem 512,.@giveapple; //Maçã
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 você ganhou este jogo, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe minhas maçãs. Foi um grande jogo e espero que possamos jogar novamente em outro momento.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 este jogo acabou em empate, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe suas maçãs de volta. Foi um grande jogo e espero que possamos jogar novamente em outro momento.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 você perdeu o jogo, ^FF0000" + strcharinfo(0) + "^000000. Lamento, mas por favor, tente novamente.";
		close;
	}
}
