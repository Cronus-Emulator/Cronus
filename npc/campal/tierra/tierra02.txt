//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Nome do NPC:                                                ||
// - tierra02.txt                                                ||
//===============================================================||
// = Criação:                                                    ||
// - eA/Herc/Cronus                                              ||
//===============================================================||
// = Versão:                                                     ||
// - 1.0                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Cabeçalho [SlexFire]                                        ||
// - Tradução [Revisões Cronus] / [SlexFire]                     ||
//===============================================================||
// = Descrição:                                                  ||
// - Batalha Campal Tierra (02)                                  ||
//===============================================================||
// = Observações:                                                ||
// - Equipe vencedora - 3 emblemas                               ||
// - Equipe perdedora - 1 emblema                                ||
//===============================================================//

// NPCs da Sala de Espera
//============================================================
bat_room,114,227,5	script	Tenente Rundel	4_M_KY_KNT,{
	end;

OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_a02::OnReadyCheck",1,0,80;
	end;

OnEnterBG:
	$@TierraBG2_id1 = waitingroom2bg("bat_a02",50,374,"start#bat_a02::OnGuillaumeQuit","");
	end;
}

bat_room,114,204,1	script	Tenente Guerrit	4_M_CRU_KNT,{
	end;

OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_a02::OnReadyCheck",1,0,80;
	end;

OnEnterBG:
	$@TierraBG2_id2 = waitingroom2bg("bat_a02",42,16,"start#bat_a02::OnCroixQuit","");
	end;
}

bat_room,1,151,3	script	#bat_a02_timer	CLEAR_NPC,{
	end;

OnInit:
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	initnpctimer;
	.@chk_bat_a02 = getmapusers("bat_a02");
	if (.@chk_bat_a02 < 1) {
		$@TierraBG2 = 0; $@TierraBG2_Victory = 0;
		if( $@TierraBG2_id1 ) { bg_destroy $@TierraBG2_id1; $@TierraBG2_id1 = 0; }
		if( $@TierraBG2_id2 ) { bg_destroy $@TierraBG2_id2; $@TierraBG2_id2 = 0; }
		donpcevent "start#bat_a02::OnReadyCheck";
	}
	end;
}

bat_a02,352,342,0	script	#bat_a02_quest_a	-1,5,5,{
	end;

OnTouch:
	if (!questprogress(2069))
		setquest 2069;
	end;
}

bat_a02,353,52,0	script	#bat_a02_quest_b	-1,5,5,{
	end;

OnTouch:
	if (!questprogress(2069))
		setquest 2069;
	end;
}

bat_room,113,220,0	warp	back_bgrooma02a	1,1,bat_room,154,150
bat_room,113,211,0	warp	back_bgrooma02b	1,1,bat_room,154,150

// Engine do Campo de Batalha do Desfiladeiro Tierra
//============================================================
bat_a02,15,15,3	script	start#bat_a02	CLEAR_NPC,{
OnInit:
	mapwarp "bat_a02","bat_room",154,150;
	end;

OnEnable:
	donpcevent "OBJ#bat_a02_a::OnKill";
	donpcevent "OBJ#bat_a02_a::OnEnable";
	donpcevent "OBJ#bat_a02_b::OnKill";
	donpcevent "OBJ#bat_a02_b::OnEnable";
	donpcevent "barricade#bat_a02_a::OnKill";
	donpcevent "barricade#bat_a02_a::OnEnable";
	donpcevent "barricade#bat_a02_b::OnKill";
	donpcevent "barricade#bat_a02_b::OnEnable";
	donpcevent "OBJ#bat_a02_n::OnKill";
	donpcevent "OBJ#bat_a02_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	donpcevent "Terapeuta de Batalha#a02_a::OnEnable";
	donpcevent "Terapeuta de Batalha#a02_b::OnEnable";
	donpcevent "countdown#bat_a02::OnEnable";
	disablenpc "Ferreiro Guillaume#a02";
	disablenpc "Ferreiro Croix#bat_a02";
	disablenpc "Guillaume Vintenar#a02_a";
	disablenpc "Croix Vintenar#a02_b";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	bg_leave;
	end;

OnReadyCheck:
	if( $@TierraBG2 )
		end;
	.@Guillaume = getwaitingroomstate(0,"Tenente Rundel");
	.@Croix = getwaitingroomstate(0,"Tenente Guerrit");
	if( !.@Guillaume && !.@Croix ) {
		donpcevent "#bat_a02_timer::OnStop";
		end;
	}
	else if( .@Guillaume < 10 || .@Croix < 10 )
		end;
	$@TierraBG2 = 1;
	donpcevent "Tenente Rundel::OnEnterBG";
	donpcevent "Tenente Guerrit::OnEnterBG";
	donpcevent "start#bat_a02::OnEnable";
	bg_warp $@TierraBG2_id1,"bat_a02",352,342;
	bg_warp $@TierraBG2_id2,"bat_a02",353,52;
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_a02_timer::OnEnable";
	end;
}

bat_a02,15,16,3	script	OBJ#bat_a02_a	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG2_id1,"bat_a02",177,345,"Suprimentos",1909,"OBJ#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_a::OnMyMobDead") < 1) {
		donpcevent "Terapeuta de Batalha#a02_a::OnStop";
		donpcevent "Terapeuta de Batalha#a02_b::OnStop";
		$@TierraBG2_Victory = 2;
		enablenpc "Guillaume Vintenar#a02_a";
		enablenpc "Croix Vintenar#a02_b";
		mapannounce "bat_a02", "Croix Vintenar Swandery: Nós destruímos os Suprimentos dos Guillaumes. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG2_id1,"bat_a02",50,374;
		bg_warp $@TierraBG2_id2,"bat_a02",42,16;
	}
	end;
}

bat_a02,15,17,3	script	OBJ#bat_a02_b	CLEAR_NPC,{
OnEnable:
	bg_monster $@TierraBG2_id2,"bat_a02",167,50,"Suprimentos",1910,"OBJ#bat_a02_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_b::OnMyMobDead") < 1) {
		donpcevent "Terapeuta de Batalha#a02_a::OnStop";
		donpcevent "Terapeuta de Batalha#a02_b::OnStop";
		$@TierraBG2_Victory = 1;
		enablenpc "Guillaume Vintenar#a02_a";
		enablenpc "Croix Vintenar#a02_b";
		mapannounce "bat_a02", "Guillaume Vintenar Axl Rose: Nós destruímos os Suprimentos dos Croixs. Nós vencemos esta! Wow!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG2_id1,"bat_a02",50,374;
		bg_warp $@TierraBG2_id2,"bat_a02",42,16;
	}
	end;
}

bat_a02,15,18,3	script	barricade#bat_a02_a	CLEAR_NPC,{
OnEnable:
	for (.@i = 185; .@i < 202; ++.@i)
		bg_monster $@TierraBG2_id1,"bat_a02",.@i,266,"Barricada",1906,"barricade#bat_a02_a::OnMyMobDead";
	setwall "bat_a02",186,266,16,6,1,"bat_a02_c1";
	end;

OnKill:
	killmonster "bat_a02","barricade#bat_a02_a::OnMyMobDead";
	delwall "bat_a02_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","barricade#bat_a02_a::OnMyMobDead") < 17) {
		killmonster "bat_a02","barricade#bat_a02_a::OnMyMobDead";
		delwall "bat_a02_c1";
		enablenpc "Ferreiro Guillaume#a02";
		mapannounce "bat_a02", "Guillaume Vintenar Axl Rose: A Barricada do vale foi destruída! Onde está o nosso Ferreiro Guillaume? Nós precisamos reconstruir a Barricada!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,19,3	script	barricade#bat_a02_b	CLEAR_NPC,{
OnEnable:
	for (.@i = 169; .@i < 186; ++.@i)
		bg_monster $@TierraBG2_id2,"bat_a02",.@i,129,"Barricada",1906,"barricade#bat_a02_b::OnMyMobDead";
	setwall "bat_a02",170,129,16,6,1,"bat_a02_g1";
	end;

OnKill:
	killmonster "bat_a02","barricade#bat_a02_b::OnMyMobDead";
	delwall "bat_a02_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","barricade#bat_a02_b::OnMyMobDead") < 17) {
		killmonster "bat_a02","barricade#bat_a02_b::OnMyMobDead";
		delwall "bat_a02_g1";
		enablenpc "Ferreiro Croix#bat_a02";
		mapannounce "bat_a02", "Croix Vintenar Swandery: A Barricada do vale foi destruída! Onde está o nosso Ferreiro Croix? Nós precisamos reconstruir a Barricada!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,20,3	script	OBJ#bat_a02_n	CLEAR_NPC,{
OnEnable:
	monster "bat_a02",273,203,"Bandeira Neutra",1911,1,"OBJ#bat_a02_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_n::OnMyMobDead") < 17) {
		bg_team_setxy getcharid(4),56,212;
		if (getcharid(4) == $@TierraBG2_id1) {
			donpcevent "NOBJ_mob#bat_a02_a::OnEnable";
			mapannounce "bat_a02", "O time Guillaume capturou a Bandeira Neutra, então eles estão em vantagem.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a02_b::OnEnable";
			mapannounce "bat_a02", "O time Croix capturou a Bandeira Neutra, então eles estão em vantagem.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,21,3	script	NOBJ_mob#bat_a02_a	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	bg_monster $@TierraBG2_id1,"bat_a02",272,204,"Guardião do Exército Guillaume",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",272,213,"Guardião do Exército Guillaume",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",273,197,"Guardião do Exército Guillaume",1950,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,15,22,3	script	NOBJ_mob#bat_a02_b	CLEAR_NPC,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	bg_monster $@TierraBG2_id2,"bat_a02",272,204,"Guardião do Exército Croix",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",272,213,"Guardião do Exército Croix",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",273,197,"Guardião do Exército Croix",1950,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,185,270,1	script	Ferreiro Guillaume#a02	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG2_id1) {
		mes "[Ferreiro Guillaume]";
		mes "Nós estamos em uma emergência! A Barricada foi destruída!";
		mes "Nós podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "E nós temos tudo isso, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro Guillaume]";
				mes "Você trouxe pedras o suficiente! Vamos então começar a reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro Guillaume]";
				mes "Uau! Está feito.";
				mes "Estamos mais aliviados agora.";
				delitem 7049,50; //Stone
				donpcevent "barricade#bat_a02_a::OnEnable";
				close2;
				disablenpc "Ferreiro Guillaume#a02";
				end;
			}
			else {
				mes "[Ferreiro Guillaume]";
				mes "Você não possui pedras o suficiente!";
				next;
				mes "[Ferreiro Guillaume]";
				mes "^3131FFNós precisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então vá mais depressa.";
				close;
			}
		case 2:
			mes "[Ferreiro Guillaume]";
			mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro Guillaume]";
		mes "Lá, o inimigo está chegando!";
		close;
	}

OnInit:
	disablenpc "Ferreiro Guillaume#a02";
	end;
}

bat_a02,170,121,5	script	Ferreiro Croix#bat_a02	4_M_REPAIR,{
	if (getcharid(4) == $@TierraBG2_id2) {
		mes "[Ferreiro Croix]";
		mes "Nós estamos em uma emergência! A Barricada foi destruída!";
		mes "Nós podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "E nós temos tudo isso, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro Croix]";
				mes "Você trouxe pedras o suficiente! Vamos então começar a reparar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal.";
				next;
				mes "[Ferreiro Croix]";
				mes "Combinando as Pedras e o Ouro na porcentagem adequada e afiando a Barricada, e então adicionando os Minérios de Elunium para deixá-la mais forte. Agora decorando com os Fragmentos de Metal, e trançando as pedras com os Nervos de Urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro Croix]";
				mes "Uau! Está feito.";
				mes "Estamos mais aliviados agora.";
				delitem 7049,50; //Stone
				donpcevent "barricade#bat_a02_b::OnEnable";
				close2;
				disablenpc "Ferreiro Croix#bat_a02";
				end;
			}
			else {
				mes "[Ferreiro Croix]";
				mes "Você não possui pedras o suficiente!";
				next;
				mes "[Ferreiro Croix]";
				mes "^3131FFNós precisamos de 50 Pedras.^000000";
				mes "Estamos ocupados, então vá mais depressa.";
				close;
			}
		case 2:
			mes "[Ferreiro Croix]";
			mes "Os inimigos estão chegando! Vamos evacuar e partir daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro Croix]";
		mes "Lá, o inimigo está chegando";
		close;
	}

OnInit:
	disablenpc "Ferreiro Croix#bat_a02";
	end;
}

bat_a02,53,377,3	script	Terapeuta de Batalha#a02_a	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta de Batalha]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta de Batalha#a02_a";
	end;

OnStop:
	disablenpc "bat_a02_rp1_a_warp";
	disablenpc "Terapeuta de Batalha#a02_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a02_rp1_a_warp";
	end;

OnTimer26000:
	disablenpc "bat_a02_rp1_a_warp";
	end;

OnTimer26500:
	donpcevent "Terapeuta de Batalha#a02_a::OnEnable";
	end;
}

bat_a02,51,375,0	script	bat_a02_rp1_a_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_a_warp";
	end;

OnTouch_:
	percentheal 100,100;
	warp "bat_a02",352,342;
	end;
}

bat_a02,45,19,3	script	Terapeuta de Batalha#a02_b	4_F_SISTER,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta de Batalha]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta de Batalha#a02_b";
	end;

OnStop:
	disablenpc "bat_a02_rp1_b_warp";
	disablenpc "Terapeuta de Batalha#a02_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a02_rp1_b_warp";
	end;

OnTimer26000:
	disablenpc "bat_a02_rp1_b_warp";
	end;

OnTimer26500:
	donpcevent "Terapeuta de Batalha#a02_b::OnEnable";
	end;
}

bat_a02,43,17,0	script	bat_a02_rp1_b_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_b_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a02",353,52;
	end;
}

bat_a02,60,216,3	script	Fantasma do Vale#bat_a02_n	4_GHOSTRING,{
	specialeffect2 EF_HEAL;
	mes "[Fantasma do Vale]";
	mes "Boo...Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	enablenpc "bat_a02_rp1_n_warp";
	end;

OnTimer26000:
	disablenpc "bat_a02_rp1_n_warp";
	end;

OnTimer26500:
	donpcevent "Fantasma do Vale#bat_a02_n::OnEnable";
	end;
}

bat_a02,55,213,0	script	bat_a02_rp1_n_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a02",301,209;
	end;
}

bat_a02,194,267,0	script	barri_warp_up#bat_a02_a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,261;
	end;
}

bat_a02,194,265,0	script	barri_warp_down#bat_a02a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,270;
	end;
}

bat_a02,177,130,0	script	barri_warp_up#bat_a02_b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,125;
	end;
}

bat_a02,177,128,0	script	barri_warp_down#bat_a02b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,134;
	end;
}

bat_a02,352,342,0	script	A_CODE#bat_a02	-1,5,5,{
OnTouch:
	if (questprogress(2069) == 1)
		setquest 2069;
	end;
}

bat_a02,353,52,0	script	B_CODE#bat_a02	-1,5,5,{
OnTouch:
	if (questprogress(2069) == 1)
		setquest 2069;
	end;
}

bat_a02,169,227,0	script	underladd#bat_a02_1	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a02",178,228;
	end;
}

bat_a02,208,164,0	script	underladd#bat_a02_2	WARPNPC,1,1,{
OnTouch_:
	warp "bat_a02",200,171;
	end;
}

bat_a02,171,309,3	script	Acampamento Guillaume#flag11	1_FLAG_LION,{ end; }
bat_a02,149,310,3	script	Acampamento Guillaume#flag12	1_FLAG_LION,{ end; }
bat_a02,119,336,3	script	Acampamento Guillaume#flag13	1_FLAG_LION,{ end; }
bat_a02,118,357,3	script	Acampamento Guillaume#flag14	1_FLAG_LION,{ end; }
bat_a02,150,380,3	script	Acampamento Guillaume#flag15	1_FLAG_LION,{ end; }
bat_a02,173,380,3	script	Acampamento Guillaume#flag16	1_FLAG_LION,{ end; }
bat_a02,210,344,3	script	Acampamento Guillaume#flag17	1_FLAG_LION,{ end; }
bat_a02,350,325,3	script	Acampamento Guillaume#flag18	1_FLAG_LION,{ end; }
bat_a02,358,325,3	script	Acampamento Guillaume#flag19	1_FLAG_LION,{ end; }

bat_a02,138,12,3	script	Acampamento Croix#flag11	1_FLAG_EAGLE,{ end; }
bat_a02,108,36,3	script	Acampamento Croix#flag12	1_FLAG_EAGLE,{ end; }
bat_a02,108,63,3	script	Acampamento Croix#flag13	1_FLAG_EAGLE,{ end; }
bat_a02,136,87,3	script	Acampamento Croix#flag14	1_FLAG_EAGLE,{ end; }
bat_a02,167,86,3	script	Acampamento Croix#flag15	1_FLAG_EAGLE,{ end; }
bat_a02,199,49,3	script	Acampamento Croix#flag16	1_FLAG_EAGLE,{ end; }
bat_a02,168,16,3	script	Acampamento Croix#flag17	1_FLAG_EAGLE,{ end; }
bat_a02,357,74,3	script	Acampamento Croix#flag18	1_FLAG_EAGLE,{ end; }
bat_a02,348,74,3	script	Acampamento Croix#flag19	1_FLAG_EAGLE,{ end; }

bat_a02,53,377,3	script	Guillaume Vintenar#a02_a	4_M_KY_HEAD,{
	if (getcharid(4) == $@TierraBG2_id1) {
		if ($@TierraBG2_Victory == 1) {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Abençoado Guillaume!";
				mes "Vamos comemorar o nosso glorioso triunfo!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Abençoado Guillaume!";
				mes "Vamos comemorar o nosso glorioso triunfo!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Axl Rose]";
				mes "Você foi derrotado, porém você se dedicou a esta batalha.";
				mes "Este é o prêmio por você ter se dedicado ao grande Guillaume Marollo!";
				mes "Tome esta derrota somente como lição, na próxima vez você irá se sair vitorioso com certeza.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Você foi derrotado, porém você se dedicou a esta batalha.";
				mes "Este é o prêmio por você ter se dedicado ao grande Guillaume Marollo!";
				mes "Tome esta derrota somente como lição, na próxima vez você irá se sair vitorioso com certeza.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	else {
		if ($@TierraBG2_Victory == 2) {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Axl Rose]";
				mes "Você foi derrotado, porém você se dedicou a esta batalha.";
				mes "Este é o prêmio por você ter se dedicado ao grande Guillaume Marollo!";
				mes "Tome esta derrota somente como lição, na próxima vez você irá se sair vitorioso com certeza.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Você foi derrotado, porém você se dedicou a esta batalha.";
				mes "Este é o prêmio por você ter se dedicado ao grande Guillaume Marollo!";
				mes "Tome esta derrota somente como lição, na próxima vez você irá se sair vitorioso com certeza.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Axl Rose]";
				mes "Abençoado Guillaume!";
				mes "Vamos comemorar o nosso glorioso triunfo!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Axl Rose]";
				mes "Abençoado Guillaume!";
				mes "Vamos comemorar o nosso glorioso triunfo!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Guillaume Vintenar#a02_a";
	end;
}

bat_a02,45,19,3	script	Croix Vintenar#a02_b	4_M_CRU_HEAD,{
	if (getcharid(4) == $@TierraBG2_id2) {
		if ($@TierraBG2_Victory == 2) {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Abençoado Croix!";
				mes "Vamos comemorar a nossa gloriosa vitória!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Abençoado Croix!";
				mes "Vamos comemorar a nossa gloriosa vitória!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Swandery]";
				mes "Oh, "+strcharinfo(0)+". Não fique triste.";
				mes "Mesmo que não tenhamos vencido, nós demos nosso melhor.";
				mes "Este é o majestoso presente do nosso Croix e por favor, não esqueça esta batalha. Nós ganharemos com toda certeza a próxima batalha.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Oh, "+strcharinfo(0)+". Não fique triste.";
				mes "Mesmo que não tenhamos vencido, nós demos nosso melhor.";
				mes "Este é o majestoso presente do nosso Croix e por favor, não esqueça esta batalha. Nós ganharemos com toda certeza a próxima batalha.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	else {
		if ($@TierraBG2_Victory == 1) {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 0) {
				mes "[Swandery]";
				mes "Oh, "+strcharinfo(0)+". Não fique triste.";
				mes "Mesmo que não tenhamos vencido, nós demos nosso melhor.";
				mes "Este é o majestoso presente do nosso Croix e por favor, não esqueça esta batalha. Nós ganharemos com toda certeza a próxima batalha.";
				close2;
				getitem 7828,1; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Oh, "+strcharinfo(0)+". Não fique triste.";
				mes "Mesmo que não tenhamos vencido, nós demos nosso melhor.";
				mes "Este é o majestoso presente do nosso Croix e por favor, não esqueça esta batalha. Nós ganharemos com toda certeza a próxima batalha.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
		else {
			.@your_medal = countitem(7828);
			.@medal_gap = 500 - .@your_medal;
			if (.@medal_gap > 2) {
				mes "[Swandery]";
				mes "Abençoado Croix!";
				mes "Vamos comemorar a nossa gloriosa vitória!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,3; //BF_Badge1
			}
			else {
				mes "[Swandery]";
				mes "Abençoado Croix!";
				mes "Vamos comemorar a nossa gloriosa vitória!";
				mes strcharinfo(0)+", isto é um sinal que reflete a nossa vitória.";
				close2;
				getitem 7828,.@medal_gap; //BF_Badge1
			}
		}
	}
	bg_leave;
	warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Croix Vintenar#a02_b";
	end;
}

bat_a02,1,5,3	script	countdown#bat_a02	CLEAR_NPC,{
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a02", "Guillaume Vintenar Axl Rose: Vamos atacar e incinerar todo os Suprimentos do Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a02", "Croix Vintenar Swandery: Mestres de Valhalla! Presenteie-nos com coragem e uma fé infalível!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a02", "Marollo VII: Guillaume Marollo, Croix Marollo! E todos os seus seguidores!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a02", "Marollo VII: Ambos exércitos são muito competitivos, então nenhum acampamento deverá ser destruído facilmente. Isto significa que os Reinos de Marollo nunca serão derrotados!",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a02", "Marollo VII: Acho que seria melhor encerrarmos a batalha, e chamarmos isto de um empate. Porém...",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_a02", "Marollo VII: Mantenha a sua lealdade e fé por um momento, e vamos resolver a batalha pelo Desfiladeiro de Tierra.",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_a02", "Axl Rose, Swandery : Sim, senhor!",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	mapwarp "bat_a02","bat_room",154,150;
	donpcevent "countdown#bat_a02::OnStop";
	end;
}

/*
bat_a02,351,75,3	script	Croix Camp Soldier#bat_a02_guide	4_M_RASWORD,{
	mes "Carregando...";
	close;
}

bat_a02,356,326,3	script	Guillaume Camp Soldier#bat_a02_guide	4_M_RASWORD,{
	mes "Carregando...";
	close;
}
*/

bat_a02,1,1,3	script	Liberar todos#a02	4_DOG01,{
	.@i = callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Eu posso te ajudar?";
		next;
		switch(select("Libertar todos.:Cancelar.")) {
		case 1:
			mes "Adeus.";
			close2;
			mapwarp "bat_a02","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}
