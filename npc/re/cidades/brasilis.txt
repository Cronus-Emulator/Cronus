//===== Cronus Script ========================================
//= Cidade de Brasilis
//===== Por: =================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.4
//===== Descrição: ===========================================
//= [Official Conversion]
//= Brasilis Town Script
//===== Comentários Adicionais: ==============================
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//= 1.4 Adicionada tradução PT-BR. [Mihael]
//============================================================

// Teleportador de Brasilis
//============================================================
/* Coordenadas Pre-Re: alberta,247,115,3 */
alberta,246,82,3	script	Marinheiro#bra2	100,{
	mes "[Marinheiro]";
	mes "Ei, você já ouvi falar sobre um lugar chamado Brasilis?";
	mes "É uma cidade tropical, que é quente como o deserto, mas também chove. É um lugar muito misterioso.";
	next;
	mes "[Marinheiro]";
	mes "Recentemente, encontrei um caminho novo no oceano que leva até lá.";
	mes "O custo é apenas 10,000 zeny por uma viagem de ida e volta. Que tal?";
	next;
	switch(select("Vamos para Brasilis!!:Não fiz minhas malas.")) {
	case 1:
		if (Zeny > 9999) {
			mes "[Marinheiro]";
			mes "Ótimo~!! Vamos lá~!";
			set zeny,zeny-10000;
			close2;
			warp "brasilis",314,60;
			end;
		} else {
			mes "[Marinheiro]";
			mes "Eu disse 10,000 zeny.";
			close;
		}
	case 2:
		mes "[Marinheiro]";
		mes "Bem se você estiver interessado, fale comigo e eu poderei te levar lá.";
		close;
	}
}

brasilis,316,57,3	script	Marinheiro#bra1	100,{
	mes "[Marinheiro]";
	mes "Meu barco está indo para Alberta, quer se juntar a nós?";
	next;
	switch(select("Voltar para Alberta.:Ainda não~")) {
	case 1:
		mes "[Marinheiro]";
		mes "Tenho certeza que sente falta de casa.";
		close2;
		if (checkre(0))
				warp "alberta",243,82;
			else
				warp "alberta",244,115;
		end;
	case 2:
		mes "[Marinheiro]";
		mes "Ok, fique à vontade. Quando quiser partir avise.";
		close;
	}
}

brasilis,155,165,3	script	Orientação#bra1	858,{
	mes ":: Museu de Arte ::";
	close;
}

brasilis,195,231,3	script	Orientação#bra2	858,{
	mes ":: Monumento ::";
	close;
}

brasilis,240,247,3	script	Orientação#bra3	858,{
	mes ":: Mercado ::";
	mes " ";
	mes "- Para procurar Poções e Armamento -";
	close;
}

brasilis,303,309,3	script	Orientação#bra4	858,{
	mes ":: Floresta ::";
	mes "";
	mes "- Não é para os fracos de coração -";
	close;
}

brasilis,278,137,3	script	Orientação#bra5	858,{
	mes ":: Hotel de Brasilis ::";
	close;
}

brasilis,137,77,5	script	Sorveteiro	85,{
	mes "[Sorveteiro]";
	mes "Venham~venham~";
	mes "Sorvetes são perfeitos para um dia quente como hoje~";
	mes "E custam apenas ^3355FF100 Zeny^000000~";
	mes "Sorvetes~";
	mes "Pegue o seu sorvete!";
	next;
	switch(select("Me dê um!:Sorvete?:Cancelar.")) {
	case 1:
		mes "[Sorveteiro]";
		mes "Como muitas pessoas procuram por sorvetes você pode encomendar apenas 5 de cada vez.";
		mes "Então quantos você vai querer?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Sorveteiro]";
				mes "Nada?";
				mes "Então saia daqui, eu tenho muitos clientes para atender.";
				close;
			} else if ((.@input < 0) || (.@input > 5)) {
				mes "[Sorveteiro]";
				mes "Uau.";
				mes "Você pediu uma quantidade não permitida.";
				next;
			} else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Sorveteiro]";
			mes "Você não possui os zenys necessários.";
			mes "São apenas ^3355FF100 Zeny^000000~!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Sorveteiro]";
			mes "Você parece estar carregando muitas coisas.";
			mes "Reduza um pouco de seu peso antes.";
			close;
		}
		set zeny,zeny-.@icecream_hap;
		getitem 536,.@input; //Sorvete
		close;
	case 2:
		mes "[Sorveteiro]";
		mes "'Sorvetes são...'";
		mes "Espere, você não sabe";
		mes "o que é um sorvete?";
		mes "Como você está conseguindo";
		mes "viver nesse calor?";
		next;
		mes "[Sorveteiro]";
		mes "Isso parece estranho.";
		mes "De qualquer forma, aproveite enquanto os sorvetes estão bons e frios.";
		close;
	case 3:
		mes "[Sorveteiro]";
		mes "Não perca a chance de comer o melhor Sorvete de todo o mundo~!";
		close;
	}